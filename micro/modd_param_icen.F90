!MNH_LIC Copyright 1995-2019 CNRS, Meteo-France and Universite Paul Sabatier
!MNH_LIC This is part of the Meso-NH software governed by the CeCILL-C licence
!MNH_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt
!MNH_LIC for details. version 1.
!-----------------------------------------------------------------
!     ######spl
      MODULE MODD_PARAM_ICE_n
!     #####################
!> @file
!!      *MODD_PARAM_ICE_n* - declaration of the control parameters for the
!!                           mixed phase cloud parameterization
!!
!!    PURPOSE
!!    -------
!!      The purpose of this declarative module is to define the set of space
!!    and time control parameters for the microphysics.
!!
!!    IMPLICIT ARGUMENTS
!!    ------------------
!!      None
!!
!!    REFERENCE
!!    ---------
!!      Book2 of documentation of Meso-NH (module MODD_PARAM_ICE)
!!
!!    AUTHOR
!!    ------
!!     J.-P. Pinty   *Laboratoire d'Aerologie*
!!
!!    MODIFICATIONS
!!    -------------
!!
!!    -  Original      14/12/95
!!    -  Jan 2015 S. Riette: new ICE3/ICE4 parameters
!!    -  01/10/16 (C.Lac)  Add droplet deposition for fog
!!       A. Marcel Jan 2025: bi-Gaussian PDF and associated subgrid precipitation
!!
!-------------------------------------------------------------------------------
!
!*       0.   DECLARATIONS
!             ------------
!
USE MODD_PARAMETERS, ONLY: JPMODELMAX
IMPLICIT NONE
!
TYPE PARAM_ICE_t
LOGICAL :: LWARM       !< When .TRUE. activates the formation of rain by
                       !! the warm microphysical processes
LOGICAL :: LSEDIC      !< TRUE to enable the droplet sedimentation
LOGICAL :: LDEPOSC     !< TRUE to enable cloud droplet deposition
REAL    :: XVDEPOSC    !< Droplet deposition velocity
!
CHARACTER(LEN=4) :: CPRISTINE_ICE !< Pristine ice type PLAT, COLU or BURO
CHARACTER(LEN=4) :: CSEDIM        !< Sedimentation calculation mode
!
LOGICAL :: LRED       !< To use modified ICE3/ICE4 to reduce time step dependency
LOGICAL :: LFEEDBACKT !< When .TRUE. feed back on temperature is taken into account
LOGICAL :: LEVLIMIT   !< When .TRUE. water vapour pressure is limited by saturation
LOGICAL :: LNULLWETG  !< When .TRUE. graupel wet growth is activated with null rate (to allow water shedding)
LOGICAL :: LWETGPOST  !< When .TRUE. graupel wet growth is activated with positive temperature (to allow water shedding)
LOGICAL :: LNULLWETH  !< Same as LNULLWETG but for hail
LOGICAL :: LWETHPOST  !< Same as LWETGPOST but for hail
CHARACTER(LEN=4) :: CSNOWRIMING !< OLD or M90 for Murakami 1990 formulation
REAL :: XFRACM90      !< Fraction used for the Murakami 1990 formulation
INTEGER :: NMAXITER_MICRO   !< Maximum number of iterations for mixing ratio or time splitting
REAL :: XMRSTEP       !< maximum mixing ratio step for mixing ratio splitting
LOGICAL :: LCONVHG    !< TRUE to allow the conversion from hail to graupel
LOGICAL :: LCRFLIMIT  !< True to limit rain contact freezing to possible heat exchange
!
REAL :: XTSTEP_TS     !< Approximative time step for time-splitting (0 for no time-splitting)
!
CHARACTER(LEN=80) :: CSUBG_RC_RR_ACCR !< subgrid rc-rr accretion
CHARACTER(LEN=80) :: CSUBG_RR_EVAP    !< subgrid rr evaporation
CHARACTER(LEN=80) :: CSUBG_PR_PDF     !< pdf for subgrid precipitation
CHARACTER(LEN=4)  :: CSUBG_AUCV_RC    !< type of subgrid rc->rr autoconv. method
CHARACTER(LEN=80) :: CSUBG_AUCV_RI    !< type of subgrid ri->rs autoconv. method
CHARACTER(LEN=80) :: CSUBG_MF_PDF     !< PDF to use for MF cloud autoconversions
!
LOGICAL :: LADJ_BEFORE !< must we perform an adjustment before rain_ice call
LOGICAL :: LADJ_AFTER  !< must we perform an adjustment after rain_ice call
LOGICAL :: LSEDIM_AFTER !< sedimentation done before (.FALSE.) or after (.TRUE.) microphysics
!
REAL :: XSPLIT_MAXCFL   !< Maximum CFL number allowed for SPLIT scheme
LOGICAL :: LSNOW_T      !< Snow parameterization from Wurtz (2021)
!
LOGICAL :: LPACK_INTERP !< To pack arrays before computing the different interpolations (kernels and other)
LOGICAL :: LPACK_MICRO  !< To pack arrays before computing the process tendencies
!
INTEGER :: NPROMICRO    !< Size of cache-blocking bloc (0 to deactivate)
!
LOGICAL :: LCRIAUTI     !< .T. to compute XACRIAUTI and XBCRIAUTI (from XCRIAUTI and XT0CRIAUTI);
                        !! .F. to compute XT0CRIAUTI (from XCRIAUTI and XBCRIAUTI)
REAL :: XCRIAUTI_NAM    !< Minimum value for the ice->snow autoconversion threshold
REAL :: XT0CRIAUTI_NAM  !< Threshold temperature (???) for the ice->snow autoconversion threshold
REAL :: XBCRIAUTI_NAM   !< B barameter for the ice->snow autoconversion 10**(aT+b) law
REAL :: XACRIAUTI_NAM   !< A barameter for the ice->snow autoconversion 10**(aT+b) law
REAL :: XCRIAUTC_NAM    !< Threshold for liquid cloud -> rain autoconversion (kg/m**3)
REAL :: XRDEPSRED_NAM   !< Tuning factor of sublimation of snow
REAL :: XRDEPGRED_NAM   !< Tuning factor of sublimation of graupel
!
LOGICAL :: LOCND2       !< Logical switch to separate liquid and ice
LOGICAL :: LKOGAN       !< Use Kogan autocoversion of liquid
LOGICAL :: LMODICEDEP   !< Logical switch for alternative dep/evap of ice
LOGICAL :: LEXCLDROP    !< Logical switch for use of external Cloud droplet (as from NRT aerosols) in microphysics

REAL, DIMENSION(40) :: XFRMIN_NAM
!
END TYPE PARAM_ICE_t
!
TYPE(PARAM_ICE_t), DIMENSION(JPMODELMAX), TARGET, SAVE :: PARAM_ICE_MODEL
TYPE(PARAM_ICE_t), POINTER, SAVE :: PARAM_ICEN => NULL()
!
LOGICAL, POINTER :: LWARM => NULL(), &
                    LSEDIC => NULL(), &
                    LDEPOSC => NULL(), &
                    LRED => NULL(), &
                    LFEEDBACKT => NULL(), &
                    LEVLIMIT => NULL(), &
                    LNULLWETG => NULL(), &
                    LWETGPOST => NULL(), &
                    LNULLWETH => NULL(), &
                    LWETHPOST => NULL(), &
                    LCONVHG => NULL(), &
                    LCRFLIMIT => NULL(), &
                    LADJ_BEFORE => NULL(), &
                    LADJ_AFTER => NULL(), &
                    LSEDIM_AFTER => NULL(), &
                    LSNOW_T => NULL(), &
                    LPACK_INTERP => NULL(), &
                    LPACK_MICRO => NULL(), &
                    LCRIAUTI => NULL(), &
                    LOCND2 => NULL(), &
                    LKOGAN => NULL(), &
                    LMODICEDEP => NULL(), &
                    LEXCLDROP => NULL()

REAL, POINTER :: XVDEPOSC => NULL(), &
                 XFRACM90 => NULL(), &
                 XMRSTEP => NULL(), &
                 XTSTEP_TS => NULL(), &
                 XSPLIT_MAXCFL => NULL(), &
                 XCRIAUTI_NAM => NULL(), &
                 XT0CRIAUTI_NAM => NULL(), &
                 XBCRIAUTI_NAM => NULL(), &
                 XACRIAUTI_NAM => NULL(), &
                 XCRIAUTC_NAM => NULL(), &
                 XRDEPSRED_NAM => NULL(), &
                 XRDEPGRED_NAM => NULL()
REAL, DIMENSION(:), POINTER :: XFRMIN_NAM => NULL()

INTEGER, POINTER :: NMAXITER_MICRO => NULL(), &
                    NPROMICRO => NULL()

CHARACTER(LEN=4), POINTER :: CPRISTINE_ICE => NULL()
CHARACTER(LEN=4), POINTER :: CSEDIM => NULL()
CHARACTER(LEN=4), POINTER :: CSNOWRIMING => NULL()
CHARACTER(LEN=80),POINTER :: CSUBG_RC_RR_ACCR => NULL()
CHARACTER(LEN=80),POINTER :: CSUBG_RR_EVAP => NULL()
CHARACTER(LEN=80),POINTER :: CSUBG_PR_PDF => NULL()
CHARACTER(LEN=4),POINTER :: CSUBG_AUCV_RC=>NULL()
CHARACTER(LEN=80),POINTER :: CSUBG_AUCV_RI=>NULL()
CHARACTER(LEN=80),POINTER :: CSUBG_MF_PDF=>NULL()
!
NAMELIST/NAM_PARAM_ICEn/LWARM,LSEDIC,LCONVHG,CPRISTINE_ICE,CSEDIM,LDEPOSC,XVDEPOSC, &
                       LRED, LFEEDBACKT, &
                       LEVLIMIT,LNULLWETG,LWETGPOST,LNULLWETH,LWETHPOST, &
                       CSNOWRIMING,XFRACM90,NMAXITER_MICRO,XMRSTEP,XTSTEP_TS, &
                       LADJ_BEFORE, LADJ_AFTER, LCRFLIMIT, &
                       XSPLIT_MAXCFL, LSEDIM_AFTER, LSNOW_T, &
                       LPACK_INTERP, LPACK_MICRO, NPROMICRO, CSUBG_RC_RR_ACCR, &
                       CSUBG_RR_EVAP, CSUBG_PR_PDF, CSUBG_AUCV_RC, CSUBG_AUCV_RI, &
                       LCRIAUTI, XCRIAUTI_NAM, XT0CRIAUTI_NAM, XBCRIAUTI_NAM, &
                       XACRIAUTI_NAM, XCRIAUTC_NAM, XRDEPSRED_NAM, XRDEPGRED_NAM, &
                       LOCND2, LKOGAN, LMODICEDEP, LEXCLDROP, &
                       XFRMIN_NAM, CSUBG_MF_PDF
!
!-------------------------------------------------------------------------------
!
CONTAINS
SUBROUTINE PARAM_ICE_GOTO_MODEL(KFROM, KTO)
!! This subroutine associate all the pointers to the right component of
!! the right strucuture. A value can be accessed through the structure PARAM_ICEN
!! or through the strucuture PARAM_ICE_MODEL(KTO) or directly through these pointers.
IMPLICIT NONE
INTEGER, INTENT(IN) :: KFROM, KTO
!
IF(.NOT. ASSOCIATED(PARAM_ICEN, PARAM_ICE_MODEL(KTO))) THEN
  !
  PARAM_ICEN => PARAM_ICE_MODEL(KTO)
  !
  LWARM => PARAM_ICEN%LWARM
  LSEDIC => PARAM_ICEN%LSEDIC
  LDEPOSC => PARAM_ICEN%LDEPOSC
  LRED => PARAM_ICEN%LRED
  LFEEDBACKT => PARAM_ICEN%LFEEDBACKT
  LEVLIMIT => PARAM_ICEN%LEVLIMIT
  LNULLWETG => PARAM_ICEN%LNULLWETG
  LWETGPOST => PARAM_ICEN%LWETGPOST
  LNULLWETH => PARAM_ICEN%LNULLWETH
  LWETHPOST => PARAM_ICEN%LWETHPOST
  LCONVHG => PARAM_ICEN%LCONVHG
  LCRFLIMIT => PARAM_ICEN%LCRFLIMIT
  LADJ_BEFORE => PARAM_ICEN%LADJ_BEFORE
  LADJ_AFTER => PARAM_ICEN%LADJ_AFTER
  LSEDIM_AFTER => PARAM_ICEN%LSEDIM_AFTER
  LSNOW_T => PARAM_ICEN%LSNOW_T
  LPACK_INTERP => PARAM_ICEN%LPACK_INTERP
  LPACK_MICRO => PARAM_ICEN%LPACK_MICRO
  LCRIAUTI => PARAM_ICEN%LCRIAUTI
  LOCND2 => PARAM_ICEN%LOCND2
  LKOGAN => PARAM_ICEN%LKOGAN
  LMODICEDEP => PARAM_ICEN%LMODICEDEP
  LEXCLDROP => PARAM_ICEN%LEXCLDROP
  !
  XVDEPOSC => PARAM_ICEN%XVDEPOSC
  XFRACM90 => PARAM_ICEN%XFRACM90
  XMRSTEP => PARAM_ICEN%XMRSTEP
  XTSTEP_TS => PARAM_ICEN%XTSTEP_TS
  XSPLIT_MAXCFL => PARAM_ICEN%XSPLIT_MAXCFL
  XCRIAUTI_NAM => PARAM_ICEN%XCRIAUTI_NAM
  XT0CRIAUTI_NAM => PARAM_ICEN%XT0CRIAUTI_NAM
  XBCRIAUTI_NAM => PARAM_ICEN%XBCRIAUTI_NAM
  XACRIAUTI_NAM => PARAM_ICEN%XACRIAUTI_NAM
  XCRIAUTC_NAM => PARAM_ICEN%XCRIAUTC_NAM
  XRDEPSRED_NAM => PARAM_ICEN%XRDEPSRED_NAM
  XRDEPGRED_NAM => PARAM_ICEN%XRDEPGRED_NAM
  XFRMIN_NAM => PARAM_ICEN%XFRMIN_NAM
  !
  NMAXITER_MICRO => PARAM_ICEN%NMAXITER_MICRO
  NPROMICRO => PARAM_ICEN%NPROMICRO
  !
  CPRISTINE_ICE => PARAM_ICEN%CPRISTINE_ICE
  CSEDIM => PARAM_ICEN%CSEDIM
  CSNOWRIMING => PARAM_ICEN%CSNOWRIMING
  CSUBG_RC_RR_ACCR => PARAM_ICEN%CSUBG_RC_RR_ACCR
  CSUBG_RR_EVAP => PARAM_ICEN%CSUBG_RR_EVAP
  CSUBG_PR_PDF => PARAM_ICEN%CSUBG_PR_PDF
  CSUBG_AUCV_RC=>PARAM_ICEN%CSUBG_AUCV_RC
  CSUBG_AUCV_RI=>PARAM_ICEN%CSUBG_AUCV_RI
  CSUBG_MF_PDF=>PARAM_ICEN%CSUBG_MF_PDF
ENDIF
END SUBROUTINE PARAM_ICE_GOTO_MODEL
!
SUBROUTINE PARAM_ICEN_INIT(HPROGRAM, TFILENAM, LDNEEDNAM, KLUOUT, &
                          &LDDEFAULTVAL, LDREADNAM, LDCHECK, KPRINT)
!!*** *PARAM_ICEN_INIT* - Code needed to initialize the MODD_PARAM_ICE_n module
!!
!!*   PURPOSE
!!    -------
!!    Sets the default values, reads the namelist, performs the checks and prints
!!
!!*   METHOD
!!    ------
!!    0. Declarations
!!       1. Declaration of arguments
!!       2. Declaration of local variables
!!    1. Default values
!!    2. Namelist
!!    3. Checks
!!    4. Prints
!!
!!    AUTHOR
!!    ------
!!    S. Riette
!!
!!    MODIFICATIONS
!!    -------------
!!      Original    Feb 2023
!-------------------------------------------------------------------------------
!
!*      0. DECLARATIONS
!       ---------------
!
USE MODE_POSNAM_PHY, ONLY: POSNAM_PHY
USE MODE_MSG, ONLY: PRINT_MSG, NVERB_FATAL
USE MODE_CHECK_NAM_VAL, ONLY: CHECK_NAM_VAL_CHAR, CHECK_NAM_VAL_REAL, CHECK_NAM_VAL_INT
USE MODD_IO,  ONLY: TFILEDATA
!
IMPLICIT NONE
!
!* 0.1. Declaration of arguments
!       ------------------------
!
CHARACTER(LEN=6),  INTENT(IN) :: HPROGRAM     !< Name of the calling program
TYPE(TFILEDATA),   INTENT(IN) :: TFILENAM     !< Namelist file
LOGICAL,           INTENT(IN) :: LDNEEDNAM    !< True to abort if namelist is absent
INTEGER,           INTENT(IN) :: KLUOUT       !< Logical unit for outputs
LOGICAL, OPTIONAL, INTENT(IN) :: LDDEFAULTVAL !< Must we initialize variables with default values (defaults to .TRUE.)
LOGICAL, OPTIONAL, INTENT(IN) :: LDREADNAM    !< Must we read the namelist (defaults to .TRUE.)
LOGICAL, OPTIONAL, INTENT(IN) :: LDCHECK      !< Must we perform some checks on values (defaults to .TRUE.)
INTEGER, OPTIONAL, INTENT(IN) :: KPRINT       !< Print level (defaults to 0): 0 for no print, 1 to safely print namelist,
                                              !! 2 to print informative messages
!
!* 0.2 Declaration of local variables
!      ------------------------------
!
LOGICAL :: LLDEFAULTVAL, LLREADNAM, LLCHECK, LLFOUND
INTEGER :: IPRINT

LLDEFAULTVAL=.TRUE.
LLREADNAM=.TRUE.
LLCHECK=.TRUE.
IPRINT=0
IF(PRESENT(LDDEFAULTVAL)) LLDEFAULTVAL=LDDEFAULTVAL
IF(PRESENT(LDREADNAM   )) LLREADNAM   =LDREADNAM
IF(PRESENT(LDCHECK     )) LLCHECK     =LDCHECK
IF(PRESENT(KPRINT      )) IPRINT      =KPRINT
!
!*      1. DEFAULT VALUES
!       -----------------
!
IF(LLDEFAULTVAL) THEN
  !NOTES ON GENERAL DEFAULTS AND MODEL-SPECIFIC DEFAULTS :
  !- General default values *MUST* remain unchanged.
  !- To change the default value for a given application,
  !  an "IF(HPROGRAM=='...')" condition must be used.

  LWARM=.TRUE.
  LSEDIC=.TRUE.
  LDEPOSC=.FALSE.
  XVDEPOSC= 0.02 ! 2 cm/s
  CPRISTINE_ICE='PLAT'
  CSEDIM='SPLI'
  LRED=.TRUE.
  LFEEDBACKT=.TRUE.
  LEVLIMIT=.TRUE.
  LNULLWETG=.TRUE.
  LWETGPOST=.TRUE.
  LNULLWETH=.TRUE.
  LWETHPOST=.TRUE.
  CSNOWRIMING='M90'
  XFRACM90=0.1
  NMAXITER_MICRO=5
  XMRSTEP=0.00005
  LCONVHG=.FALSE.
  LCRFLIMIT=.TRUE.
  XTSTEP_TS=0.
  CSUBG_RC_RR_ACCR='NONE'
  CSUBG_RR_EVAP='NONE'
  CSUBG_PR_PDF='SIGM'
  CSUBG_AUCV_RC='NONE'
  CSUBG_AUCV_RI='NONE'
  CSUBG_MF_PDF='TRIANGLE'
  LADJ_BEFORE=.TRUE.
  LADJ_AFTER=.TRUE.
  LSEDIM_AFTER=.FALSE.
  XSPLIT_MAXCFL=0.8
  LSNOW_T=.FALSE.
  LPACK_INTERP=.TRUE.
  LPACK_MICRO=.TRUE.
  NPROMICRO=0
  LCRIAUTI=.FALSE.
  !!XCRIAUTIi_NAM = 0.25E-3 !  Critical ice content for the autoconversion to occur
  XCRIAUTI_NAM = 0.2E-4 !  Revised value by Chaboureau et al. (2001)
  XACRIAUTI_NAM=0.06
  XBCRIAUTI_NAM=-3.5
  XT0CRIAUTI_NAM=(LOG10(XCRIAUTI_NAM)-XBCRIAUTI_NAM)/0.06
  XCRIAUTC_NAM=0.5E-3
  XRDEPSRED_NAM=1.
  XRDEPGRED_NAM=1.
  LOCND2=.FALSE.
  LKOGAN=.FALSE.
  LMODICEDEP=.FALSE.
  LEXCLDROP=.FALSE.  ! Only can be TRUE if external Cloud droplets are used.
  ! Tuning and modication of graupeln etc:
  XFRMIN_NAM(1:4)=0.        ! Different thresholds for snow, ice, graupel and graupel again, respectively, leading to conversion of super-cooled rain into graupel
  XFRMIN_NAM(5)=0.          ! Higher value means less graupel and more snow (Experimental)
  XFRMIN_NAM(6)=0.          ! Higher value means more graupel and less snow (Experimental)
  XFRMIN_NAM(7)=1.          ! Tuning factor for the collisions between rain and snow. Higher values give less super-cooled rain and more snow. Zero means that those collisions are disregarded (probably OK)
  XFRMIN_NAM(8)=1.          ! > 1. Increase melt of graupel, < 1 decrease it (Experimental)
  XFRMIN_NAM(9)=1.          ! > 1 means increase IN-concentration and <1 decrease
  XFRMIN_NAM(10)=10.        ! >10 means faster Kogan autoconversion <10 slower, only active for LKOGAN=T.
  XFRMIN_NAM(11)=1.         ! Setting e.g. 0.01 means that subgrid-scale fraction of cloud water is used. Minimum cloud fraction=0.01, only active for LKOGAN=T.
  XFRMIN_NAM(12)=0.         ! Threshold supersaturation with respect to ice in the supersaturated part of the grid-box for treatment in the microphysics computation, only used with OCND2
  XFRMIN_NAM(13)=1.0E-15    ! Threshold mixing ratio for different non-vapor water species treated in the microphysics computation, only used with OCND2
  XFRMIN_NAM(14)=120.       ! Time scale for conversion of large ice crystals to snow, only used with LMODICEDEP (Experimental)
  XFRMIN_NAM(15)=1.0E-4     ! Ice crystal diameter(m) for conversion from cloud ice to snow. Larger values lead to more ice and less snow
  XFRMIN_NAM(16)=0.         ! "C" parameter for size distribution of snow. (constant for number concentration, N=C lamda^x) Only active if non-zero (Experimental)
  XFRMIN_NAM(17)=0.         ! "x" parameter for size distribution of snow. (slope for number concentration, N=C lamda^x) Only active if XFRMIN(16) is non-zero (Experimental)
  XFRMIN_NAM(18)=0.         ! With XFRMIN(18)=1, snow and graupel melt are based on wet bulb temperature, instead of temperature and leads to slower melting (Experimental)
  XFRMIN_NAM(19)=0.         ! Threshold cloud thickness for StCu/Cu transition [m] Only active for EDMF scheme and if non-zero, but very small effect
  XFRMIN_NAM(20)=0.         ! Threshold cloud thickness used in shallow/deep decision [m]. Only active for EDMF scheme and if non-zero, higher value gives more shallow convection and less deep model resolved convection
  XFRMIN_NAM(21)=1.         ! Tuning factor for ice clouds, such as cirrus. A larger value means a larger effect of the presence of solid water and thus more ice clouds
  XFRMIN_NAM(22)=1.         ! Tuning parameter for CDNC at lowest model level. Lower value give lower CDNC. XFRMIN(22)=0.5 means CDNC= old CDNC x 0.5
  XFRMIN_NAM(23)=0.5        ! The lower limit for reduction of VSIGQSAT, only active with LHGT_QS
  XFRMIN_NAM(24)=1.5        ! The upper limit for increase of VSIGQSAT, only active with LHGT_QS
  XFRMIN_NAM(25)=30.        ! The level thickness for which VSIGQSAT is unchanged with LHGT_QS, only active with LHGT_QS
  XFRMIN_NAM(26)=0.         ! If > 0.01, it replaces default CDNC everywhere. So XFRMIN(26)=50E6 (Beware of that it is in m-3!) gives CDNC = 50 cm-3 at reference level (1000 hPa) and XFRMIN(26) x pressure/ ref-pressure elsewhere.
  XFRMIN_NAM(27)=0.         ! Minimum temperature (K) used for Meyers ice number concentration. Larger values give less ice for temperatures below XFRMIN(27) (Experimental)
  XFRMIN_NAM(28)=0.         ! Currently not used
  XFRMIN_NAM(29)=0.         ! If >0. and XFRMIN(22)>0 it gives the upper limit in metres for which the reduction of CDNC has an effect. A linear decrease from the lowest level to XFRMIN(29) meters is assumed
  XFRMIN_NAM(30)=1.         ! If not unity, CDNC is reduced/increased over sea with a factor XFRMIN(30) for the lowest model level and linearly reaching "no change" at XFRMIN(29) m height. If XFRMIN(29) is unset, XFRMIN(30) only affects the lowest model level
  XFRMIN_NAM(31:38)=0.      ! Currently not used
  XFRMIN_NAM(39)=0.25       ! Speed factor for deposition/evaporation rate of graupel. Larger values give faster deposition/evaporation, only used when OCND2=T and LMODICEDEP=F
  XFRMIN_NAM(40)=0.15       ! Speed factor for deposition/evaporation rate of snow. Larger values give faster deposition/evaporation, only used when OCND2=T and LMODICEDEP=F

  IF(HPROGRAM=='AROME') THEN
    LCONVHG=.TRUE.
    LADJ_BEFORE=.TRUE.
    LADJ_AFTER=.FALSE.
    LRED=.FALSE.
    CSEDIM='STAT'
    LSEDIC=.FALSE.
    XMRSTEP=0.
    CSUBG_AUCV_RC='PDF'
  ELSEIF(HPROGRAM=='LMDZ') THEN
    CSUBG_AUCV_RC='PDF'
    CSEDIM='STAT'
    NMAXITER_MICRO=1
    LCRIAUTI=.TRUE.
    XCRIAUTC_NAM=0.001
    XCRIAUTI_NAM=0.0002
    XT0CRIAUTI_NAM=-5.
    LRED=.TRUE.
    LCONVHG=.TRUE.
    LADJ_BEFORE=.TRUE.
    LADJ_AFTER=.FALSE.
  ENDIF
ENDIF
!
!*      2. NAMELIST
!       -----------
!
IF(LLREADNAM) THEN
  CALL POSNAM_PHY(TFILENAM, 'NAM_PARAM_ICEN', LDNEEDNAM, LLFOUND)
  IF(LLFOUND) READ(UNIT=TFILENAM%NLU, NML=NAM_PARAM_ICEn)
ENDIF
!
!*      3. CHECKS
!       ---------
!
IF(LLCHECK) THEN
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CPRISTINE_ICE', CPRISTINE_ICE, 'PLAT', 'COLU', 'BURO')
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CSEDIM', CSEDIM, 'SPLI', 'STAT', 'NONE')
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CSUBG_RC_RR_ACCR', CSUBG_RC_RR_ACCR, 'NONE', 'PRFR')
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CSUBG_RR_EVAP', CSUBG_RR_EVAP, 'NONE', 'CLFR', 'PRFR')
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CSUBG_PR_PDF', CSUBG_PR_PDF, 'SIGM', 'HLCRECTPD', 'HLCTRIANGPDF', &
                                                                'HLCQUADRAPDF', 'HLCISOTRIPDF')
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CSUBG_AUCV_RC', CSUBG_AUCV_RC, 'PDF ', 'CLFR', 'NONE', 'ADJU', 'SIGM')
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CSUBG_AUCV_RI', CSUBG_AUCV_RI, 'NONE', 'CLFR', 'ADJU')
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CSUBG_MF_PDF', CSUBG_MF_PDF, 'NONE', 'TRIANGLE', 'BIGA')
  CALL CHECK_NAM_VAL_CHAR(KLUOUT, 'CSNOWRIMING', CSNOWRIMING, 'OLD ', 'M90 ')
  CALL CHECK_NAM_VAL_REAL(KLUOUT, 'XTSTEP_TS', XTSTEP_TS, '>=', 0.)
  CALL CHECK_NAM_VAL_REAL(KLUOUT, 'XSPLIT_MAXCFL', XSPLIT_MAXCFL, '>', 0., '<=', 1.)
  CALL CHECK_NAM_VAL_REAL(KLUOUT, 'XVDEPOSC', XVDEPOSC, '>=', 0.)
  CALL CHECK_NAM_VAL_REAL(KLUOUT, 'XFRACM90', XFRACM90, '>=', 0., '<=', 1.)
  CALL CHECK_NAM_VAL_REAL(KLUOUT, 'XMRSTEP', XMRSTEP, '>=', 0.)
  CALL CHECK_NAM_VAL_INT(KLUOUT, 'NPROMICRO', NPROMICRO, '>=', 0)

  IF (LOCND2 .AND. (XRDEPSRED_NAM /= 1 .OR. XRDEPGRED_NAM /= 1)) THEN
    CALL ABOR1 ("XRDESRED_NAM and XRDEGRED_NAM must not be activated together with LOCND2")
  ENDIF

  IF(HPROGRAM=='AROME' .OR. HPROGRAM=='LMDZ') THEN
    IF(.NOT. (LADJ_BEFORE .AND. .NOT. LADJ_AFTER)) THEN
      CALL PRINT_MSG(NVERB_FATAL, 'GEN', 'MODD_PARAM_ICE_n', 'With AROME/LMDZ, LADJ_BEFORE must be .T. and LADJ_AFTER must be .F.')
    ENDIF
  ENDIF

  IF(LSNOW_T .AND. .NOT. LRED) THEN
    CALL PRINT_MSG(NVERB_FATAL, 'GEN', 'MODD_PARAM_ICE_n', 'It is not possible to run LSNOW_T without LRED')
  ENDIF
ENDIF
!
!*      3. PRINTS
!       ---------
!
IF(IPRINT>=1) THEN
  WRITE(UNIT=KLUOUT,NML=NAM_PARAM_ICEn)
ENDIF
!
END SUBROUTINE PARAM_ICEN_INIT
!
END MODULE MODD_PARAM_ICE_n
