SUBROUTINE APL_AROME_ADJUST(YDCST, YDMODEL, YDCPG_BNDS, YDCPG_OPTS, YDCPG_MISC, YDGEOMETRY, YDVARS, &
                          & YDMF_PHYS, YDMF_PHYS_BASE_STATE, &
                          & YSPP_ALL, YDDDH, &
                          & PRHODJM, PEXNREFM, &
                          & PPABSM, PWM, PTKEM, &
                          & PRHODREFM, &
                          & PTHM, PRM, PLIMAM, PTENDRA, &
                          & PTHS, PTHSIN, PRS, PRSIN, PLIMAS, PLIMASIN, &
                          & PTENDLIMA, PZZ_F, PTENDT, &
                          & PP1EZDIAG_XX, PP1EZDIAG_YY, &
                          & PSRCS, PNEBMNH, &
                          & PICLDFR, PWCLDFR, PSSIO, PSSIU, PIFR, &
                          & PQDM, PQVM, PQCM, PQRM, PQIM, PQSM, PQGM, PQHM, &
                          & PCPM, PRHM, PTM, &
                          & PAPHIM, PAPHIFM, &
                          & PZZ, PTENDTT, PDZZ, &
                          & PDTHRAD, PICEFR, PPRCFR, &
                          & PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF)

!**** *APL_AROME_ADJUST* - Setup and call Arome microphysics adjustment

!     Author.
!     -------
!      Rolf Heilemann Myhre *Met Norway*
!      Original : 01-09-2023
!      See APL_AROME for modification history

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------
!
!   YDCST:                Model constants
!   YDMODEL:              Model configuration settings
!   YDCPG_BNDS:           Array dimensions and bounds
!   YDCPG_OPTS:           Various dimensions
!   YDCPG_MISC:
!   YDGEOMETRY:           Geomtery settings from IFS model
!   YDVARS:               Persistent fields
!   YDMF_PHYS:            Physics fields
!   YDMF_PHYS_BASE_STATE:
!   YSPP_ALL:             SPP fields and settings
!   YDDDH:                DDH fields and settings
!
!   PTM:    Temperature.
!   PCPM:   Specific heat at constant pressure for air
!   PRM:    Moist variables at time t
!   PTKEM:  Turbulent kinetic energy
!   PTENDT: Temperature tendency
!

  USE PARKIND1, ONLY: JPRB, JPRD, JPIM

  USE YOMCST,                      ONLY: TCST
  USE TYPE_MODEL,                  ONLY: MODEL
  USE CPG_OPTS_TYPE_MOD,           ONLY: CPG_BNDS_TYPE, CPG_OPTS_TYPE
  USE CPG_TYPE_MOD,                ONLY: CPG_MISC_TYPE
  USE GEOMETRY_MOD,                ONLY: GEOMETRY
  USE FIELD_VARIABLES_MOD,         ONLY: FIELD_VARIABLES

  USE MF_PHYS_TYPE_MOD,            ONLY: MF_PHYS_TYPE
  USE MF_PHYS_BASE_STATE_TYPE_MOD, ONLY: MF_PHYS_BASE_STATE_TYPE

  USE DDH_MIX,                     ONLY: TYP_DDH, NEW_ADD_FIELD_3D
  USE SPP_MOD_TYPE,                ONLY: UA_PHYS_SPP_VARS

  USE YOMHOOK,                     ONLY: LHOOK, DR_HOOK, JPHOOK

  IMPLICIT NONE

  TYPE(TCST),                    INTENT(IN)    :: YDCST
  TYPE(MODEL),                   INTENT(IN)    :: YDMODEL
  TYPE(CPG_BNDS_TYPE),           INTENT(IN)    :: YDCPG_BNDS
  TYPE(CPG_OPTS_TYPE),           INTENT(IN)    :: YDCPG_OPTS
  TYPE(CPG_MISC_TYPE),           INTENT(IN)    :: YDCPG_MISC
  TYPE(GEOMETRY),                INTENT(IN)    :: YDGEOMETRY
  TYPE(FIELD_VARIABLES),         INTENT(INOUT) :: YDVARS

  TYPE(MF_PHYS_TYPE),            INTENT(IN)    :: YDMF_PHYS
  TYPE(MF_PHYS_BASE_STATE_TYPE), INTENT(IN)    :: YDMF_PHYS_BASE_STATE

  TYPE(UA_PHYS_SPP_VARS),        INTENT(INOUT) :: YSPP_ALL
  TYPE(TYP_DDH),                 INTENT(INOUT) :: YDDDH

  REAL(KIND=JPRB), INTENT(IN)    :: PRHODJM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PEXNREFM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PPABSM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), TARGET, INTENT(IN)  :: PWM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PTKEM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(INOUT) :: PRHODREFM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTHM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PRM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PLIMAM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDRA(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTHS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTHSIN(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PRS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PRSIN(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PLIMAS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PLIMASIN(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDLIMA(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PZZ_F(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG) ! temperature tendency

  REAL(KIND=JPRB), INTENT(INOUT) :: PP1EZDIAG_XX(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PP1EZDIAG_YY(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PSRCS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(OUT)   :: PNEBMNH(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PICLDFR(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PWCLDFR(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PSSIO(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PSSIU(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PIFR(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PQDM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PQVM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PQCM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PQRM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PQIM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PQSM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PQGM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PQHM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PCPM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PRHM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PTM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PAPHIM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PAPHIFM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PZZ(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PTENDTT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG) ! temperature tendency
  REAL(KIND=JPRB), INTENT(OUT)   :: PDZZ(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PDTHRAD(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PICEFR(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PPRCFR(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(OUT)   :: PHLC_HRC(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PHLC_HCF(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PHLI_HRI(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PHLI_HCF(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZSIGM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZMFM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZRC_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZRI_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZCF_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZHLC_HRC_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZHLC_HCF_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZHLI_HRI_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZHLI_HCF_MF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZWEIGHT_MF_CLOUD(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZTMPAF(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZCON1(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZCON2(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZCON3(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), POINTER :: ZPTRWNU(:,:)
  REAL(KIND=JPRB), TARGET  :: ZWNU(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  ! ACRANEB2 local variables
  REAL(KIND=JPRB) :: ZNEB0    (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  ! protected cloud fractions
  REAL(KIND=JPRB) :: ZDECRD   (YDCPG_OPTS%KLON)       ! decorrelation depth
  REAL(KIND=JPRB) :: ZCLCT_RAD(YDCPG_OPTS%KLON)       ! total cloud cover for radiation

  CHARACTER(LEN=11) :: CLNAME
  CHARACTER(LEN=2), DIMENSION(7), PARAMETER :: CLVARNAME=(/"QV","QL","QR","QI","QS","QG","QH"/)

  REAL(KIND=JPRB) :: ZQHGM(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

  INTEGER(KIND=JPIM) :: JLON, JLEV, JRR, JGFL

  INTEGER(KIND=JPIM), PARAMETER :: IKL = -1
  INTEGER(KIND=JPIM), PARAMETER :: IKU = 1

  REAL(KIND=JPRB) :: ZDT, ZRHO, ZINVG, ZEPSNEB

  LOGICAL :: LLIMAINIT

  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

  !------------------------------------------------------------------

#include "aro_adjust.h"
#include "aro_adjust_lima.h"
#include "gprcp_qlirsg.intfb.h"
#include "gpgeo.intfb.h"
#include "aro_suintbudget_omp.h"
#include "acnpart.intfb.h"

    !------------------------------------------------------------------

  IF (LHOOK) CALL DR_HOOK('APL_AROME_ADJUST_MOD:APL_AROME_ADJUST', 0, ZHOOK_HANDLE)

  IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMICRO) THEN

    !initialise sigma for subgrid condensation coming
    !from previous time step turbulence scheme
    IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%LSIGMAS) THEN
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        ZSIGM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = YDMF_PHYS_BASE_STATE%SRC(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
      ENDDO
    ENDIF

    !initialise convective mas flux for subgrid condensation coming
    !from previous time step convection scheme
    IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%LSUBG_COND .AND. .NOT. &
      & YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%LSIGMAS) THEN

      IF (YDMODEL%YRML_PHY_MF%YRARPHY%LKFBCONV) THEN
        DO JLEV = 1, YDCPG_OPTS%KFLEVG
          ZMFM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = YDMF_PHYS_BASE_STATE%SRC(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ENDDO
      ELSE
        DO JLEV = 1, YDCPG_OPTS%KFLEVG
          ZMFM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = 0._JPRB
        ENDDO
      ENDIF

    ENDIF

    IF (YDMODEL%YRML_DYN%YRDYNA%LTWOTL) THEN
      ZDT = YDCPG_OPTS%ZDTPHY/2._JPRB
    ELSE
      IF (YDCPG_OPTS%NSTEP /= 0) THEN
        ZDT = YDCPG_OPTS%ZDTPHY/2._JPRB
      ELSE
        ZDT = YDCPG_OPTS%ZDTPHY
      ENDIF
    ENDIF

    ZINVG=1._JPRB/YDCST%RG

    IF (YDMODEL%YRML_PHY_MF%YRARPHY%LMFSHAL .AND. &
     & (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_CLOUD == 'DIRE' .OR. &
     &  YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_CLOUD == 'BIGA')) THEN

      IF (YDCPG_OPTS%NSTEP == 0) THEN
        DO JLEV = 1, YDCPG_OPTS%KFLEVG
          ZRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
          ZRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
          ZCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
          ZWEIGHT_MF_CLOUD(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
          ZHLC_HRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
          ZHLC_HCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
          ZHLI_HRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
          ZHLI_HCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
        ENDDO
      ELSE
        DO JLEV = 1, YDCPG_OPTS%KFLEVG
          ZRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = YDMF_PHYS_BASE_STATE%LMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
          ZCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = YDMF_PHYS_BASE_STATE%AMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
          ZRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = YDMF_PHYS_BASE_STATE%IMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
          ZWEIGHT_MF_CLOUD(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = YDMF_PHYS_BASE_STATE%WMFC(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
          ZHLC_HRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = YDMF_PHYS_BASE_STATE%HLMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
          ZHLC_HCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) =YDMF_PHYS_BASE_STATE%HLCFMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
          ZHLI_HRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = YDMF_PHYS_BASE_STATE%HIMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
          ZHLI_HCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = YDMF_PHYS_BASE_STATE%HICFMF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
        ENDDO
      ENDIF
    ELSE
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        ZRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
        ZRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
        ZCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
        ZWEIGHT_MF_CLOUD(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
        ZHLC_HRC_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
        ZHLC_HCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
        ZHLI_HRI_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
        ZHLI_HCF_MF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 0._JPRB
      ENDDO
    ENDIF

    ! for now a copy is needed (see below, inside). I don't like than :-( REK
    PTHS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = PTHSIN(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PRS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG, 1:YDMODEL%YRML_PHY_MF%YRPARAR%NRR) = &
    & PRSIN(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG, 1:YDMODEL%YRML_PHY_MF%YRPARAR%NRR)

    IF (YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO == 'LIMA') THEN

      !initialisation de ZZI_THRAD
      IF (YDCPG_OPTS%NSTEP==0) THEN
        DO JLEV = 1, YDCPG_OPTS%KFLEVG
          PDTHRAD(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = 0._JPRB
        ENDDO
        LLIMAINIT = .TRUE.
        PP1EZDIAG_XX(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = 0._JPRB
        PP1EZDIAG_YY(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = 0._JPRB
      ELSE
        DO JLEV = 1, YDCPG_OPTS%KFLEVG
          PDTHRAD(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = PP1EZDIAG_YY(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ENDDO
        LLIMAINIT = .FALSE.
      ENDIF

      IF (YDMODEL%YRML_PHY_MF%YRARPHY%LTURB) THEN
        DO JLON=YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          DO JLEV=1,YDCPG_OPTS%KFLEVG
            ZWNU(JLON,JLEV) = PWM(JLON,JLEV) + 0.66*SQRT(PTKEM(JLON,JLEV))
          ENDDO
        ENDDO
        ZPTRWNU => ZWNU(1:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG)
      ELSE
        ZPTRWNU => PWM(1:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG)
      ENDIF

      ! for now a copy is needed (see below, inside). I don't like than :-( REK
      PLIMAS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG, 1:YDMODEL%YRML_GCONF%YGFL%NLIMA) = &
      & PLIMASIN(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG, 1:YDMODEL%YRML_GCONF%YGFL%NLIMA)

      CALL ARO_ADJUST_LIMA(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX,                          &
      & YDCPG_OPTS%KFLEVG, IKU, IKL,                                                   &
      & YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDCPG_BNDS%KFDIA,                         &
      & YDMODEL%YRML_PHY_MF%YRPARAR%NRR,                                               &
      & YDMODEL%YRML_GCONF%YGFL%NLIMA, YDCPG_OPTS%NSTEP+1,                             &
      & YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%LSUBG_COND,                             &
      & YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%LSIGMAS,                                &
      & ZDT, YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%VSIGQSAT, PZZ_F,                   &
      & PRHODJM(:, 1:YDCPG_OPTS%KFLEVG), PRHODREFM(:, 1:YDCPG_OPTS%KFLEVG), PEXNREFM,  &
      & PPABSM(:, 1:YDCPG_OPTS%KFLEVG), PTHM(:, 1:YDCPG_OPTS%KFLEVG),                  &
      & PRM, PLIMAM, ZSIGM, ZPTRWNU, PDTHRAD, ZMFM, ZRC_MF, ZRI_MF, ZCF_MF, PTHS, PRS, &
      & PLIMAS, PSRCS(:, 1:YDCPG_OPTS%KFLEVG), PNEBMNH,                                &
      & PICEFR, PPRCFR,                                                                &
      & YDDDH, YDMODEL%YRML_DIAG%YRLDDH, YDMODEL%YRML_DIAG%YRMDDH,                     &
      & LLIMAINIT)

    ELSE

      CALL ARO_ADJUST(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX,              &
      & YDCPG_BNDS%KFDIA, YDCPG_BNDS%KIDIA,                           &
      & YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG,                          &
      & YDMODEL%YRML_PHY_MF%YRPARAR%NRR,                              &
      & YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO,                           &
      & ZDT, PZZ_F,                                                   &
      & PRHODJM(:, 1:YDCPG_OPTS%KFLEVG), PEXNREFM,                    &
      & PRHODREFM(:, 1:YDCPG_OPTS%KFLEVG),                            &
      & PPABSM(:, 1:YDCPG_OPTS%KFLEVG), PTHM(:, 1:YDCPG_OPTS%KFLEVG), &
      & PRM, ZSIGM, ZMFM,                                             &
      & ZRC_MF, ZRI_MF, ZCF_MF, ZWEIGHT_MF_CLOUD, &
      & PTHS, PRS, PSRCS(:, 1:YDCPG_OPTS%KFLEVG),                     &
      & PNEBMNH,                                                      &
      & PICLDFR, PWCLDFR, PSSIO, PSSIU, PIFR,                         &
      & PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF,                       &
      & ZHLC_HRC_MF, ZHLC_HCF_MF, ZHLI_HRI_MF, ZHLI_HCF_MF, &
      & YDDDH, YDMODEL%YRML_DIAG%YRLDDH, YDMODEL%YRML_DIAG%YRMDDH,    &
      & YSPP_ALL%YSPP_PSIGQSAT, YSPP_ALL%YSPP_ICE_CLD_WGT)

    ENDIF

    DO JLEV=1,YDCPG_OPTS%KFLEVG
      YDVARS%A%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = PNEBMNH(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
    ENDDO

    !adjusted zthm and zrm
    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PTHM(JLON, JLEV) = PTHS(JLON,JLEV)*YDCPG_OPTS%ZDTPHY
      ENDDO
    ENDDO

    DO JRR=1, YDMODEL%YRML_PHY_MF%YRPARAR%NRR
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
          PRM(JLON, JLEV, JRR) = PRS(JLON, JLEV, JRR)*YDCPG_OPTS%ZDTPHY
        ENDDO
      ENDDO
    ENDDO

    !initialisation de qdm utile pour
    !convertir tendance de r en tendance de q
    IF (YDMODEL%YRML_PHY_MF%YRPARAR%NRR == 6) THEN
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          PQDM(JLON,JLEV) = 1._JPRB/(1._JPRB + &
          & PRM(JLON,JLEV,1) + PRM(JLON,JLEV,2) + PRM(JLON,JLEV,3) + &
          & PRM(JLON,JLEV,4) + PRM(JLON,JLEV,5) + PRM(JLON,JLEV,6))
        ENDDO
      ENDDO
    ELSEIF (YDMODEL%YRML_PHY_MF%YRPARAR%NRR == 7) THEN
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          PQDM(JLON,JLEV) = 1._JPRB/(1._JPRB + &
          & PRM(JLON,JLEV,1) + PRM(JLON,JLEV,2) + PRM(JLON,JLEV,3) + &
          & PRM(JLON,JLEV,4) + PRM(JLON,JLEV,5) + PRM(JLON,JLEV,6) + PRM(JLON,JLEV,7))
        ENDDO
      ENDDO
    ENDIF

    !reinitialisation des qi
    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        PQVM(JLON,JLEV)=PRM(JLON,JLEV,1)*PQDM(JLON,JLEV)
        PQCM(JLON,JLEV)=PRM(JLON,JLEV,2)*PQDM(JLON,JLEV)
        PQRM(JLON,JLEV)=PRM(JLON,JLEV,3)*PQDM(JLON,JLEV)
        PQIM(JLON,JLEV)=PRM(JLON,JLEV,4)*PQDM(JLON,JLEV)
        PQSM(JLON,JLEV)=PRM(JLON,JLEV,5)*PQDM(JLON,JLEV)
        PQGM(JLON,JLEV)=PRM(JLON,JLEV,6)*PQDM(JLON,JLEV)
      ENDDO
    ENDDO

    IF (YDMODEL%YRML_PHY_MF%YRPARAR%NRR == 7) THEN
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          PQHM(JLON,JLEV) = PRM(JLON,JLEV,7)*PQDM(JLON,JLEV)
        ENDDO
      ENDDO
    ELSE
      PQHM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG) = 0._JPRB
    ENDIF

    ! Tendances des variables LIMA
    DO JGFL=1, YDMODEL%YRML_GCONF%YGFL%NLIMA
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        ! RÃ©initialisation des variables LIMA
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          PLIMAM(JLON,JLEV,JGFL) = PLIMAS(JLON,JLEV,JGFL)*YDCPG_OPTS%ZDTPHY
          PTENDLIMA(JLON, JLEV, JGFL) = PTENDLIMA(JLON, JLEV, JGFL) + (PLIMAS(JLON, JLEV, JGFL) - PLIMASIN(JLON, JLEV, JGFL))
        ENDDO
      ENDDO
    ENDDO

    !modif de R et CP
    ZQHGM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,:) = PQHM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,:) &
                                             & + PQGM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,:)

    CALL GPRCP_QLIRSG(YDCPG_OPTS%KLON, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, &
                    & PQ=PQVM, PQI=PQIM, PQL=PQCM, PQR=PQRM, PQS=PQSM, PQG=ZQHGM, PCP=PCPM, PR=PRHM)

    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        PTM(JLON,JLEV) = PTHM(JLON,JLEV)*PEXNREFM(JLON,JLEV)
        ZRHO = YDMF_PHYS_BASE_STATE%YCPG_PHY%PREF(JLON,JLEV)/(PRHM(JLON,JLEV)*PTM(JLON,JLEV))
        PRHODREFM(JLON,JLEV) = ZRHO*PQDM(JLON,JLEV)
      ENDDO
    ENDDO

    !geopotentiel calculation
    PAPHIM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

    CALL GPGEO(YDCPG_OPTS%KLON, YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, PAPHIM, PAPHIFM, &
    & PTM, PRHM, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%LNPR, YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%ALPH, YDGEOMETRY%YRVERT_GEOM)

    !calcul de l'altitude
    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        PZZ(JLON,JLEV) = PAPHIM(JLON,JLEV)*ZINVG
        !initialisation de ZZZ_F_
        PZZ_F(JLON,JLEV) = PAPHIFM(JLON,JLEV)*ZINVG
        ! tendency of T
        PTENDT(JLON,JLEV) = PTENDT(JLON,JLEV) + (PTHS(JLON,JLEV) - PTHSIN(JLON,JLEV))*PEXNREFM(JLON,JLEV)
        PTENDTT(JLON,JLEV) = PTHS(JLON,JLEV) - PTHSIN(JLON,JLEV)
      ENDDO
    ENDDO

    !inversion niveaux tendances des ri et conversion en qi en multipliant par qd
    DO JRR = 1, YDMODEL%YRML_PHY_MF%YRPARAR%NRR
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          PTENDRA(JLON,JLEV,JRR) = PTENDRA(JLON,JLEV,JRR) + (PRS(JLON,JLEV,JRR) - PRSIN(JLON,JLEV,JRR))*PQDM(JLON,JLEV)
        ENDDO
      ENDDO
    ENDDO

    !initialisation de PDZZ
    DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      PDZZ(JLON,1) = PAPHIM(JLON,0)*ZINVG - PZZ(JLON,1)
    ENDDO
    DO JLEV = 2, YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PDZZ(JLON,JLEV) = PZZ(JLON,JLEV-1) - PZZ(JLON,JLEV)
      ENDDO
    ENDDO

  ELSE

    PTM (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%T(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PRHM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%R(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PQVM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%Q(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PQIM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%I(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PQCM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%L(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PQRM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%R(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PQSM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%S(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PQGM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%G(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    IF (YDMODEL%YRML_PHY_MF%YRPARAR%NRR == 7) THEN
      PQHM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
      & YDMF_PHYS_BASE_STATE%H(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)
    ELSE
      PQHM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG) = 0._JPRB
    ENDIF

    PCPM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, YDCPG_OPTS%KTDIA:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, YDCPG_OPTS%KTDIA:YDCPG_OPTS%KFLEVG)

    PAPHIM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG)

    PAPHIFM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

    PZZ(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) = &
    & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)*ZINVG

    !initialisation of PCLFS outside LMICRO to be zero in case LMICRO=F
    YDVARS%A%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG) = 0._JPRB

  ENDIF ! ADJUSTMENT LMICRO

  IF (YDMODEL%YRML_DIAG%YRLDDH%LFLEXDIA) THEN

    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      DO JLON=YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        ZTMPAF(JLON,JLEV) = (PTHS(JLON,JLEV) - PTHSIN(JLON,JLEV))*PEXNREFM(JLON,JLEV) &
                        & * YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)*PCPM(JLON,JLEV)/YDCST%RG
      ENDDO
    ENDDO
    CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZTMPAF, 'TCTADJU', YDDDH)

    DO JRR = 1, YDMODEL%YRML_PHY_MF%YRPARAR%NRR

      CLNAME='T'//CLVARNAME(JRR)//'ADJU'

      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
          ZTMPAF(JLON,JLEV) = (PRS(JLON,JLEV,JRR) - PRSIN(JLON,JLEV,JRR))*PQDM(JLON,JLEV) &
                          & * YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)/YDCST%RG
        ENDDO
      ENDDO
      CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZTMPAF, CLNAME, YDDDH)

      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        DO JLON=YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          ZTMPAF(JLON,JLEV)=YDVARS%A%T1(JLON,JLEV)*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)
        ENDDO
      ENDDO
      CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZTMPAF, 'VNT', YDDDH)

    ENDDO

    ! specific to new data flow for diagnostics
    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      ZCON1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = 1.0_JPRB
      ZCON2(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV) = PQDM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, JLEV)
    ENDDO

    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      DO JLON=YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        ZCON3(JLON, JLEV) = YDMF_PHYS_BASE_STATE%YCPG_DYN%RCP%CP(JLON, JLEV)*PEXNREFM(JLON, JLEV)
      ENDDO
    ENDDO

    CALL ARO_SUINTBUDGET_OMP(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, ZCON1, ZCON2, ZCON3, YDDDH)

  ENDIF

  IF (JPRD == JPRB) THEN
    ZEPSNEB=1.E-12
  ELSE
    ZEPSNEB=1.E-06
  ENDIF

  ! protect cloudiness from being 0 or 1  (needed for ACRANEB2 and ACNPART)
  DO JLEV=YDCPG_OPTS%KTDIA, YDCPG_OPTS%KFLEVG
    DO JLON=YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      ZNEB0(JLON,JLEV) = MAX(ZEPSNEB, MIN(1._JPRB-ZEPSNEB, YDVARS%A%T1(JLON, JLEV)))
    ENDDO
  ENDDO

  ! decorrelation depth for cloud overlaps
  IF (YDMODEL%YRML_PHY_MF%YRPHY%LRNUEXP) THEN
    DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      ZDECRD(JLON) = YDMODEL%YRML_PHY_MF%YRPHY0%RDECRD1 &
                 & + YDMODEL%YRML_PHY_MF%YRPHY0%RDECRD2*EXP(-((ASIN(YDVARS%GEOMETRY%GEMU%T0(JLON)) &
                 & - YDMODEL%YRML_PHY_MF%YRPHY0%RDECRD3*YDMODEL%YRML_GCONF%YRRIP%RDECLI) &
                 & / YDMODEL%YRML_PHY_MF%YRPHY0%RDECRD4)**2)
    ENDDO
  ENDIF

  ! calculate high, medium, low and total cloud cover
  CALL ACNPART(YDCST, YDMODEL%YRML_PHY_MF, &
  & YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KLON, &
  & YDMODEL%YRML_PHY_MF%YRTOPH%NTNEBU, YDCPG_OPTS%KFLEVG,&
  & YDMF_PHYS_BASE_STATE%YCPG_DYN%PHI, YDMF_PHYS_BASE_STATE%YCPG_DYN%PHIF, YDMF_PHYS_BASE_STATE%YCPG_PHY%PREF, &
  & ZDECRD, ZNEB0, &
  & YDMF_PHYS%OUT%CLCH, YDMF_PHYS%OUT%CLCM, YDMF_PHYS%OUT%CLCL, &
  & YDCPG_MISC%CLCT, ZCLCT_RAD)

  IF (LHOOK) CALL DR_HOOK('APL_AROME_ADJUST_MOD:APL_AROME_ADJUST', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_AROME_ADJUST
