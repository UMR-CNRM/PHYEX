SUBROUTINE ARP_SHALLOW_MF(YDCST,YDPHYEX,YDCPG_OPTS,YDCPG_BNDS,&
                        & PTSPHY,PZZ,PZZF,PR,&
                        & PCP,PU,PV,PT,PQV,PQL,PQI,PQR,PQS,PTKE, &
                        & PAPRSF,PDELP,PDIFTQ,PDIFTS,PSTRTU,PSTRTV,YDMF_PHYS_SURF,&
                        & PRODTH_CVPP,PQLI,PNEB,KNLAB,PMF_UP)


!     ##########################################################################
!
!!****  * -  interface to call SHALLOW_MF :
!!             computation of turbulence "mass flux" fluxes and their divergence
!!
!!
!!
!!    PURPOSE
!!    -------
!!
!!
!!
!!
!!**  METHOD
!!    ------
!!
!!
!!
!!    EXTERNAL
!!    --------
!!      Subroutine SHALLOW_MF (PHYEX subroutine)
!!
!!    IMPLICIT ARGUMENTS
!!    ------------------
!!
!!    REFERENCE
!!    ---------
!!
!!      Documentation ARPEGE
!!
!!    AUTHOR
!!    ------
!!    Y.Bouteloup from aro_shallow_mf
!!
!!    MODIFICATIONS
!!    -------------
!!      Original    11/2010
!!      S. Riette shallow_mf now outputs ice cloud
!!      S. Riette Jan 2012: support for both order of vertical levels
!!      S. Riette April 2022: call abort, waiting for an update from an arpege developper...
!!      Y.Bouteloup September 2023 : Rewriting for PHYEX
!!      A. Marcel Jan 2025: EDMF contribution to dynamic TKE production
!!      A. Marcel Jan 2025: TKE mixing
!!      A. Marcel Jan 2025: bi-Gaussian PDF and associated subgrid precipitation
!!
!-------------------------------------------------------------------------------
!
!*       0.    DECLARATIONS
!              ------------

USE PARKIND1          , ONLY : JPIM, JPRB
USE YOMHOOK           , ONLY : DR_HOOK, JPHOOK, LHOOK
USE YOMCST            , ONLY : TCST
USE YOMLSFORC         , ONLY : LMUSCLFA, NMUSCLFA
USE MODD_PHYEX        , ONLY : PHYEX_T
USE MODD_DIMPHYEX     , ONLY : DIMPHYEX_T
USE MODI_SHALLOW_MF   , ONLY : SHALLOW_MF
USE MODE_FILL_DIMPHYEX, ONLY : FILL_DIMPHYEX
USE MF_PHYS_SURFACE_TYPE_MOD &
                    & , ONLY : MF_PHYS_SURF_TYPE
USE CPG_OPTS_TYPE_MOD , ONLY : CPG_BNDS_TYPE, CPG_OPTS_TYPE

IMPLICIT NONE

!*       0.1   Declarations of dummy arguments :


TYPE(TCST)              ,INTENT(IN)     :: YDCST                               
TYPE(PHYEX_T)           ,INTENT(IN)     :: YDPHYEX                               
TYPE(CPG_OPTS_TYPE)     ,INTENT(IN)     :: YDCPG_OPTS
TYPE(CPG_BNDS_TYPE)     ,INTENT(IN)     :: YDCPG_BNDS
REAL(KIND=JPRB)         ,INTENT(IN)     :: PTSPHY                                            ! Time step
REAL(KIND=JPRB)         ,INTENT(IN)     :: PZZ(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG)          ! Height of layer boundaries 
REAL(KIND=JPRB)         ,INTENT(IN)     :: PZZF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)           ! Height of level
REAL(KIND=JPRB)         ,INTENT(IN)     :: PR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)             ! Air gaz constant
REAL(KIND=JPRB)         ,INTENT(IN)     :: PCP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)            ! Cp
REAL(KIND=JPRB)         ,INTENT(IN)     :: PU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PT(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PQV(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PQL(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PQI(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PQR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PQS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PTKE(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PAPRSF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(IN)     :: PDELP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(INOUT)  :: PDIFTQ(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(INOUT)  :: PDIFTS(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(INOUT)  :: PSTRTU(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(INOUT)  :: PSTRTV(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(OUT)    :: PRODTH_CVPP(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
REAL(KIND=JPRB)         ,INTENT(OUT)    :: PQLI(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(OUT)    :: PNEB(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
INTEGER(KIND=JPIM)      ,INTENT(OUT)    :: KNLAB(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)   
REAL(KIND=JPRB)         ,INTENT(OUT)    :: PMF_UP(YDCPG_OPTS%KLON,0:YDCPG_OPTS%KFLEVG) 
TYPE(MF_PHYS_SURF_TYPE) ,INTENT(IN)     :: YDMF_PHYS_SURF

INTEGER(KIND=JPIM) :: IKL       
INTEGER(KIND=JPIM) :: IKR       
INTEGER(KIND=JPIM) :: IKRL      
INTEGER(KIND=JPIM) :: IKRI      
INTEGER(KIND=JPIM) :: JLON, JLEV
INTEGER(KIND=JPIM) :: ISV_LGBEG, ISV_LGEND, ITCOUNT
REAL(KIND=JPRB)    :: ZINVG, ZTDCP, ZVMD

INTEGER(KIND=JPIM) :: IKLCL      (YDCPG_OPTS%KLON)        
INTEGER(KIND=JPIM) :: IKETL      (YDCPG_OPTS%KLON)       
INTEGER(KIND=JPIM) :: IKCTL      (YDCPG_OPTS%KLON)       
REAL(KIND=JPRB)    :: ZFLXZTHMF  (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZFLXZRMF   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZFLXZUMF   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZFLXZVMF   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZFLXZTKEMF (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)    :: ZEMF       (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZDETR      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZENTR      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZDZZ       (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZZZ        (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZZZF       (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZTHETA     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZEXNER     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZHRO       (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZHRODJ     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZHRODREF   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZRM        (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,5)
REAL(KIND=JPRB)    :: ZSFTH      (YDCPG_OPTS%KLON)       
REAL(KIND=JPRB)    :: ZSFRV      (YDCPG_OPTS%KLON)       
REAL(KIND=JPRB)    :: ZSVM       (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,1)
REAL(KIND=JPRB)    :: ZSVDT_MF   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG,1)
REAL(KIND=JPRB)    :: ZDUDT_MF   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! tendency of U   by massflux scheme
REAL(KIND=JPRB)    :: ZDVDT_MF   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! tendency of V   by massflux scheme
REAL(KIND=JPRB)    :: ZDTKEDT_MF (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! tendency of TKE by massflux scheme
REAL(KIND=JPRB)    :: ZDTHLDT_MF (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! tendency of thl by massflux scheme
REAL(KIND=JPRB)    :: ZDRTDT_MF  (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! tendency of rt  by massflux scheme
REAL(KIND=JPRB)    :: ZSIGMF     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! cloud info for the cloud scheme
REAL(KIND=JPRB)    :: ZFLXZTHVMF (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! Thermal production for TKE scheme
REAL(KIND=JPRB)    :: ZTHL_UP    (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! Thl updraft characteristics
REAL(KIND=JPRB)    :: ZRT_UP     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! Rt  updraft characteristics
REAL(KIND=JPRB)    :: ZRV_UP     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! Vapor updraft characteristics
REAL(KIND=JPRB)    :: ZU_UP      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! U wind updraft characteristics
REAL(KIND=JPRB)    :: ZV_UP      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! V wind updraft characteristics
REAL(KIND=JPRB)    :: ZTKE_UP    (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! TKE updraft characteristics
REAL(KIND=JPRB)    :: ZRC_UP     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! cloud content updraft characteristics
REAL(KIND=JPRB)    :: ZRI_UP     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! ice content   updraft characteristics
REAL(KIND=JPRB)    :: ZTHV_UP    (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! Thv   updraft characteristics
REAL(KIND=JPRB)    :: ZW_UP      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! vertical speed updraft characteristics
REAL(KIND=JPRB)    :: ZFRAC_UP   (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! updraft fraction
REAL(KIND=JPRB)    :: ZDPSG      (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! Delta P / g
REAL(KIND=JPRB)    :: ZFQ_MF     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! Flux de qv by massflux scheme
REAL(KIND=JPRB)    :: ZFH_MF     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)                   ! Flux d'hentalpy by massflux scheme
REAL(KIND=JPRB)    :: ZFU_MF     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZFV_MF     (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZQDM       (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZTKE       (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZU         (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZV         (YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)  
REAL(KIND=JPRB)    :: ZHLC_HRC_MF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)    :: ZHLC_HCF_MF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)    :: ZHLI_HRI_MF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)    :: ZHLI_HCF_MF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
REAL(KIND=JPRB)    :: ZWEIGHT_MF_CLOUD(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

TYPE(DIMPHYEX_t) :: YLDIMPHYEX

#include "wrscmr.intfb.h"

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK ('ARP_SHALLOW_MF', 0, ZHOOK_HANDLE)

!------------------------------------------------------------------------------

!*       1.     PRELIMINARY COMPUTATIONS
!               ------------------------

CALL FILL_DIMPHYEX (YLDIMPHYEX, YDCPG_OPTS%KLON, 1, YDCPG_OPTS%KFLEVG, 0, YDCPG_BNDS%KFDIA)

IKR=5         ! <== Number of water species
IKRL=2
IKRI=2
ZINVG = 1./YDCST%RG
ISV_LGBEG   =  0
ISV_LGEND   =  0
ITCOUNT     =  1


IF (YDCPG_OPTS%NSTEP == 0) THEN
  YDMF_PHYS_SURF%GSD_SFL%PGROUP(:,:) = 0.0_JPRB
ENDIF

!------------------------------------------------------------------------------

!*       2.   INITIALISATION

!             ---------------

! AROME type initialisation

DO JLEV = 1, YDCPG_OPTS%KFLEVG
  DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
    ZZZ(JLON,JLEV)  = PZZ(JLON,JLEV)*ZINVG
    ZZZF(JLON,JLEV) = PZZF(JLON,JLEV)*ZINVG
  ENDDO
ENDDO


DO JLEV = 2 , YDCPG_OPTS%KFLEVG
   DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      ZDZZ(JLON,JLEV)=ZZZF(JLON,JLEV-1) - ZZZF(JLON,JLEV)
   ENDDO
ENDDO
DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
  ZDZZ(JLON,1)=ZDZZ(JLON,2)
ENDDO


! Some initialisations
DO JLEV = 1, YDCPG_OPTS%KFLEVG
  DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      ZEXNER(JLON,JLEV)=(PAPRSF(JLON,JLEV)/YDCST%RATM)**YDCST%RKAPPA
      ZTHETA(JLON,JLEV)=PT(JLON,JLEV)/ZEXNER(JLON,JLEV)   
      ZHRO(JLON,JLEV)=PAPRSF(JLON,JLEV)/(PT(JLON,JLEV)*PR(JLON,JLEV))
      ZQDM(JLON,JLEV)=(1.-PQV(JLON,JLEV)-PQL(JLON,JLEV)-PQI(JLON,JLEV)-PQR(JLON,JLEV)-PQS(JLON,JLEV))

      ZHRODREF(JLON,JLEV)=ZHRO(JLON,JLEV)*ZQDM(JLON,JLEV)
      ZHRODJ(JLON,JLEV)=PDELP(JLON,JLEV)*ZINVG
      ZRM(JLON,JLEV,1)=PQV(JLON,JLEV)/ZQDM(JLON,JLEV)
      ZRM(JLON,JLEV,2)=PQL(JLON,JLEV)/ZQDM(JLON,JLEV)
      ZRM(JLON,JLEV,3)=PQR(JLON,JLEV)/ZQDM(JLON,JLEV)
      ZRM(JLON,JLEV,4)=PQI(JLON,JLEV)/ZQDM(JLON,JLEV)
      ZRM(JLON,JLEV,5)=PQS(JLON,JLEV)/ZQDM(JLON,JLEV)

      ZDPSG(JLON,JLEV) = MAX(1.E-15,PDELP(JLON,JLEV)/YDCST%RG)
   ENDDO
ENDDO

ZSVM(:,:,:)=0.

DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
  ZSFTH(JLON) = -YDMF_PHYS_SURF%GSD_SFL%PGROUP(JLON, 1)/ZHRO(JLON,YDCPG_OPTS%KFLEVG)/YDCST%RCPD
  ZSFRV(JLON) = -YDMF_PHYS_SURF%GSD_SFL%PGROUP(JLON, 2)/ZHRO(JLON,YDCPG_OPTS%KFLEVG)
ENDDO

!  set tendencies to 0 

ZDUDT_MF(:,:)   = 0.
ZDVDT_MF(:,:)   = 0.
ZDTKEDT_MF(:,:) = 0.
ZDTHLDT_MF(:,:) = 0.
ZDRTDT_MF(:,:)  = 0.
PMF_UP(:,:)     = 0.
ZEMF(:,:)       = 0.
PQLI(:,:)       = 0.
KNLAB (:,:)     = 0
ZRC_UP(:,:)     = 0.
ZRI_UP(:,:)     = 0.
ZRT_UP(:,:)     = 0.
ZFLXZTHVMF(:,:) = 0.
ZFLXZTHMF(:,:)  = 0.
ZFLXZTHMF(:,:)  = 0.
ZFLXZUMF(:,:)   = 0.
ZFLXZVMF(:,:)   = 0.
ZFLXZTKEMF(:,:) = 0.

!
!------------------------------------------------------------------------------
!
!
!*       3.   CALL TO PHYEX SHALLOW CONVECTION
!
!         ---------------------------------

CALL SHALLOW_MF(YLDIMPHYEX, YDPHYEX%CST, YDPHYEX%NEBN, YDPHYEX%PARAM_MFSHALLN, YDPHYEX%TURBN, YDPHYEX%CSTURB, YDPHYEX%RAIN_ICE_PARAMN, &
     KRR=IKR,KRRL=IKRL,KRRI=IKRI, KSV=1,                               &
     ONOMIXLG=YDPHYEX%MISC%ONOMIXLG,KSV_LGBEG=ISV_LGBEG,KSV_LGEND=ISV_LGEND,      &
     PTSTEP=PTSPHY,                                                        &
     PDZZ=ZDZZ,PZZ=ZZZ,                                                 &
     PRHODJ=ZHRODJ,PRHODREF=ZHRODREF,                                   &
     PPABSM=PAPRSF,PEXNM=ZEXNER,                                        &
     PSFTH=ZSFTH,PSFRV=ZSFRV,                                           &
     PTHM=ZTHETA,PRM=ZRM,PUM=PU,PVM=PV,PTKEM=PTKE,PSVM=ZSVM,            &
!  Output
     PDUDT_MF=ZDUDT_MF,PDVDT_MF=ZDVDT_MF,PDTKEDT_MF=ZDTKEDT_MF,                &
     PDTHLDT_MF=ZDTHLDT_MF,PDRTDT_MF=ZDRTDT_MF,PDSVDT_MF=ZSVDT_MF,             &
     PSIGMF=ZSIGMF,PRC_MF=ZRC_UP,PRI_MF=ZRI_UP,PCF_MF=PNEB, &
     PHLC_HRC=ZHLC_HRC_MF, PHLC_HCF=ZHLC_HCF_MF, PHLI_HRI=ZHLI_HRI_MF, PHLI_HCF=ZHLI_HCF_MF, &
     PWEIGHT_MF_CLOUD=ZWEIGHT_MF_CLOUD, PFLXZTHVMF=ZFLXZTHVMF, &
     PFLXZTHMF=ZFLXZTHMF,PFLXZRMF=ZFLXZRMF,PFLXZUMF=ZFLXZUMF,PFLXZVMF=ZFLXZVMF,PFLXZTKEMF=ZFLXZTKEMF,&
     PTHL_UP=ZTHL_UP,PRT_UP=ZRT_UP,PRV_UP=ZRV_UP,PRC_UP=ZRC_UP,PRI_UP=ZRI_UP,  &
     PU_UP=ZU_UP, PV_UP=ZV_UP, PTKE_UP=ZTKE_UP, PTHV_UP=ZTHV_UP, PW_UP=ZW_UP,                   &
     PFRAC_UP=ZFRAC_UP,PEMF=ZEMF,PDETR=ZDETR,PENTR=ZENTR,                      &
     KKLCL=IKLCL,KKETL=IKETL,KKCTL=IKCTL,                                      &
!
     PDX=0._JPRB, PDY=0._JPRB, KBUDGETS=0)


IF (LMUSCLFA) THEN
  CALL WRSCMR(NMUSCLFA, 'PEMF_CVPP', ZEMF, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  CALL WRSCMR(NMUSCLFA, 'PWUP_CVPP', ZW_UP, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  CALL WRSCMR(NMUSCLFA, 'PFRAC_CVPP', ZFRAC_UP, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  CALL WRSCMR(NMUSCLFA, 'ZDRTDT_MF', ZDRTDT_MF, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  CALL WRSCMR(NMUSCLFA, 'ZDTHLDT_MF', ZDTHLDT_MF, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  CALL WRSCMR(NMUSCLFA, 'PNEB_CVPP', PNEB, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
ENDIF

!  Conversion des tendances de theta en tendance de cpT
!  et conversion en qi en multipliant par qd
! Puis calcul des flux

ZFQ_MF(:,:) = 0.
ZFH_MF(:,:) = 0.
ZFU_MF(:,:) = 0.
ZFV_MF(:,:) = 0.

ZVMD=YDCST%RCPV-YDCST%RCPD

DO JLEV = 2,YDCPG_OPTS%KFLEVG
  DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA

      ZFQ_MF(JLON,JLEV) = ZFQ_MF(JLON,JLEV-1) - ZDPSG(JLON,JLEV)*ZDRTDT_MF(JLON,JLEV)*ZQDM(JLON,JLEV)
      ZTDCP=ZVMD*ZDRTDT_MF(JLON,JLEV)

      ZFH_MF(JLON,JLEV) = ZFH_MF(JLON,JLEV-1) - ZDPSG(JLON,JLEV) &
      & * (ZDTHLDT_MF(JLON,JLEV)*ZEXNER(JLON,JLEV)*(PCP(JLON,JLEV)+PTSPHY*ZTDCP)+PT(JLON,JLEV)*ZTDCP)

      ZFU_MF(JLON,JLEV) = ZFU_MF(JLON,JLEV-1) - ZDPSG(JLON,JLEV)*ZDUDT_MF(JLON,JLEV)
      ZFV_MF(JLON,JLEV) = ZFV_MF(JLON,JLEV-1) - ZDPSG(JLON,JLEV)*ZDVDT_MF(JLON,JLEV)
   ENDDO
ENDDO

PRODTH_CVPP(:,:) = 0.

! stockage dans les flux turbulents (Inversion des niveaux !)

DO JLEV = 1, YDCPG_OPTS%KFLEVG     ! Loop from top to bottom
  DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA 
      PDIFTQ(JLON,JLEV) = PDIFTQ(JLON,JLEV) + ZFQ_MF(JLON,JLEV)
      PDIFTS(JLON,JLEV) = PDIFTS(JLON,JLEV) + ZFH_MF(JLON,JLEV)
      PSTRTU(JLON,JLEV) = PSTRTU(JLON,JLEV) + ZFU_MF(JLON,JLEV)
      PSTRTV(JLON,JLEV) = PSTRTV(JLON,JLEV) + ZFV_MF(JLON,JLEV)

      PRODTH_CVPP(JLON,JLEV) = YDCST%RG/ZTHETA(JLON,JLEV)*ZFLXZTHVMF(JLON,JLEV)

! Shallow cloud information     
      PQLI  (JLON,JLEV) =  (ZRC_UP(JLON,JLEV)+ZRI_UP(JLON,JLEV))/(1.+ZRT_UP(JLON,JLEV)) ! with HFRAC_ICE='N', ZRI_MF=0
      KNLAB (JLON,JLEV) =  INT(MAX(0._JPRB,SIGN(1._JPRB,PQLI(JLON,JLEV)-1.E-8)))
      PMF_UP(JLON,JLEV) = -ZEMF(JLON,JLEV)/ZHRODREF(JLON,JLEV) ! <== On ne sort pas le flux de masse mais
   ENDDO
ENDDO

IF (LMUSCLFA) THEN
  CALL WRSCMR(NMUSCLFA, 'PQLI_CVPP', PQLI, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
ENDIF

IF (LHOOK) CALL DR_HOOK ('ARP_SHALLOW_MF', 1, ZHOOK_HANDLE)

END SUBROUTINE ARP_SHALLOW_MF

