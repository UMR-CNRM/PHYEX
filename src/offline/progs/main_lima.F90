PROGRAM MAIN_LIMA

USE XRD_GETOPTIONS,  ONLY: INITOPTIONS, GETOPTION, CHECKOPTIONS
USE GETDATA_LIMA_MOD, ONLY: GETDATA_LIMA
USE COMPUTE_DIFF,    ONLY: DIFF
USE MODI_LIMA,       ONLY: LIMA
USE MODD_DIMPHYEX,   ONLY: DIMPHYEX_t
USE MODD_PHYEX,      ONLY: PHYEX_t
USE STACK_MOD,       ONLY: STACK
USE MODE_MNH_ZWORK,  ONLY: ZMNH_STACK, IMNH_BLOCK, YMNH_STACK, INUMPIN
USE OMP_LIB
USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK
#ifdef _OPENACC
USE MODD_UTIL_PHYEX_T, ONLY: COPY_PHYEX_T, WIPE_PHYEX_T
#endif

IMPLICIT NONE

INTEGER      :: KLEV
INTEGER      :: KRR, KSV

REAL, ALLOCATABLE   :: PRHODREF     (:,:,:) 
REAL, ALLOCATABLE   :: PEXNREF      (:,:,:)
REAL, ALLOCATABLE   :: PDZZ         (:,:,:)
REAL, ALLOCATABLE   :: PRHODJ       (:,:,:)
REAL, ALLOCATABLE   :: PPABSM       (:,:,:)
REAL, ALLOCATABLE   :: PDTHRAD      (:,:,:)
REAL, ALLOCATABLE   :: PTHT         (:,:,:)
REAL, ALLOCATABLE   :: PRT          (:,:,:,:)
REAL, ALLOCATABLE   :: PSVT         (:,:,:,:)
REAL, ALLOCATABLE   :: PW_NU        (:,:,:)
REAL, ALLOCATABLE   :: PTHS         (:,:,:)
REAL, ALLOCATABLE   :: PRS          (:,:,:,:)
REAL, ALLOCATABLE   :: PSVS         (:,:,:,:)
REAL, ALLOCATABLE   :: PCLDFR       (:,:,:)
REAL, ALLOCATABLE   :: PICEFR       (:,:,:)
REAL, ALLOCATABLE   :: PPRCFR       (:,:,:)
REAL, ALLOCATABLE   :: PFPR         (:,:,:,:)
REAL, ALLOCATABLE   :: PTHS_OUT     (:,:,:)
REAL, ALLOCATABLE   :: PRS_OUT      (:,:,:,:)
REAL, ALLOCATABLE   :: PSVS_OUT     (:,:,:,:)
REAL, ALLOCATABLE   :: ZINPRC_OUT   (:,:)
REAL, ALLOCATABLE   :: ZINDEP_OUT   (:,:)
REAL, ALLOCATABLE   :: PINPRR_OUT   (:,:)
REAL, ALLOCATABLE   :: ZINPRI_OUT   (:,:)
REAL, ALLOCATABLE   :: PINPRS_OUT   (:,:)
REAL, ALLOCATABLE   :: PINPRG_OUT   (:,:)
REAL, ALLOCATABLE   :: PINPRH_OUT   (:,:)
REAL, ALLOCATABLE   :: PEVAP_OUT    (:,:,:)
REAL, ALLOCATABLE   :: PCLDFR_OUT   (:,:,:)
REAL, ALLOCATABLE   :: PICEFR_OUT   (:,:,:)
REAL, ALLOCATABLE   :: PPRCFR_OUT   (:,:,:)
REAL, ALLOCATABLE   :: PFPR_OUT     (:,:,:,:)
REAL, ALLOCATABLE   :: ZINPRC       (:,:)
REAL, ALLOCATABLE   :: ZINDEP       (:,:)
REAL, ALLOCATABLE   :: PINPRR       (:,:)
REAL, ALLOCATABLE   :: ZINPRI       (:,:)
REAL, ALLOCATABLE   :: PINPRS       (:,:)
REAL, ALLOCATABLE   :: PINPRG       (:,:)
REAL, ALLOCATABLE   :: PINPRH       (:,:)
REAL, ALLOCATABLE   :: PEVAP        (:,:,:)
REAL, ALLOCATABLE   :: PAERO        (:,:,:,:)
REAL, ALLOCATABLE   :: PSOLORG        (:,:,:,:)
REAL, ALLOCATABLE   :: PMI        (:,:,:,:)

INTEGER :: NPROMA, NGPBLKS, NFLEVG
INTEGER :: JLON, JLEV
INTEGER, TARGET :: IBL

TYPE(DIMPHYEX_t)         :: D, D0
TYPE(PHYEX_t)            :: PHYEX
LOGICAL                  :: LLCHECK
LOGICAL                  :: LLCHECKDIFF
LOGICAL                  :: LLDIFF
INTEGER                  :: IBLOCK1, IBLOCK2
INTEGER                  :: ISTSZ(2), JBLK1, JBLK2
INTEGER                  :: NTID, ITID
INTEGER                  :: JRR
REAL                     :: ZTHVREFZIKB! for electricity use only
INTEGER                  :: NMOD_CCN, NMOD_IFN, NMOD_IMM
INTEGER                  :: NSP, NCARB, NSOA
CHARACTER(LEN=4)         :: HACTCCN ! kind of CCN activation

REAL, ALLOCATABLE, TARGET :: PSTACK8(:,:)
REAL(KIND=4), ALLOCATABLE, TARGET :: PSTACK4(:,:)
TYPE(STACK), TARGET :: YLSTACK

REAL(KIND=8) :: TS,TE
REAL(KIND=8) :: TSC, TEC, TSD, TED, ZTC, ZTD 
INTEGER :: ITIME, NTIME
INTEGER :: IRANK, ISIZE
LOGICAL :: LLVERBOSE, LLSTAT, LLBIND
REAL (KIND=JPHOOK) :: ZHOOK_HANDLE
CHARACTER(LEN=32) :: CLTEXT

CALL INITOPTIONS ()
NGPBLKS = 296
CALL GETOPTION ("--blocks", NGPBLKS)
NPROMA = 32
CALL GETOPTION ("--nproma", NPROMA)
NFLEVG = -1
CALL GETOPTION ("--nflevg", NFLEVG)
CALL GETOPTION ("--check",  LLCHECK)
CALL GETOPTION ("--checkdiff",  LLCHECKDIFF)
IBLOCK1 = 1
CALL GETOPTION ("--check-block-1", IBLOCK1)
IBLOCK2 = NGPBLKS
CALL GETOPTION ("--check-block-2", IBLOCK2)
CALL GETOPTION ("--stat", LLSTAT)
NTIME = 1
CALL GETOPTION ("--times", NTIME)
CALL GETOPTION ("--verbose", LLVERBOSE)
CALL GETOPTION ("--bind", LLBIND)
CALL CHECKOPTIONS ()

LLDIFF = .FALSE.
! En attendant un init d'ORILAM externalisÃ©
HACTCCN='   '

IRANK = 0
ISIZE = 1
IF (LLBIND) THEN
  CALL LINUX_BIND      (IRANK, ISIZE)
  CALL LINUX_BIND_DUMP (IRANK, ISIZE)
ENDIF

CALL GETDATA_LIMA (NPROMA, NGPBLKS, NFLEVG, KRR, KSV, NMOD_CCN, NMOD_IFN, NMOD_IMM, NSP, NCARB, NSOA, &
                  &ZTHVREFZIKB, PRHODREF, PEXNREF, PDZZ, PRHODJ, PPABSM, PDTHRAD, PTHT, &
                  &PRT, PSVT, PW_NU, PTHS, PRS, PSVS, PCLDFR, PICEFR, PPRCFR, PFPR, &
                  &PTHS_OUT, PRS_OUT, PSVS_OUT, ZINPRC_OUT, ZINDEP_OUT, PINPRR_OUT, ZINPRI_OUT, &
                  &PINPRS_OUT, PINPRG_OUT, PINPRH_OUT, PEVAP_OUT, PCLDFR_OUT, PICEFR_OUT, &
                  &PPRCFR_OUT, PFPR_OUT, &
                  &ZINPRC, ZINDEP, PINPRR, ZINPRI, PINPRS, PINPRG, PINPRH, PEVAP, &
                  &PAERO, PSOLORG, PMI, &
                  &LLVERBOSE)

KLEV = SIZE (PRS, 2)

IF (LLVERBOSE) PRINT *, " KLEV = ", KLEV, " KRR = ", KRR

PRINT *, " NPROMA = ", NPROMA, " KLEV = ", KLEV, " NGPBLKS = ", NGPBLKS

#ifndef _OPENACC
! Code is not yet ready for GPU
CALL INIT_PHYEX(KRR, PHYEX)
#endif

D0%NIT  = NPROMA
D0%NIB  = 1
D0%NIE  = NPROMA
D0%NJT  = 1
D0%NJB  = 1
D0%NJE  = 1
D0%NIJT = D0%NIT * D0%NJT
D0%NIJB = 1
D0%NIJE = NPROMA
D0%NKL  = -1
D0%NKT  = KLEV
D0%NKA  = KLEV
D0%NKU  = 1
D0%NKB  = KLEV 
D0%NKE  = 1
D0%NKTB = 1
D0%NKTE = KLEV

ISTSZ=0
ISTSZ(KIND(PRHODJ)/4) = NPROMA * 15 * KLEV
#ifndef USE_STACK
ISTSZ(2) = ISTSZ(2) + CEILING(ISTSZ(1) / 2.)
ISTSZ(1) = 0
#endif
ALLOCATE (PSTACK4 (ISTSZ(1), NGPBLKS))
ALLOCATE (PSTACK8 (ISTSZ(2), NGPBLKS))
ZMNH_STACK => PSTACK8

TS = OMP_GET_WTIME ()

ZTD = 0.
ZTC = 0.

IF (LHOOK) CALL DR_HOOK ('MAIN',0,ZHOOK_HANDLE)

DO ITIME = 1, NTIME

  TSD = OMP_GET_WTIME ()

#ifdef _OPENACC
  CALL COPY_PHYEX_T(PHYEX)
#endif

!$acc data &
!$acc      & copyin  (D0, &
!$acc      &          PRHODREF, PEXNREF, PDZZ, PRHODJ, PPABSM, PDTHRAD, PTHT, &
!$acc      &          PRT, PSVT, PW_NU) &
!$acc      & copy    (PRS, PSVS, PTHS, PCLDFR, PICEFR, PPRCFR, PFPR) &
!$acc      & copyout (ZINPRC, ZINDEP, PINPRR, ZINPRI, PINPRS, PINPRG, PINPRH, &
!$acc      &          PEVAP) &
!$acc      & create  (PSTACK4, PSTACK8) 

  TSC = OMP_GET_WTIME ()

#ifdef USE_OPENMP
!$OMP PARALLEL PRIVATE (D, YLSTACK, ITID, JBLK1, JBLK2)

  NTID = OMP_GET_MAX_THREADS ()
  ITID = OMP_GET_THREAD_NUM ()
  JBLK1 = 1 +  (NGPBLKS * (ITID+0)) / NTID
  JBLK2 =      (NGPBLKS * (ITID+1)) / NTID
#else
  JBLK1 = 1
  JBLK2 = NGPBLKS
#endif

  D = D0

!$acc parallel loop gang vector present (PHYEX) private (YLSTACK, IBL, JLON, D) collapse (2)

  DO IBL = JBLK1, JBLK2

#ifdef USE_COLCALL
  DO JLON = 1, NPROMA
    D = D0
    D%NIB = JLON
    D%NIE = JLON
    D%NIJB = JLON
    D%NIJE = JLON
#endif

#ifdef USE_STACK
    !Using cray pointers, AROME mechanism
    YLSTACK%L(1) = LOC (PSTACK4 (1, IBL))
    YLSTACK%U(1) = YLSTACK%L(1) + ISTSZ(1) * KIND (PSTACK4)
    YLSTACK%L(2) = LOC (PSTACK8 (1, IBL))
    YLSTACK%U(2) = YLSTACK%L(2) + ISTSZ(2) * KIND (PSTACK8)
#else
    !Using fortran indexing, Meso-NH mechanism
    YLSTACK%L(2) = 1
    YLSTACK%U(2) = ISTSZ(2)
    IMNH_BLOCK => IBL
    YMNH_STACK => YLSTACK
    INUMPIN = 0
#endif

#ifdef _OPENACC
    ! Code is not yet ready for GPU
    ZINPRC(JLON, IBL)=0.
    ZINDEP(JLON, IBL)=0.
    PINPRR(JLON, IBL)=0.
    ZINPRI(JLON, IBL)=0.
    PINPRS(JLON, IBL)=0.
    PINPRG(JLON, IBL)=0.
    PINPRH(JLON, IBL)=0.
    PEVAP(JLON, :, IBL)=0.
#else
    CALL LIMA (D, PHYEX%CST, PHYEX%RAIN_ICE_DESCRN, PHYEX%RAIN_ICE_PARAMN,   &
               PHYEX%ELEC_DESCR, PHYEX%ELEC_PARAM, &
               PHYEX%MISC%TBUCONF, PHYEX%MISC%YLBUDGET, HACTCCN,PHYEX%MISC%NBUDGET, KRR, &
               PTSTEP=2*PHYEX%MISC%PTSTEP, OELEC=PHYEX%MISC%OELEC,                 &
               PRHODREF=PRHODREF(:, :, IBL), PEXNREF=PEXNREF(:, :, IBL), PDZZ=PDZZ(:, :, IBL), PTHVREFZIKB=ZTHVREFZIKB,       &
               PRHODJ=PRHODJ(:, :, IBL), PPABST=PPABSM(:, :, IBL),                                 &
               NCCN=NMOD_CCN, NIFN=NMOD_IFN, NIMM=NMOD_IMM,                   &
               ODTHRAD=.TRUE., PDTHRAD=PDTHRAD(:, :, IBL), PTHT=PTHT(:, :, IBL), PRT=PRT(:, :, :, IBL), PSVT=PSVT(:, :, :, IBL), &
               PW_NU=PW_NU(:, :, IBL),                  &
               PAERO=PAERO(:,:,:, IBL), PSOLORG=PSOLORG(:,:,:,IBL), PMI=PMI(:,:,:,IBL), &
               PTHS=PTHS(:, :, IBL), PRS=PRS(:, :, :, IBL), PSVS=PSVS(:, :, :, IBL),                                &
               PINPRC=ZINPRC(:, IBL), PINDEP=ZINDEP(:, IBL), PINPRR=PINPRR(:, IBL), PINPRI=ZINPRI(:, IBL), PINPRS=PINPRS(:, IBL), &
               PINPRG=PINPRG(:, IBL), PINPRH=PINPRH(:, IBL), &
               PEVAP3D=PEVAP(:, :, IBL), PCLDFR=PCLDFR(:, :, IBL), PICEFR=PICEFR(:, :, IBL), PPRCFR=PPRCFR(:, :, IBL), &
               PFPR=PFPR(:, :, :, IBL) &

#ifdef USE_STACK
    & , YDSTACK=YLSTACK &
#endif
    & )
#endif

#ifdef USE_COLCALL
    ENDDO
#endif

  ENDDO

#ifdef USE_OPENMP
!$OMP END PARALLEL
#endif

!$acc end parallel loop

  TEC = OMP_GET_WTIME ()

!$acc end data

#ifdef _OPENACC
  CALL WIPE_PHYEX_T(PHYEX)
#endif

  TED = OMP_GET_WTIME ()

  ZTC = ZTC + (TEC - TSC)
  ZTD = ZTD + (TED - TSD)

ENDDO

IF (LHOOK) CALL DR_HOOK ('MAIN',1,ZHOOK_HANDLE)

TE = OMP_GET_WTIME()

WRITE (*,'(A,F8.2,A)') 'elapsed time : ',TE-TS,' s'
WRITE (*,'(A,F8.4,A)') '          i.e. ',1000.*(TE-TS)/(NPROMA*NGPBLKS)/NTIME,' ms/gp'

PRINT *, " ZTD = ", ZTD, ZTD / REAL (NPROMA*NGPBLKS*NTIME)
PRINT *, " ZTC = ", ZTC, ZTC / REAL (NPROMA*NGPBLKS*NTIME)

IF (LLCHECK .OR. LLSTAT .OR. LLCHECKDIFF) THEN
  DO IBL = IBLOCK1, IBLOCK2
    PRINT *, " IBL = ", IBL
    CALL DIFF ("PTHS",     PTHS_OUT     (:,:,IBL),   PTHS     (:,:,IBL),   LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    !CALL DIFF ("PEVAP",    PEVAP_OUT    (:,:,IBL),   PEVAP    (:,:,IBL),   LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PCLDFR",   PCLDFR_OUT   (:,:,IBL),   PCLDFR   (:,:,IBL),   LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PICEFR",   PICEFR_OUT   (:,:,IBL),   PICEFR   (:,:,IBL),   LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("ZINPRC",   ZINPRC_OUT   (:,IBL),     ZINPRC   (:,IBL),     LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PINPRR",   PINPRR_OUT   (:,IBL),     PINPRR   (:,IBL),     LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("ZINPRI",   ZINPRI_OUT   (:,IBL),     ZINPRI   (:,IBL),     LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PINPRS",   PINPRS_OUT   (:,IBL),     PINPRS   (:,IBL),     LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PINPRG",   PINPRG_OUT   (:,IBL),     PINPRG   (:,IBL),     LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PINPRH",   PINPRH_OUT   (:,IBL),     PINPRH   (:,IBL),     LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PRS(1)",   PRS_OUT      (:,:,1,IBL), PRS      (:,:,1,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PRS(2)",   PRS_OUT      (:,:,2,IBL), PRS      (:,:,2,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PRS(3)",   PRS_OUT      (:,:,3,IBL), PRS      (:,:,3,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    DO JRR=1, KRR
      WRITE (CLTEXT, '("PRS JRR=",I3.3)') JRR
      CALL DIFF (CLTEXT,   PRS_OUT       (:,:,JRR,IBL), PRS      (:,:,JRR,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
      IF(JRR>=2) THEN                                                                                                               
        WRITE (CLTEXT, '("PFPR JRR=",I3.3)') JRR                                                                                    
        CALL DIFF (CLTEXT, PFPR_OUT      (:,:,JRR,IBL), PFPR     (:,:,JRR,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)       
      ENDIF
    ENDDO
    DO JRR=1, KSV
      WRITE (CLTEXT, '("PSVS JRR=",I3.3)') JRR
      CALL DIFF (CLTEXT, PSVS_OUT      (:,:,JRR,IBL), PSVS     (:,:,JRR,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    ENDDO
  ENDDO
ENDIF

IF (LLCHECKDIFF) THEN
  IF (LLDIFF) THEN
    PRINT*, "THERE ARE DIFF SOMEWHERE"
  ELSE
    PRINT*, "THERE IS NO DIFF AT ALL"
  ENDIF
ENDIF

STOP

CONTAINS

SUBROUTINE INIT_PHYEX(KRR, PHYEX)

USE MODD_BUDGET, ONLY: TBUCONF_ASSOCIATE, NBUDGET_RI, TBUCONF, LBU_ENABLE, LBUDGET_U, LBUDGET_V, LBUDGET_W, LBUDGET_TH, &
                       LBUDGET_TKE, LBUDGET_RV, LBUDGET_RC, LBUDGET_RR, LBUDGET_RI, LBUDGET_RS, LBUDGET_RG, LBUDGET_RH, LBUDGET_SV
USE MODD_PHYEX, ONLY: PHYEX_t
USE MODI_INI_PHYEX, ONLY: INI_PHYEX

IMPLICIT NONE

! -----------------------------------------------------------------------
!     DUMMY VARIABLES
INTEGER,          INTENT(IN)  :: KRR
TYPE(PHYEX_t),    INTENT(OUT) :: PHYEX

!-----------------------------------------------------------------------
!    LOCAL VARIABLES
INTEGER :: IULOUT, JRR
REAL :: ZDZMIN
CHARACTER(LEN=6) :: CPROGRAM
CHARACTER(LEN=4) :: CMICRO, CSCONV, CTURB
REAL             :: PTSTEP
! -----------------------------------------------------------------------

IULOUT=20
CPROGRAM='AROME'
ZDZMIN=20.
CMICRO='LIMA'
CSCONV='NONE'
CTURB='TKEL'
PTSTEP = 25.000000000000000
PHYEX%MISC%TPFILE%NLU=0

!Default values
CALL INI_PHYEX(CPROGRAM, PHYEX%MISC%TPFILE, .TRUE., IULOUT, 0, 1, &
              &PTSTEP, ZDZMIN, &
              &CMICRO, CSCONV, CTURB, &
              &LDDEFAULTVAL=.TRUE., LDREADNAM=.FALSE., LDCHECK=.FALSE., KPRINT=0, LDINIT=.FALSE., &
              &PHYEX_OUT=PHYEX)

!Control parameters
PHYEX%MISC%PTSTEP       = PTSTEP
PHYEX%MISC%LMFCONV      = .TRUE.
PHYEX%MISC%KRR          = KRR
PHYEX%MISC%OCOMPUTE_SRC = .TRUE.
PHYEX%MISC%OELEC        = .FALSE.

!Emulate the namelist reading
PHYEX%PARAM_LIMA%NMOM_C=2
PHYEX%PARAM_LIMA%NMOM_R=2
PHYEX%PARAM_LIMA%NMOM_I=1
PHYEX%PARAM_LIMA%NMOM_S=1
PHYEX%PARAM_LIMA%NMOM_G=1
PHYEX%PARAM_LIMA%NMOD_CCN=1
PHYEX%PARAM_LIMA%CCCN_MODES='COPT'
PHYEX%PARAM_LIMA%NMOD_IFN=0
PHYEX%PARAM_LIMA%LSNOW_T=.TRUE.
PHYEX%PARAM_LIMA%LMURAKAMI=.TRUE.
PHYEX%NEBN%LSUBG_COND   = .TRUE.
PHYEX%NEBN%LSIGMAS = .TRUE.
PHYEX%NEBN%CFRAC_ICE_ADJUST='T'
PHYEX%NEBN%CFRAC_ICE_SHALLOW_MF='T'
PHYEX%NEBN%VSIGQSAT=0.02

!Param initialisation
CALL INI_PHYEX(CPROGRAM, PHYEX%MISC%TPFILE, .TRUE., IULOUT, 0, 1, &
              &PTSTEP, ZDZMIN, &
              &CMICRO, CSCONV, CTURB, &
              &LDDEFAULTVAL=.FALSE., LDREADNAM=.FALSE., LDCHECK=.TRUE., KPRINT=2, LDINIT=.TRUE., &
              &PHYEX_IN=PHYEX, PHYEX_OUT=PHYEX)

!Budgets
CALL TBUCONF_ASSOCIATE
PHYEX%MISC%NBUDGET=NBUDGET_RI
DO JRR=1, PHYEX%MISC%NBUDGET
  PHYEX%MISC%YLBUDGET(JRR)%NBUDGET=JRR
ENDDO
LBU_ENABLE=.FALSE.                                                                                                       
LBUDGET_U=.FALSE.
LBUDGET_V=.FALSE.
LBUDGET_W=.FALSE.
LBUDGET_TH=.FALSE.
LBUDGET_TKE=.FALSE.
LBUDGET_RV=.FALSE.
LBUDGET_RC=.FALSE.
LBUDGET_RR=.FALSE.
LBUDGET_RI=.FALSE.
LBUDGET_RS=.FALSE.
LBUDGET_RG=.FALSE.
LBUDGET_RH=.FALSE.
LBUDGET_SV=.FALSE.
PHYEX%MISC%TBUCONF=TBUCONF

END SUBROUTINE INIT_PHYEX

END PROGRAM

