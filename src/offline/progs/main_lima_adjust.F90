PROGRAM MAIN_LIMA_ADJUST

USE XRD_GETOPTIONS,  ONLY: INITOPTIONS, GETOPTION, CHECKOPTIONS
USE GETDATA_LIMA_ADJUST_MOD, ONLY: GETDATA_LIMA_ADJUST
USE COMPUTE_DIFF,    ONLY: DIFF
USE MODI_LIMA_ADJUST_SPLIT
USE MODD_DIMPHYEX,   ONLY: DIMPHYEX_t
USE MODD_PHYEX,      ONLY: PHYEX_t
USE STACK_MOD,       ONLY: STACK
USE MODE_MNH_ZWORK,  ONLY: ZMNH_STACK, IMNH_BLOCK, YMNH_STACK, INUMPIN
USE OMP_LIB
USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK

#ifdef _OPENACC
USE MODD_UTIL_PHYEX_T, ONLY: COPY_PHYEX_T, WIPE_PHYEX_T
#endif

IMPLICIT NONE

INTEGER      :: KLEV
INTEGER      :: KRR, KSV

REAL, ALLOCATABLE   :: PRHODREF       (:,:,:)
REAL, ALLOCATABLE   :: PRHODJ         (:,:,:)
REAL, ALLOCATABLE   :: PEXNREF        (:,:,:)
REAL, ALLOCATABLE   :: PSIGS          (:,:,:)
REAL, ALLOCATABLE   :: PMFCONV        (:,:,:)
REAL, ALLOCATABLE   :: PPABSM         (:,:,:)
REAL, ALLOCATABLE   :: ZZZ            (:,:,:)
REAL, ALLOCATABLE   :: ZSIGQSAT       (:,:)   
REAL, ALLOCATABLE   :: PDTHRAD        (:,:,:)
REAL, ALLOCATABLE   :: PW_NU          (:,:,:)
REAL, ALLOCATABLE   :: PRT            (:,:,:,:)
REAL, ALLOCATABLE   :: PRS            (:,:,:,:)
REAL, ALLOCATABLE   :: PSVT           (:,:,:,:)
REAL, ALLOCATABLE   :: PSVS           (:,:,:,:)
REAL, ALLOCATABLE   :: PTHS           (:,:,:)
REAL, ALLOCATABLE   :: PRC_MF         (:,:,:)
REAL, ALLOCATABLE   :: PRI_MF         (:,:,:)
REAL, ALLOCATABLE   :: PCF_MF         (:,:,:)
REAL, ALLOCATABLE   :: PRS_OUT        (:,:,:,:)
REAL, ALLOCATABLE   :: PSVS_OUT       (:,:,:,:)
REAL, ALLOCATABLE   :: PTHS_OUT       (:,:,:)
REAL, ALLOCATABLE   :: PSRCS          (:,:,:)
REAL, ALLOCATABLE   :: PCLDFR         (:,:,:)
REAL, ALLOCATABLE   :: PICEFR         (:,:,:)
REAL, ALLOCATABLE   :: PSRCS_OUT      (:,:,:)
REAL, ALLOCATABLE   :: PCLDFR_OUT     (:,:,:)
REAL, ALLOCATABLE   :: PICEFR_OUT     (:,:,:)
REAL, ALLOCATABLE   :: PAERO        (:,:,:,:)
REAL, ALLOCATABLE   :: PSOLORG        (:,:,:,:)
REAL, ALLOCATABLE   :: PMI        (:,:,:,:)

INTEGER :: NPROMA, NGPBLKS, NFLEVG
INTEGER :: JLON, JLEV
INTEGER, TARGET :: IBL

TYPE(DIMPHYEX_t)         :: D, D0
TYPE(PHYEX_t)            :: PHYEX
LOGICAL                  :: LLCHECK
LOGICAL                  :: LLCHECKDIFF
LOGICAL                  :: LLDIFF
INTEGER                  :: IBLOCK1, IBLOCK2
INTEGER                  :: ISTSZ(2), JBLK1, JBLK2
INTEGER                  :: NTID, ITID
INTEGER                  :: NSP, NCARB, NSOA
CHARACTER(LEN=4)         :: HACTCCN ! kind of CCN activation
LOGICAL                  :: LDUST, LSALT, LORILAM 

REAL, ALLOCATABLE, TARGET :: PSTACK8(:,:)
REAL(KIND=4), ALLOCATABLE, TARGET :: PSTACK4(:,:)
TYPE(STACK), TARGET :: YLSTACK

REAL(KIND=8) :: TS,TE
REAL(KIND=8) :: TSC, TEC, TSD, TED, ZTC, ZTD 
INTEGER :: ITIME, NTIME
INTEGER :: IRANK, ISIZE
LOGICAL :: LLVERBOSE, LLSTAT, LLBIND
REAL (KIND=JPHOOK) :: ZHOOK_HANDLE

CALL INITOPTIONS ()
NGPBLKS = 296
CALL GETOPTION ("--blocks", NGPBLKS)
NPROMA = 32
CALL GETOPTION ("--nproma", NPROMA)
NFLEVG = -1
CALL GETOPTION ("--nflevg", NFLEVG)
CALL GETOPTION ("--check",  LLCHECK)
CALL GETOPTION ("--checkdiff",  LLCHECKDIFF)
IBLOCK1 = 1
CALL GETOPTION ("--check-block-1", IBLOCK1)
IBLOCK2 = NGPBLKS
CALL GETOPTION ("--check-block-2", IBLOCK2)
CALL GETOPTION ("--stat", LLSTAT)
NTIME = 1
CALL GETOPTION ("--times", NTIME)
CALL GETOPTION ("--verbose", LLVERBOSE)
CALL GETOPTION ("--bind", LLBIND)
CALL CHECKOPTIONS ()

LLDIFF = .FALSE.

! En attendant un init d'ORILAM externalisÃ©
HACTCCN='   '
LDUST=.FALSE.
LSALT=.FALSE.
LORILAM=.FALSE.

IRANK = 0
ISIZE = 1
IF (LLBIND) THEN
  CALL LINUX_BIND      (IRANK, ISIZE)
  CALL LINUX_BIND_DUMP (IRANK, ISIZE)
ENDIF

CALL GETDATA_LIMA_ADJUST (NPROMA, NGPBLKS, NFLEVG, KRR, KSV, NSP, NCARB, NSOA, &
                         &PRHODREF, PRHODJ, PEXNREF, ZSIGQSAT, PSIGS, PMFCONV, PPABSM, ZZZ, &                             
                         &PDTHRAD, PW_NU, PRT, PRS, PSVT, PSVS, PTHS, &                                         
                         &PRC_MF, PRI_MF, PCF_MF, PRS_OUT, PSVS_OUT, PTHS_OUT, &                                  
                         &PSRCS, PCLDFR, PICEFR, &
                         &PSRCS_OUT, PCLDFR_OUT, PICEFR_OUT, &
                         &PAERO, PSOLORG, PMI,&                                                          
                         &LLVERBOSE)

KLEV = SIZE (PRS, 2)

IF (LLVERBOSE) PRINT *, " KLEV = ", KLEV, " KRR = ", KRR

PRINT *, " NPROMA = ", NPROMA, " KLEV = ", KLEV, " NGPBLKS = ", NGPBLKS

#ifndef _OPENACC
! Code is not yet ready for GPU
CALL INIT_PHYEX(KRR, PHYEX)
#endif

D0%NIT  = NPROMA
D0%NIB  = 1
D0%NIE  = NPROMA
D0%NJT  = 1
D0%NJB  = 1
D0%NJE  = 1
D0%NIJT = D0%NIT * D0%NJT
D0%NIJB = 1
D0%NIJE = NPROMA
D0%NKL  = -1
D0%NKT  = KLEV
D0%NKA  = KLEV
D0%NKU  = 1
D0%NKB  = KLEV 
D0%NKE  = 1
D0%NKTB = 1
D0%NKTE = KLEV

ISTSZ=0
ISTSZ(KIND(PRHODJ)/4) = NPROMA * 15 * KLEV
#ifndef USE_STACK
ISTSZ(2) = ISTSZ(2) + CEILING(ISTSZ(1) / 2.)
ISTSZ(1) = 0
#endif
ALLOCATE (PSTACK4 (ISTSZ(1), NGPBLKS))
ALLOCATE (PSTACK8 (ISTSZ(2), NGPBLKS))
ZMNH_STACK => PSTACK8

TS = OMP_GET_WTIME ()

ZTD = 0.
ZTC = 0.

IF (LHOOK) CALL DR_HOOK ('MAIN',0,ZHOOK_HANDLE)

DO ITIME = 1, NTIME

  TSD = OMP_GET_WTIME ()

#ifdef _OPENACC
  CALL COPY_PHYEX_T(PHYEX)
#endif

!$acc data &
!$acc      & copyin  (D0, &
!$acc      &          PRHODREF, PRHODJ, PEXNREF, PSIGS, PMFCONV, PPABSM, ZZZ, &
!$acc      &          PDTHRAD, PW_NU, PRT, PSVT, PRC_MF, PRI_MF, PCF_MF) &
!$acc      & copy    (PRS, PSVS, PTHS) &
!$acc      & copyout (PSRCS, PCLDFR, PICEFR) &
!$acc      & create  (PSTACK4, PSTACK8) 

  TSC = OMP_GET_WTIME ()

#ifdef USE_OPENMP
!$OMP PARALLEL PRIVATE (D, YLSTACK, ITID, JBLK1, JBLK2)

  NTID = OMP_GET_MAX_THREADS ()
  ITID = OMP_GET_THREAD_NUM ()
  JBLK1 = 1 +  (NGPBLKS * (ITID+0)) / NTID
  JBLK2 =      (NGPBLKS * (ITID+1)) / NTID
#else
  JBLK1 = 1
  JBLK2 = NGPBLKS
#endif

  D = D0

!$acc parallel loop gang vector present (PHYEX) private (YLSTACK, IBL, JLON, D) collapse (2)

  DO IBL = JBLK1, JBLK2

#ifdef USE_COLCALL
  DO JLON = 1, NPROMA
    D = D0
    D%NIB = JLON
    D%NIE = JLON
    D%NIJB = JLON
    D%NIJE = JLON
#endif

#ifdef USE_STACK
    !Using cray pointers, AROME mechanism
    YLSTACK%L(1) = LOC (PSTACK4 (1, IBL))
    YLSTACK%U(1) = YLSTACK%L(1) + ISTSZ(1) * KIND (PSTACK4)
    YLSTACK%L(2) = LOC (PSTACK8 (1, IBL))
    YLSTACK%U(2) = YLSTACK%L(2) + ISTSZ(2) * KIND (PSTACK8)
#else
    !Using fortran indexing, Meso-NH mechanism
    YLSTACK%L(2) = 1
    YLSTACK%U(2) = ISTSZ(2)
    IMNH_BLOCK => IBL
    YMNH_STACK => YLSTACK
    INUMPIN = 0
#endif

#ifdef _OPENACC
    ! Code is not yet ready for GPU
    PSRCS(JLON, :, IBL)=0.
    PCLDFR(JLON, :, IBL)=0.
    PICEFR(JLON, :, IBL)=0.
#else
    CALL LIMA_ADJUST_SPLIT(PHYEX%PARAM_LIMA, PHYEX%PARAM_LIMA_WARM, PHYEX%TNSV, &
                           D, PHYEX%CST, PHYEX%NEBN, PHYEX%TURBN, PHYEX%MISC%TBUCONF, PHYEX%MISC%YLBUDGET, PHYEX%MISC%NBUDGET, &
                           KRR, PHYEX%NEBN%CCONDENS, PHYEX%NEBN%CLAMBDA3, &
                           NCARB, NSOA , NSP, LDUST, LSALT, LORILAM, &
                           PHYEX%NEBN%LSUBG_COND, PHYEX%NEBN%LSIGMAS, PHYEX%MISC%PTSTEP, ZSIGQSAT (:, IBL), &
                           PRHODREF(:, :, IBL), PRHODJ(:, :, IBL), PEXNREF(:, :, IBL), PSIGS(:, :, IBL), &
                           PHYEX%MISC%LMFCONV, PMFCONV(:, :, IBL), &
                           PPABSM(:, :, IBL), ZZZ(:, :, IBL), .TRUE., PDTHRAD(:, :, IBL), PW_NU(:, :, IBL), &
                           PRT(:, :, :, IBL), PRS(:, :, :, IBL), PSVT(:, :, :, IBL), PSVS(:, :, :, IBL), &
                           HACTCCN, PAERO(:,:,:, IBL), PSOLORG(:,:,:,IBL), PMI(:,:,:,IBL), &
                           PTHS(:, :, IBL), PHYEX%MISC%OCOMPUTE_SRC, PSRCS(:, :, IBL), PCLDFR(:, :, IBL), PICEFR(:, :, IBL), &
                           PRC_MF(:, :, IBL), PRI_MF(:, :, IBL), PCF_MF(:, :, IBL) &
#ifdef USE_STACK
    & , YDSTACK=YLSTACK &
#endif
    & )
#endif

#ifdef USE_COLCALL
    ENDDO
#endif

  ENDDO

#ifdef USE_OPENMP
!$OMP END PARALLEL
#endif

!$acc end parallel loop

  TEC = OMP_GET_WTIME ()

!$acc end data

#ifdef _OPENACC
  CALL WIPE_PHYEX_T(PHYEX)
#endif

  TED = OMP_GET_WTIME ()

  ZTC = ZTC + (TEC - TSC)
  ZTD = ZTD + (TED - TSD)

ENDDO

IF (LHOOK) CALL DR_HOOK ('MAIN',1,ZHOOK_HANDLE)

TE = OMP_GET_WTIME()

WRITE (*,'(A,F8.2,A)') 'elapsed time : ',TE-TS,' s'
WRITE (*,'(A,F8.4,A)') '          i.e. ',1000.*(TE-TS)/(NPROMA*NGPBLKS)/NTIME,' ms/gp'

PRINT *, " ZTD = ", ZTD, ZTD / REAL (NPROMA*NGPBLKS*NTIME)
PRINT *, " ZTC = ", ZTC, ZTC / REAL (NPROMA*NGPBLKS*NTIME)

IF (LLCHECK .OR. LLSTAT .OR. LLCHECKDIFF) THEN
  DO IBL = IBLOCK1, IBLOCK2
    PRINT *, " IBL = ", IBL
    CALL DIFF ("PSRCS",    PSRCS_OUT    (:,:,IBL),   PSRCS    (:,:,IBL),   LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PCLDFR",   PCLDFR_OUT   (:,:,IBL),   PCLDFR   (:,:,IBL),   LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PICEFR",   PICEFR_OUT   (:,:,IBL),   PICEFR   (:,:,IBL),   LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PRS(1)",   PRS_OUT      (:,:,1,IBL), PRS      (:,:,1,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PRS(2)",   PRS_OUT      (:,:,2,IBL), PRS      (:,:,2,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PRS(3)",   PRS_OUT      (:,:,3,IBL), PRS      (:,:,3,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
  ENDDO
ENDIF

IF (LLCHECKDIFF) THEN
  IF (LLDIFF) THEN
    PRINT*, "THERE ARE DIFF SOMEWHERE"
  ELSE
    PRINT*, "THERE IS NO DIFF AT ALL"
  ENDIF
ENDIF

STOP

CONTAINS

SUBROUTINE INIT_PHYEX(KRR, PHYEX)

USE MODD_BUDGET, ONLY: TBUCONF_ASSOCIATE, NBUDGET_RI, TBUCONF, LBU_ENABLE, LBUDGET_U, LBUDGET_V, LBUDGET_W, LBUDGET_TH, &
                       LBUDGET_TKE, LBUDGET_RV, LBUDGET_RC, LBUDGET_RR, LBUDGET_RI, LBUDGET_RS, LBUDGET_RG, LBUDGET_RH, LBUDGET_SV
USE MODD_PHYEX, ONLY: PHYEX_t
USE MODI_INI_PHYEX, ONLY: INI_PHYEX

IMPLICIT NONE

! -----------------------------------------------------------------------
!     DUMMY VARIABLES
INTEGER,          INTENT(IN)  :: KRR
TYPE(PHYEX_t),    INTENT(OUT) :: PHYEX

!-----------------------------------------------------------------------
!    LOCAL VARIABLES
INTEGER :: IULOUT, JRR
REAL :: ZDZMIN
CHARACTER(LEN=6) :: CPROGRAM
CHARACTER(LEN=4) :: CMICRO, CSCONV, CTURB
REAL             :: PTSTEP
! -----------------------------------------------------------------------

IULOUT=20
CPROGRAM='AROME'
ZDZMIN=20.
CMICRO='LIMA'
CSCONV='NONE'
CTURB='TKEL'
PTSTEP = 50.000000000000000
PHYEX%MISC%TPFILE%NLU=0

!Default values
CALL INI_PHYEX(CPROGRAM, PHYEX%MISC%TPFILE, .TRUE., IULOUT, 0, 1, &
              &PTSTEP, ZDZMIN, &
              &CMICRO, CSCONV, CTURB, &
              &LDDEFAULTVAL=.TRUE., LDREADNAM=.FALSE., LDCHECK=.FALSE., KPRINT=0, LDINIT=.FALSE., &
              &PHYEX_OUT=PHYEX)

!Control parameters
PHYEX%MISC%PTSTEP       = PTSTEP
PHYEX%MISC%LMFCONV      = .TRUE.
PHYEX%MISC%KRR          = KRR
PHYEX%MISC%OCOMPUTE_SRC = .TRUE.

!Emulate the namelist reading
PHYEX%PARAM_LIMA%NMOM_C=2
PHYEX%PARAM_LIMA%NMOM_R=2
PHYEX%PARAM_LIMA%NMOM_I=1
PHYEX%PARAM_LIMA%NMOM_S=1
PHYEX%PARAM_LIMA%NMOM_G=1
PHYEX%PARAM_LIMA%NMOD_CCN=1
PHYEX%PARAM_LIMA%CCCN_MODES='COPT'
PHYEX%PARAM_LIMA%NMOD_IFN=0
PHYEX%PARAM_LIMA%LSNOW_T=.TRUE.
PHYEX%PARAM_LIMA%LMURAKAMI=.TRUE.
PHYEX%NEBN%LSUBG_COND   = .TRUE.
PHYEX%NEBN%LSIGMAS = .TRUE.
PHYEX%NEBN%CFRAC_ICE_ADJUST='T'
PHYEX%NEBN%CFRAC_ICE_SHALLOW_MF='T'
PHYEX%NEBN%VSIGQSAT=0.02

!Param initialisation
CALL INI_PHYEX(CPROGRAM, PHYEX%MISC%TPFILE, .TRUE., IULOUT, 0, 1, &
              &PTSTEP, ZDZMIN, &
              &CMICRO, CSCONV, CTURB, &
              &LDDEFAULTVAL=.FALSE., LDREADNAM=.FALSE., LDCHECK=.TRUE., KPRINT=2, LDINIT=.TRUE., &
              &PHYEX_IN=PHYEX, PHYEX_OUT=PHYEX)

!Budgets
CALL TBUCONF_ASSOCIATE
PHYEX%MISC%NBUDGET=NBUDGET_RI
DO JRR=1, PHYEX%MISC%NBUDGET
  PHYEX%MISC%YLBUDGET(JRR)%NBUDGET=JRR
ENDDO
LBU_ENABLE=.FALSE.                                                                                                       
LBUDGET_U=.FALSE.
LBUDGET_V=.FALSE.
LBUDGET_W=.FALSE.
LBUDGET_TH=.FALSE.
LBUDGET_TKE=.FALSE.
LBUDGET_RV=.FALSE.
LBUDGET_RC=.FALSE.
LBUDGET_RR=.FALSE.
LBUDGET_RI=.FALSE.
LBUDGET_RS=.FALSE.
LBUDGET_RG=.FALSE.
LBUDGET_RH=.FALSE.
LBUDGET_SV=.FALSE.
PHYEX%MISC%TBUCONF=TBUCONF

END SUBROUTINE INIT_PHYEX

END PROGRAM

