SUBROUTINE APL_AROME_MICRO(YDMODEL, YDCPG_BNDS, YDCPG_OPTS, &
                         & YDMF_PHYS, YDMF_PHYS_SURF, &
                         & YSPP_ALL, YDDDH, &
                         & PTOWNS, &
                         & PWM, PTKEM, &
                         & PDT, PDZZ, PRHODJM, PRHODREFM, &
                         & PEXNREFM, PPABSM, PTHM, &
                         & PTHSIN, PRSIN, PLIMASIN, PSVMIN, &
                         & PDTHRAD, PSIGS, &
                         & PICLDFR, PWCLDFR, &
                         & PSSIO, PSSIU, PIFR, &
                         & PQDM, PZZ, &
                         & PIFN_NC, PCLDROP, PAEROM, &
                         & PRM, PLIMAM, &
                         & PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF, &
                         & PICEFR, PPRCFR, &
                         & PINPRG_NOTINCR, PINPRR_NOTINCR, PINPRS_NOTINCR, &
                         & PINPRG, PINPRR, PINPRS, PINPRH, &
                         & PNEBMNH, PTENDT, PTENDTT, &
                         & PTENDRA, PTENDLIMA, PTENDEXT, &
                         & PTHS, PRS, PLIMAS, PSVM, &
                         & PTENDAERO)

!**** *APL_AROME_MICRO* - Setup Arome microphysics and call
!                         ARO_LIMA or ARO_RAIN_ICE, and maybe ARO_RAINAERO

!     Author.
!     -------
!      Rolf Heilemann Myhre *Met Norway*
!      Original : 01-09-2023
!      See APL_AROME for modification history

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------
!
!   YDMODEL:        Model configuration settings
!   YDCPG_BNDS:     Array dimensions and bounds
!   YDCPG_OPTS:     Various dimensions
!   YDMF_PHYS_SURF: Surface fields
!   YSPP_ALL:       SPP fields and settings
!   YDDDH:          DDH fields and settings
!
!   PDT:            Time step (in s)
!   PRM:            Moist variables at time t
!   PTKEM:          Turbulent kinetic energy
!   PSVM:           Passive scalars
!   PSIGS:          Sigma for subgridcond
!   PTENDT:         Temperature tendency
!   PTENDEXT:       Passive scalars tendency
!

  USE PARKIND1, ONLY: JPRB, JPIM

  USE TYPE_MODEL,               ONLY: MODEL
  USE CPG_OPTS_TYPE_MOD,        ONLY: CPG_BNDS_TYPE, CPG_OPTS_TYPE
  USE MF_PHYS_TYPE_MOD,         ONLY: MF_PHYS_TYPE
  USE MF_PHYS_SURFACE_TYPE_MOD, ONLY: MF_PHYS_SURF_TYPE

  USE DDH_MIX,                  ONLY: TYP_DDH
  USE SPP_MOD_TYPE,             ONLY: UA_PHYS_SPP_VARS

  USE YOMHOOK,                  ONLY: LHOOK, DR_HOOK, JPHOOK

  IMPLICIT NONE

  TYPE(MODEL),             INTENT(IN)    :: YDMODEL
  TYPE(CPG_BNDS_TYPE),     INTENT(IN)    :: YDCPG_BNDS
  TYPE(CPG_OPTS_TYPE),     INTENT(IN)    :: YDCPG_OPTS
  TYPE(MF_PHYS_TYPE),      INTENT(IN)    :: YDMF_PHYS
  TYPE(MF_PHYS_SURF_TYPE), INTENT(IN)    :: YDMF_PHYS_SURF

  TYPE(UA_PHYS_SPP_VARS),  INTENT(INOUT) :: YSPP_ALL
  TYPE(TYP_DDH),           INTENT(INOUT) :: YDDDH


  REAL(KIND=JPRB), INTENT(IN)    :: PTOWNS(YDCPG_BNDS%KFDIA)

  REAL(KIND=JPRB), TARGET, INTENT(IN)  :: PWM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PTKEM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PDT
  REAL(KIND=JPRB), INTENT(IN)    :: PDZZ(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PRHODJM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PRHODREFM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PEXNREFM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PPABSM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PTHM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PTHSIN(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PRSIN(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(IN)    :: PLIMASIN(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(IN)    :: PSVMIN(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)

  REAL(KIND=JPRB), INTENT(IN)    :: PDTHRAD(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PSIGS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PICLDFR(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PWCLDFR(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PSSIO(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PSSIU(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PIFR(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PQDM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PZZ(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PIFN_NC(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PCLDROP(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PAEROM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NAERO)

  REAL(KIND=JPRB), INTENT(INOUT) :: PRM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PLIMAM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)

  REAL(KIND=JPRB), INTENT(INOUT) :: PHLC_HRC(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PHLC_HCF(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PHLI_HRI(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PHLI_HCF(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(INOUT) :: PICEFR(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PPRCFR(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRG_NOTINCR(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRR_NOTINCR(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRS_NOTINCR(YDCPG_BNDS%KFDIA)

  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRG(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRR(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRS(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRH(YDCPG_BNDS%KFDIA)

  REAL(KIND=JPRB), INTENT(INOUT) :: PNEBMNH(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDT (YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG) ! temperature tendency
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDTT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG) ! array to save heating profile for LHN

  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDRA(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDLIMA(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDEXT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)

  REAL(KIND=JPRB), INTENT(OUT)   :: PTHS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(OUT)   :: PRS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(OUT)   :: PLIMAS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(OUT)   :: PSVM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)

  REAL(KIND=JPRB), INTENT(OUT)   :: PTENDAERO(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NAERO)

  REAL(KIND=JPRB), POINTER :: ZPTRWNU(:,:)
  REAL(KIND=JPRB), TARGET  :: ZWNU(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZSEA(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB) :: ZLSM(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB) :: ZUGST(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB) :: ZVGST(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB) :: ZTOWN(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB) :: ZCIT(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZEVAP(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZPFPR(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB) :: ZAER_TEND(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NAERO)

  REAL(KIND=JPRB) :: ZINPRH_NOTINCR(YDCPG_BNDS%KFDIA)

  INTEGER(KIND=JPIM) :: JLON, JLEV, JR, JGFL
  INTEGER(KIND=JPIM) :: IKU, IKL

  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

  !------------------------------------------------------------------

#include "aro_rain_ice.h"
#include "aro_lima.h"
#include "aro_rainaero.h"

  !------------------------------------------------------------------

  IF (LHOOK) CALL DR_HOOK('APL_AROME_MICRO_MOD:APL_AROME_MICRO', 0, ZHOOK_HANDLE)

  ! for now a copy is needed (see below, inside). I don't like than :-( REK
  PTHS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG) &
  & = PTHSIN(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)

  PRS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG, 1:YDMODEL%YRML_PHY_MF%YRPARAR%NRR) = &
  & PRSIN(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG, 1:YDMODEL%YRML_PHY_MF%YRPARAR%NRR)

  ! for now a copy is needed (see below, inside). I don't like than :-( REK
  PLIMAS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG, 1:YDMODEL%YRML_GCONF%YGFL%NLIMA) = &
  & PLIMASIN(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG, 1:YDMODEL%YRML_GCONF%YGFL%NLIMA)

  ZSEA(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0.0_JPRB
  IF (YDMODEL%YRML_PHY_MF%YRPARAR%LOLSMC) THEN
    DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
      IF (YDMF_PHYS_SURF%GSD_VF%PLSM(JLON) < 0.5) THEN
        ZSEA(JLON) = 1.0_JPRB
      ENDIF
    ENDDO
  ENDIF

  IF (YDMODEL%YRML_PHY_MF%YRPARAR%LOTOWNC) THEN
    ZTOWN(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = PTOWNS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
  ELSE
    ZTOWN(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)=0.0_JPRB
  ENDIF

  IKU = 1
  IKL = -1

  IF (YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO == 'LIMA') THEN

    IF (YDMODEL%YRML_PHY_MF%YRARPHY%LTURB) THEN
      DO JLON=YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        DO JLEV=1,YDCPG_OPTS%KFLEVG
          ZWNU(JLON,JLEV) = PWM(JLON,JLEV) + 0.66*SQRT(PTKEM(JLON,JLEV))
        ENDDO
      ENDDO
      ZPTRWNU => ZWNU(1:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG)
    ELSE
      ZPTRWNU => PWM(1:YDCPG_BNDS%KFDIA,1:YDCPG_OPTS%KFLEVG)
    ENDIF

    CALL ARO_LIMA(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & YDCPG_OPTS%KFLEVG, IKU, IKL, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDCPG_BNDS%KFDIA, &
    & YDMODEL%YRML_PHY_MF%YRPARAR%NRR, YDMODEL%YRML_GCONF%YGFL%NLIMA, &
    & PDT, PDZZ, PRHODJM(:, 1:YDCPG_OPTS%KFLEVG), PRHODREFM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PEXNREFM, PPABSM(:, 1:YDCPG_OPTS%KFLEVG), ZPTRWNU, PDTHRAD, PTHM(:, 1:YDCPG_OPTS%KFLEVG), PRM, &
    & PLIMAM, PTHS, PRS, PLIMAS, ZEVAP, &
    & PINPRR_NOTINCR, PINPRS_NOTINCR, PINPRG_NOTINCR, ZINPRH_NOTINCR, ZPFPR, &
    & PNEBMNH, PICEFR, PPRCFR, &
    & YDDDH, YDMODEL%YRML_DIAG%YRLDDH, YDMODEL%YRML_DIAG%YRMDDH)

  ELSE

    ZCIT(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG)=0.0_JPRB

    CALL ARO_RAIN_ICE(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & YDCPG_OPTS%KFLEVG, IKU, IKL, &
    & YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDCPG_BNDS%KFDIA, &
    & YDMODEL%YRML_PHY_MF%YRPARAR%NRR,          &
    & YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO, &
    & PDT, PDZZ, &
    & PRHODJM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PRHODREFM(:, 1:YDCPG_OPTS%KFLEVG), PEXNREFM, PPABSM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF,                       &
    & PTHM(:, 1:YDCPG_OPTS%KFLEVG),   &
    & PRM, PSIGS(:, 1:YDCPG_OPTS%KFLEVG), PNEBMNH, PTHS, PRS, ZEVAP,                      &
    & ZCIT, ZSEA, ZTOWN, &
    & PICLDFR, PWCLDFR, PSSIO, PSSIU, PIFR, &
    & YDMODEL%YRML_PHY_MF%YRPARAR%LKOGAN, YDMODEL%YRML_PHY_MF%YRPARAR%LMODICEDEP, &
    & PINPRR_NOTINCR, PINPRS_NOTINCR, PINPRG_NOTINCR, ZINPRH_NOTINCR, ZPFPR, &
    & YDMODEL%YRML_PHY_MF%YRPHY%LAERONRT, YDMODEL%YRML_PHY_MF%YRNRTAER%LAEIFN, PCLDROP, PIFN_NC,&
    & YDDDH, YDMODEL%YRML_DIAG%YRLDDH, YDMODEL%YRML_DIAG%YRMDDH,&
    & YSPP_ALL%YSPP_ICENU, YSPP_ALL%YSPP_KGN_ACON, YSPP_ALL%YSPP_KGN_SBGR)

  ENDIF

  IF (YDMODEL%YRML_PHY_MF%YRPHY%LAERONRT) THEN
    ! Copy fields from Arpege- to Meso-NH-style (is this needed?)
    ZLSM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)  = YDMF_PHYS_SURF%GSD_VF%PLSM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
    ZUGST(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = YDMF_PHYS%OUT%UGST(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
    ZVGST(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = YDMF_PHYS%OUT%VGST(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA)
    ! Aerosol removal proceses
    CALL ARO_AEROSOL(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & YDCPG_OPTS%KFLEVG, &
    & IKU, IKL, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, &
    & YDMODEL%YRML_PHY_MF%YRPARAR%NRR, &
    & YDMODEL%YRML_GCONF%YGFL%NAERO, &
    & PDT, &
    & PPABSM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PTHM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PDZZ, &
    & PRHODREFM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PRM, &
    & PNEBMNH, &
    & ZPFPR, &
    & ZLSM, ZUGST, ZVGST, &
    & PAEROM, &
    & ZAER_TEND)
    DO JGFL = 1, YDMODEL%YRML_GCONF%YGFL%NAERO
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        ! n.r.t. aerosols scavenging rate
        PTENDAERO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV,JGFL) = ZAER_TEND(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV,JGFL)
      ENDDO
    ENDDO
  ENDIF

  DO JLON=YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
    PINPRR(JLON) = PINPRR(JLON) + PINPRR_NOTINCR(JLON)
    PINPRS(JLON) = PINPRS(JLON) + PINPRS_NOTINCR(JLON)
    PINPRG(JLON) = PINPRG(JLON) + PINPRG_NOTINCR(JLON)
    PINPRH(JLON) = PINPRH(JLON) + ZINPRH_NOTINCR(JLON)
  ENDDO

  !conversion de la tendance de theta en tendance de T et inversion niveau
  DO JLEV = 1,YDCPG_OPTS%KFLEVG
    DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      PTENDT(JLON,JLEV)  = PTENDT(JLON,JLEV)  + (PTHS(JLON,JLEV) - PTHSIN(JLON,JLEV))*PEXNREFM(JLON,JLEV)
      PTENDTT(JLON,JLEV) = PTENDTT(JLON,JLEV) +  PTHS(JLON,JLEV) - PTHSIN(JLON,JLEV)
    ENDDO
  ENDDO

  !inversion niveaux tendances des ri et conversion en qi en multipliant par qd
  DO JR=1, YDMODEL%YRML_PHY_MF%YRPARAR%NRR
    DO JLEV=1, YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PTENDRA(JLON,JLEV,JR) = PTENDRA(JLON,JLEV,JR) + (PRS(JLON,JLEV,JR) - PRSIN(JLON,JLEV,JR))*PQDM(JLON,JLEV)
      ENDDO
    ENDDO
  ENDDO

  ! Tendances des variables LIMA
  DO JGFL = 1, YDMODEL%YRML_GCONF%YGFL%NLIMA
    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        PTENDLIMA(JLON,JLEV,JGFL) = PTENDLIMA(JLON,JLEV,JGFL) + (PLIMAS(JLON,JLEV,JGFL) - PLIMASIN(JLON,JLEV,JGFL))
      ENDDO
    ENDDO
  ENDDO

  IF (YDMODEL%YRML_PHY_MF%YRARPHY%LRDEPOS) THEN

    CALL ARO_RAINAERO(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT, YDMODEL%YRML_PHY_MF%YRPARAR%NRR, &
    & YDCPG_OPTS%ZDTPHY, PSVMIN, PZZ, PPABSM(:, 1:YDCPG_OPTS%KFLEVG), &
    & PTHM(:, 1:YDCPG_OPTS%KFLEVG), PRHODREFM(:, 1:YDCPG_OPTS%KFLEVG), YDCPG_OPTS%NSTEP+1, PRM, &
    & ZEVAP, YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%CLOUDPARN%NSPLITR, PSVM)
    ! return to tendency
    DO JGFL = 1, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT
      DO JLEV = 1, YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          PTENDEXT(JLON,JLEV,JGFL) = PTENDEXT(JLON,JLEV,JGFL)+(PSVM(JLON,JLEV,JGFL)-PSVMIN(JLON,JLEV,JGFL))/YDCPG_OPTS%ZDTPHY
        ENDDO
      ENDDO
    ENDDO

  ENDIF ! LRDEPOS

  IF (LHOOK) CALL DR_HOOK('APL_AROME_MICRO_MOD:APL_AROME_MICRO', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_AROME_MICRO
