SUBROUTINE APL_AROME_MICRO(YDMODEL, YDCPG_BNDS, YDCPG_OPTS, &
                         & YDMF_PHYS, YDMF_PHYS_SURF, &
                         & YSPP_ALL, YDDDH, &
                         & PTOWNS, &
                         & PWM, PTKEM, &
                         & PDT, PDZZ, PRHODJM, PRHODREFM, &
                         & PEXNREFM, PPABSM, PTHM, &
                         & PDTHRAD, PSIGS, &
                         & PICLDFR, PWCLDFR, &
                         & PSSIO, PSSIU, PIFR, &
                         & PQDM, PZZ, &
                         & PIFN_NC, PCLDROP, PAEROM, &
                         & PRM, PLIMAM, &
                         & PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF, &
                         & PICEFR, PPRCFR, &
                         & PINPRG_NOTINCR, PINPRR_NOTINCR, PINPRS_NOTINCR, &
                         & PINPRG, PINPRR, PINPRS, PINPRH, &
                         & PNEBMNH, PTENDT, PTENDTT, &
                         & PTENDRA, PTENDLIMA, PTENDEXT, &
                         & PTHS, PRS, PLIMAS, PSVM, &
                         & PTENDAERO)

!**** *APL_AROME_MICRO* - Setup Arome microphysics and call
!                         ARO_LIMA or ARO_RAIN_ICE, and maybe ARO_RAINAERO

!     Author.
!     -------
!      Rolf Heilemann Myhre *Met Norway*
!      Original : 01-09-2023
!      See APL_AROME for modification history

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------
!
!   YDMODEL:        Model configuration settings
!   YDCPG_BNDS:     Array dimensions and bounds
!   YDCPG_OPTS:     Various dimensions
!   YDMF_PHYS_SURF: Surface fields
!   YSPP_ALL:       SPP fields and settings
!   YDDDH:          DDH fields and settings
!
!   PDT:            Time step (in s)
!   PRM:            Moist variables at time t
!   PTKEM:          Turbulent kinetic energy
!   PSVM:           Passive scalars
!   PSIGS:          Sigma for subgridcond
!   PTENDT:         Temperature tendency
!   PTENDEXT:       Passive scalars tendency
!

  USE PARKIND1, ONLY: JPRB, JPIM

  USE TYPE_MODEL,               ONLY: MODEL
  USE CPG_OPTS_TYPE_MOD,        ONLY: CPG_BNDS_TYPE, CPG_OPTS_TYPE
  USE MF_PHYS_TYPE_MOD,         ONLY: MF_PHYS_TYPE
  USE MF_PHYS_SURFACE_TYPE_MOD, ONLY: MF_PHYS_SURF_TYPE

  USE DDH_MIX,                  ONLY: TYP_DDH
  USE SPP_MOD_TYPE,             ONLY: UA_PHYS_SPP_VARS

  USE YOMHOOK,                  ONLY: LHOOK, DR_HOOK, JPHOOK

  IMPLICIT NONE

  TYPE(MODEL),             INTENT(IN)    :: YDMODEL
  TYPE(CPG_BNDS_TYPE),     INTENT(IN)    :: YDCPG_BNDS
  TYPE(CPG_OPTS_TYPE),     INTENT(IN)    :: YDCPG_OPTS
  TYPE(MF_PHYS_TYPE),      INTENT(IN)    :: YDMF_PHYS
  TYPE(MF_PHYS_SURF_TYPE), INTENT(IN)    :: YDMF_PHYS_SURF

  TYPE(UA_PHYS_SPP_VARS),  INTENT(INOUT) :: YSPP_ALL
  TYPE(TYP_DDH),           INTENT(INOUT) :: YDDDH


  REAL(KIND=JPRB), INTENT(IN)    :: PTOWNS(YDCPG_OPTS%KLON)

  REAL(KIND=JPRB), TARGET, INTENT(IN)  :: PWM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PTKEM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PDT
  REAL(KIND=JPRB), INTENT(IN)    :: PDZZ(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PRHODJM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PRHODREFM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PEXNREFM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PPABSM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(IN)    :: PTHM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG+1)


  REAL(KIND=JPRB), INTENT(IN)    :: PDTHRAD(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PSIGS(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(IN)    :: PICLDFR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PWCLDFR(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG)
  
  REAL(KIND=JPRB), INTENT(IN)    :: PSSIO(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PSSIU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PIFR(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PQDM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PZZ(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PIFN_NC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PCLDROP(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PAEROM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NAERO)

  REAL(KIND=JPRB), INTENT(INOUT) :: PRM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PLIMAM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)

  REAL(KIND=JPRB), INTENT(INOUT) :: PHLC_HRC(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PHLC_HCF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PHLI_HRI(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PHLI_HCF(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(INOUT) :: PICEFR(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PPRCFR(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRG_NOTINCR(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRR_NOTINCR(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRS_NOTINCR(YDCPG_OPTS%KLON)

  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRG(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRR(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRS(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB), INTENT(INOUT) :: PINPRH(YDCPG_OPTS%KLON)

  REAL(KIND=JPRB), INTENT(INOUT) :: PNEBMNH(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDT (YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG) ! temperature tendency
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDTT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG) ! array to save heating profile for LHN

  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDRA(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDLIMA(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDEXT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTHS(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PRS(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PLIMAS(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PSVM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)

  REAL(KIND=JPRB), INTENT(OUT)   :: PTENDAERO(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NAERO)

  REAL(KIND=JPRB) :: ZTHSIN(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZRSIN(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB) :: ZSVMIN(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)
  REAL(KIND=JPRB) :: ZLIMASIN(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)

  REAL(KIND=JPRB), POINTER :: ZPTRWNU(:,:)
  REAL(KIND=JPRB), TARGET  :: ZWNU(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB) :: ZSEA(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB) :: ZLSM(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB) :: ZUGST(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB) :: ZVGST(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB) :: ZTOWN(YDCPG_OPTS%KLON)
  REAL(KIND=JPRB) :: ZCIT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZEVAP(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB) :: ZPFPR(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)

  REAL(KIND=JPRB) :: ZINPRH_NOTINCR(YDCPG_OPTS%KLON)

  INTEGER(KIND=JPIM) :: JLON, JLEV, JGFL, JRR
  INTEGER(KIND=JPIM) :: IKU, IKL

  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

  !------------------------------------------------------------------

#include "aro_rain_ice.h"
#include "aro_lima.h"
#include "aro_rainaero.h"

  !------------------------------------------------------------------

  IF (LHOOK) CALL DR_HOOK('APL_AROME_MICRO', 0, ZHOOK_HANDLE)

  ASSOCIATE(KLON => YDCPG_OPTS%KLON, KFLEVG => YDCPG_OPTS%KFLEVG, KIDIA => YDCPG_BNDS%KIDIA, KFDIA => YDCPG_BNDS%KFDIA)

  ! for now a copies are needed (see below, inside). I don't like than :-( REK
  DO JLEV = 1, KFLEVG
    DO JLON = KIDIA, KFDIA
      ZTHSIN(JLON, JLEV) = PTHS(JLON, JLEV)
    ENDDO
  ENDDO

  DO JGFL = 1, YDMODEL%YRML_PHY_MF%YRPARAR%NRR
    DO JLEV = 1, KFLEVG
      DO JLON = KIDIA, KFDIA
        ZRSIN(JLON, JLEV, JGFL) = PRS(JLON, JLEV, JGFL)
      ENDDO
    ENDDO
  ENDDO

  DO JGFL = 1, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT
    DO JLEV = 1, KFLEVG
      DO JLON = KIDIA, KFDIA
        ZSVMIN(JLON, JLEV, JGFL) = PSVM(JLON, JLEV, JGFL)
      ENDDO
    ENDDO
  ENDDO

  DO JGFL = 1, YDMODEL%YRML_GCONF%YGFL%NLIMA
    DO JLEV = 1, KFLEVG
      DO JLON = KIDIA, KFDIA
        ZLIMASIN(JLON, JLEV, JGFL) = PLIMAS(JLON, JLEV, JGFL)
      ENDDO
    ENDDO
  ENDDO

  ZSEA(KIDIA:KFDIA)=0.0_JPRB
  IF (YDMODEL%YRML_PHY_MF%YRPARAR%LOLSMC) THEN
    DO JLON = KIDIA, KFDIA
      IF (YDMF_PHYS_SURF%GSD_VF%PLSM(JLON) < 0.5) THEN
        ZSEA(JLON) = 1.0_JPRB
      ENDIF
    ENDDO
  ENDIF

  IF (YDMODEL%YRML_PHY_MF%YRPARAR%LOTOWNC) THEN
    ZTOWN(KIDIA:KFDIA) = PTOWNS(KIDIA:KFDIA)
  ELSE
    ZTOWN(KIDIA:KFDIA)=0.0_JPRB
  ENDIF

  IKU = 1
  IKL = -1

  IF (YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO == 'LIMA') THEN

    IF (YDMODEL%YRML_PHY_MF%YRARPHY%LTURB) THEN
      DO JLON=KIDIA, KFDIA
        DO JLEV=1,KFLEVG
          ZWNU(JLON,JLEV) = PWM(JLON,JLEV) + 0.66*SQRT(PTKEM(JLON,JLEV))
        ENDDO
      ENDDO
      ZPTRWNU => ZWNU(1:KFDIA,1:KFLEVG)
    ELSE
      ZPTRWNU => PWM(1:KFDIA,1:KFLEVG)
    ENDIF

    CALL ARO_LIMA(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & KFLEVG, IKU, IKL, KLON, KFLEVG, &
    & KIDIA, KFDIA, &
    & YDMODEL%YRML_PHY_MF%YRPARAR%NRR, YDMODEL%YRML_GCONF%YGFL%NLIMA, &
    & PDT, PDZZ, PRHODJM(:, 1:KFLEVG), PRHODREFM(:, 1:KFLEVG), &
    & PEXNREFM, PPABSM(:, 1:KFLEVG), ZPTRWNU, PDTHRAD, PTHM(:, 1:KFLEVG), PRM, &
    & PLIMAM, ZCIT, PTHS, PRS, PLIMAS, ZEVAP, &
    & PINPRR_NOTINCR, PINPRS_NOTINCR, PINPRG_NOTINCR, ZINPRH_NOTINCR, ZPFPR, &
    & PNEBMNH, PICEFR, PPRCFR, &
    & PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF,                       &
    & YDDDH, YDMODEL%YRML_DIAG%YRLDDH, YDMODEL%YRML_DIAG%YRMDDH)

  ELSE

    ZCIT(KIDIA:KFDIA, 1:KFLEVG)=0.0_JPRB

    CALL ARO_RAIN_ICE(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & KFLEVG, IKU, IKL, &
    & KLON, KFLEVG, &
    & KIDIA, KFDIA, &
    & YDMODEL%YRML_PHY_MF%YRPARAR%NRR, &
    & YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO, &
    & PDT, PDZZ, &
    & PRHODJM(:, 1:KFLEVG), &
    & PRHODREFM(:, 1:KFLEVG), PEXNREFM, PPABSM(:, 1:KFLEVG), &
    & PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF, &
    & PTHM(:, 1:KFLEVG), &
    & PRM, PSIGS(:, 1:KFLEVG), PNEBMNH, PTHS, PRS, ZEVAP, &
    & ZCIT, ZSEA, ZTOWN, &
    & PICLDFR, PSSIO, PSSIU, PIFR, &
    & PINPRR_NOTINCR, PINPRS_NOTINCR, PINPRG_NOTINCR, ZINPRH_NOTINCR, ZPFPR, &
    & YDMODEL%YRML_PHY_MF%YRPHY%LAERONRT, YDMODEL%YRML_PHY_MF%YRNRTAER%LAEIFN, PCLDROP, PIFN_NC,&
    & YDDDH, YDMODEL%YRML_DIAG%YRLDDH, YDMODEL%YRML_DIAG%YRMDDH,&
    & YSPP_ALL%YSPP_ICENU, YSPP_ALL%YSPP_KGN_ACON, YSPP_ALL%YSPP_KGN_SBGR)

  ENDIF

  IF (YDMODEL%YRML_PHY_MF%YRPHY%LAERONRT) THEN
    ! Copy fields from Arpege- to Meso-NH-style (is this needed?)
    ZLSM(KIDIA:KFDIA)  = YDMF_PHYS_SURF%GSD_VF%PLSM(KIDIA:KFDIA)
    ZUGST(KIDIA:KFDIA) = YDMF_PHYS%UGST(KIDIA:KFDIA)
    ZVGST(KIDIA:KFDIA) = YDMF_PHYS%VGST(KIDIA:KFDIA)

    ! Aerosol removal proceses
    CALL ARO_AEROSOL(YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & KFLEVG, &
    & IKU, IKL, KLON, KFLEVG, &
    & YDMODEL%YRML_PHY_MF%YRPARAR%NRR, &
    & YDMODEL%YRML_GCONF%YGFL%NAERO, &
    & PDT, &
    & PPABSM(:, 1:KFLEVG), &
    & PTHM(:, 1:KFLEVG), &
    & PDZZ, &
    & PRHODREFM(:, 1:KFLEVG), &
    & PRM, &
    & PNEBMNH, &
    & ZPFPR, &
    & ZLSM, ZUGST, ZVGST, &
    & PAEROM, &
    & PTENDAERO)

  ENDIF

  DO JLON=KIDIA,KFDIA
    PINPRR(JLON) = PINPRR(JLON) + PINPRR_NOTINCR(JLON)
    PINPRS(JLON) = PINPRS(JLON) + PINPRS_NOTINCR(JLON)
    PINPRG(JLON) = PINPRG(JLON) + PINPRG_NOTINCR(JLON)
    PINPRH(JLON) = PINPRH(JLON) + ZINPRH_NOTINCR(JLON)
  ENDDO

  !conversion de la tendance de theta en tendance de T et inversion niveau
  DO JLEV = 1,KFLEVG
    DO JLON = KIDIA,KFDIA
      PTENDT(JLON,JLEV)  = PTENDT(JLON,JLEV)  + (PTHS(JLON,JLEV) - ZTHSIN(JLON,JLEV))*PEXNREFM(JLON,JLEV)
      PTENDTT(JLON,JLEV) = PTENDTT(JLON,JLEV) +  PTHS(JLON,JLEV) - ZTHSIN(JLON,JLEV)
    ENDDO
  ENDDO

  !inversion niveaux tendances des ri et conversion en qi en multipliant par qd
  DO JRR = 1, YDMODEL%YRML_PHY_MF%YRPARAR%NRR
    DO JLEV = 1, KFLEVG
      DO JLON = KIDIA,KFDIA
        PTENDRA(JLON,JLEV,JRR) = PTENDRA(JLON,JLEV,JRR) + (PRS(JLON,JLEV,JRR) - ZRSIN(JLON,JLEV,JRR))*PQDM(JLON,JLEV)
      ENDDO
    ENDDO
  ENDDO

  ! Tendances des variables LIMA
  DO JGFL = 1, YDMODEL%YRML_GCONF%YGFL%NLIMA
    DO JLEV = 1, KFLEVG
      DO JLON = KIDIA, KFDIA
        PTENDLIMA(JLON,JLEV,JGFL) = PTENDLIMA(JLON,JLEV,JGFL) + (PLIMAS(JLON,JLEV,JGFL) - ZLIMASIN(JLON,JLEV,JGFL))
      ENDDO
    ENDDO
  ENDDO

  IF (YDMODEL%YRML_PHY_MF%YRARPHY%LRDEPOS) THEN

    CALL ARO_RAINAERO(KLON, KFLEVG, KIDIA, KFDIA, &
    & YDMODEL%YRML_GCONF%YGFL%NGFL_EXT, YDMODEL%YRML_PHY_MF%YRPARAR%NRR, &
    & YDCPG_OPTS%ZDTPHY, ZSVMIN, PZZ, PPABSM(:, 1:KFLEVG), &
    & PTHM(:, 1:KFLEVG), PRHODREFM(:, 1:KFLEVG), YDCPG_OPTS%NSTEP+1, PRM, &
    & ZEVAP, YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%CLOUDPARN%NSPLITR, PSVM)

    ! return to tendency
    DO JGFL = 1, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT
      DO JLEV = 1, KFLEVG
        DO JLON = KIDIA, KFDIA
          PTENDEXT(JLON,JLEV,JGFL) = PTENDEXT(JLON,JLEV,JGFL)+(PSVM(JLON,JLEV,JGFL) - ZSVMIN(JLON,JLEV,JGFL))/YDCPG_OPTS%ZDTPHY
        ENDDO
      ENDDO
    ENDDO

  ENDIF ! LRDEPOS

  END ASSOCIATE

  IF (LHOOK) CALL DR_HOOK('APL_AROME_MICRO', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_AROME_MICRO
