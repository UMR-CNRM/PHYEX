MODULE MODD_SPP_TYPE

 !
 ! Module for handling of SPP data brought into the mpa routines
 !
 ! U. Andrae, SMHI, 2020-12 : Original implementation
 !

 USE PARKIND1, ONLY : JPIM,JPRB
 IMPLICIT NONE

 TYPE TSPP_CONFIG_MPA
  LOGICAL :: LPERT=.FALSE.                
  LOGICAL :: LLNN_MEAN1=.FALSE.
  LOGICAL :: LLNN_MEAN1_SELF=.FALSE.

  REAL(KIND=JPRB) :: CMPERT
  REAL(KIND=JPRB) :: SDEV
  REAL(KIND=JPRB) :: CLIP(2)
  REAL(KIND=JPRB), POINTER :: PGP2DSPP(:) => NULL()

 END TYPE TSPP_CONFIG_MPA

 CONTAINS

 !
 !-----------------------------------------------------------------------
 !

 SUBROUTINE CLEAR_SPP_TYPE(TSPP)

  ! Clear and reset the content of a type
  IMPLICIT NONE
  TYPE(TSPP_CONFIG_MPA), INTENT(INOUT) :: TSPP

  TSPP%LPERT=.FALSE.
  NULLIFY(TSPP%PGP2DSPP)

 END SUBROUTINE CLEAR_SPP_TYPE

 !
 !-----------------------------------------------------------------------
 !

 SUBROUTINE SET_SPP_TYPE(TSPP,LLNN_MEAN1,LLNN_MEAN1_SELF, &
                        CMPERT,SDEV,CLIP,MP_SELF, &
                        KLON,KLEV,N2D, &
                        NEZDIAG, NEZDIAG_POS, &
                        PGP2DSPP, &
                        PREFVAL, &
                        PEZDIAG)

  ! Init a SPP type

  IMPLICIT NONE

  ! Arguments
  TYPE(TSPP_CONFIG_MPA), INTENT(INOUT) :: TSPP
  LOGICAL, INTENT(IN) :: LLNN_MEAN1,LLNN_MEAN1_SELF
  REAL, INTENT(IN) :: CMPERT,SDEV,CLIP(2),PREFVAL
  INTEGER, INTENT(IN) :: MP_SELF,KLON,KLEV,N2D,NEZDIAG,NEZDIAG_POS
  
  REAL, DIMENSION(KLON,N2D), TARGET, INTENT(IN) :: PGP2DSPP
  REAL, DIMENSION(KLON,KLEV,NEZDIAG), OPTIONAL, INTENT(INOUT) :: PEZDIAG

  ! Local

  INTEGER :: JKO,JKE,JI
  REAL :: ZMU,ZVAL

  !--------------------------------------------------------------------------

  ! Copy settings
  TSPP%LPERT           = .TRUE.
  TSPP%LLNN_MEAN1      = LLNN_MEAN1
  TSPP%LLNN_MEAN1_SELF = LLNN_MEAN1_SELF

  TSPP%CMPERT   = CMPERT
  TSPP%SDEV     = SDEV
  TSPP%CLIP     = CLIP
  TSPP%PGP2DSPP => PGP2DSPP(:,MP_SELF)

  IF ( PRESENT(PEZDIAG) .AND. NEZDIAG_POS > 0 ) THEN
   IF (TSPP%LLNN_MEAN1.OR.TSPP%LLNN_MEAN1_SELF) THEN
    ZMU = -0.5_JPRB * (TSPP%CMPERT * TSPP%SDEV)**2
   ELSE
    ZMU = 0._JPRB
   ENDIF
   JKO=2*MP_SELF-1
   JKE=2*MP_SELF
   DO JI=1,KLON
    PEZDIAG(JI,JKO,NEZDIAG_POS) = TSPP%PGP2DSPP(JI)
   ENDDO
   DO JI=1,KLON
    ZVAL = PREFVAL*EXP(ZMU+TSPP%CMPERT*TSPP%PGP2DSPP(JI))
    PEZDIAG(JI,JKE,NEZDIAG_POS) = MAX(TSPP%CLIP(1),MIN(ZVAL,TSPP%CLIP(2)))
   ENDDO
  ENDIF

 END SUBROUTINE SET_SPP_TYPE

END MODULE MODD_SPP_TYPE
