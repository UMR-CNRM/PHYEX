SUBROUTINE APL_AROME_TURBULENCE(YDCST, YDMODEL, YDGEOMETRY, YDMF_PHYS_BASE_STATE, YDMF_PHYS, &
                              & YDCPG_BNDS, YDCPG_OPTS, YDVARS, YDHGRAD, YDXFU, YSPP_ALL, YDDDH, &
                              & PAPHIM, PAPHIFM, PZZ, PZZ_F, &
                              & PLIMAM, PLIMASIN, PSVM, PSVSIN, PTKES, PSIGMF, &
                              & PEXNREFM, PQDM, PTKEEDMFS, PZS, &
                              & PRHODJM, PTHVREFM, PRHODREFM, &
                              & PSFTH, PSFRV, PSFU, PSFV, &
                              & PPABSM, PUM, PVM, PWM, &
                              & PTKEM, PSRCS, PTHM, &
                              & PRM, PUS, PVS, PWS, &
                              & PTHS, PRS, &
                              & PFLXZTHVMF_SUM, PFLXZUMF_SUM, PFLXZVMF_SUM, PLENGTH_M, PLENGTH_H, &
                              & PMF_UP, PTENDT, PTEND_Q, &
                              & PTENDTKE, PTENDLIMA, PSFSV, &
                              & PLIMAS, PSVS, PTKES_OUT, PSIGS, &
                              & PTENDU_TURB, PTENDV_TURB)

!**** *APL_AROME_TURBULENCE* - Setup and call Arome turbulence

!     Author.
!     -------
!      Rolf Heilemann Myhre *Met Norway*
!      Original : 01-09-2023
!      See APL_AROME for modification history

!-----------------------------------------------------------------------

! -   INPUT ARGUMENTS.
!     -------------------
!
!   YDCST:                Model constants
!   YDMODEL:              Model configuration settings
!   YDMF_PHYS_BASE_STATE:
!   YDMF_PHYS:            Physics fields
!   YDCPG_BNDS:           Array dimensions and bounds
!   YDCPG_OPTS:           Various dimensions
!   YDVARS:               Persistent fields
!   YSPP_ALL:             SPP fields and settings
!   YDDDH:                DDH fields and settings
!
!   PRM:                  Moist variables at time t
!   PTKEM:                Turbulent kinetic energy
!   PUM:                  Zonal wind
!   PVM:                  Meridian wind
!   PWM:                  Vertical velocity (m/s)
!   PSIGS:                Sigma for subgridcond
!   PTENDT:               Temperature tendency
!   PTENDTKE:             TKE tendency
!

  USE PARKIND1, ONLY: JPRB, JPIM

  USE YOMCST,                      ONLY: TCST
  USE TYPE_MODEL,                  ONLY: MODEL
  USE GEOMETRY_MOD,                ONLY: GEOMETRY
  USE MF_PHYS_BASE_STATE_TYPE_MOD, ONLY: MF_PHYS_BASE_STATE_TYPE
  USE MF_PHYS_TYPE_MOD,            ONLY: MF_PHYS_TYPE
  USE CPG_OPTS_TYPE_MOD,           ONLY: CPG_BNDS_TYPE, CPG_OPTS_TYPE
  USE FIELD_VARIABLES_MOD,         ONLY: FIELD_VARIABLES
  USE SPP_MOD_TYPE,                ONLY: UA_PHYS_SPP_VARS
  USE DDH_MIX,                     ONLY: NEW_ADD_FIELD_3D, TYP_DDH
  USE YOMDGRADIENT_TYPE_MOD,       ONLY: GRADIENT_TYPE
  USE YOMXFU,                      ONLY: TXFU

  USE YOMHOOK,                     ONLY: LHOOK, DR_HOOK, JPHOOK

  IMPLICIT NONE

  TYPE(TCST),                    INTENT(IN) :: YDCST
  TYPE(MODEL),                   INTENT(IN) :: YDMODEL
  TYPE(GEOMETRY),                INTENT(IN) :: YDGEOMETRY
  TYPE(MF_PHYS_BASE_STATE_TYPE), INTENT(IN) :: YDMF_PHYS_BASE_STATE
  TYPE(MF_PHYS_TYPE),            INTENT(IN) :: YDMF_PHYS
  TYPE(CPG_BNDS_TYPE),           INTENT(IN) :: YDCPG_BNDS
  TYPE(CPG_OPTS_TYPE),           INTENT(IN) :: YDCPG_OPTS
  TYPE(FIELD_VARIABLES),         INTENT(IN) :: YDVARS
  TYPE(GRADIENT_TYPE),           INTENT(IN) :: YDHGRAD
  TYPE(TXFU),                    INTENT(IN) :: YDXFU

  TYPE(UA_PHYS_SPP_VARS),        INTENT(INOUT) :: YSPP_ALL
  TYPE(TYP_DDH),                 INTENT(INOUT) :: YDDDH

  REAL(KIND=JPRB), INTENT(IN)    :: PAPHIM(YDCPG_OPTS%KLON, 0:YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PAPHIFM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PZZ(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PZZ_F(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PLIMAM(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(IN)    :: PLIMASIN(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(IN)    :: PSVM(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)
  REAL(KIND=JPRB), INTENT(IN)    :: PSVSIN(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)
  ! Updraft characteristics for Meso-NH world (input of ARO_SHALLOW_MF)
  REAL(KIND=JPRB), INTENT(IN)    :: PTKES(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PSIGMF(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(IN)    :: PEXNREFM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PQDM(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PTKEEDMFS(YDCPG_OPTS%KLON,YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(IN)    :: PZS(YDCPG_BNDS%KFDIA)

  REAL(KIND=JPRB), INTENT(INOUT) :: PRHODJM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTHVREFM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)  ! thetav de l etat
  REAL(KIND=JPRB), INTENT(INOUT) :: PRHODREFM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(INOUT) :: PSFTH(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PSFRV(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PSFU(YDCPG_BNDS%KFDIA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PSFV(YDCPG_BNDS%KFDIA)

  REAL(KIND=JPRB), INTENT(INOUT) :: PPABSM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PUM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PVM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PWM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTKEM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PSRCS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTHM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(INOUT) :: PRM(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)
  REAL(KIND=JPRB), INTENT(INOUT) :: PUS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PVS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PWS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTHS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PRS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_PHY_MF%YRPARAR%NRR)

  REAL(KIND=JPRB), INTENT(INOUT) :: PFLXZTHVMF_SUM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PFLXZUMF_SUM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PFLXZVMF_SUM(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PLENGTH_M(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PLENGTH_H(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(INOUT) :: PMF_UP(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDT(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG) ! temperature tendency
  REAL(KIND=JPRB), INTENT(INOUT) :: PTEND_Q(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)

  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDTKE(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG)
  REAL(KIND=JPRB), INTENT(INOUT) :: PTENDLIMA(YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(INOUT) :: PSFSV(YDCPG_BNDS%KFDIA, YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)

  REAL(KIND=JPRB), INTENT(OUT)   :: PLIMAS(YDCPG_BNDS%KFDIA, YDCPG_OPTS%KFLEVG, YDMODEL%YRML_GCONF%YGFL%NLIMA)
  REAL(KIND=JPRB), INTENT(OUT)   :: PSVS(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT)
  REAL(KIND=JPRB), INTENT(OUT)   :: PTKES_OUT(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(OUT)   :: PSIGS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB), INTENT(OUT)   :: PTENDU_TURB(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB), INTENT(OUT)   :: PTENDV_TURB(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB) :: ZTENDTHL_TURB(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB) :: ZTENDRT_TURB(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB) :: ZTENDSV_TURBLIMA(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NLIMA) ! LIMA

  ! THE DDH budgets
  REAL(KIND=JPRB) :: ZDP(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB) :: ZTP(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB) :: ZDPMF(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB) :: ZTPMF(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB) :: ZTDIFF(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB) :: ZTDISS(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)
  REAL(KIND=JPRB) :: ZEDR(YDCPG_BNDS%KFDIA, 0:YDCPG_OPTS%KFLEVG+1)

  REAL(KIND=JPRB) :: ZSFSVLIMA(YDCPG_BNDS%KFDIA, YDMODEL%YRML_GCONF%YGFL%NLIMA) ! surf. flux of LIMA vars
  REAL(KIND=JPRB) :: ZZTOP(YDCPG_BNDS%KFDIA)

  REAL(KIND=JPRB) :: ZTURB3DLEO(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1,YDMODEL%YRML_PHY_MF%YGRAD%YPTR%NDIMLEO)
  REAL(KIND=JPRB) :: ZTURB3DGOG(YDCPG_BNDS%KFDIA,0:YDCPG_OPTS%KFLEVG+1,YDMODEL%YRML_PHY_MF%YGRAD%YPTR%NDIMGOG)

  REAL(KIND=JPRB) :: ZTENDSV_TURB(YDCPG_BNDS%KFDIA,YDCPG_OPTS%KFLEVG,YDMODEL%YRML_GCONF%YGFL%NGFL_EXT) ! Array for Dust modelling

  REAL(KIND=JPRB) :: ZDT, ZINVG

  INTEGER(KIND=JPIM) :: JLON, JLEV, JGFL

  REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

  !------------------------------------------------------------------

#include "aro_turb_mnh.h"
#include "arotherdia.intfb.h"

  !------------------------------------------------------------------

  IF (LHOOK) CALL DR_HOOK('APL_AROME_TURBULENCE_MOD:APL_AROME_TURBULENCE', 0, ZHOOK_HANDLE)

  IF (YDMODEL%YRML_DYN%YRDYNA%LTWOTL) THEN
    ZDT=YDCPG_OPTS%ZDTPHY/2._JPRB
  ELSE
    IF (YDCPG_OPTS%NSTEP/=0) THEN
      ZDT=YDCPG_OPTS%ZDTPHY/2._JPRB
    ELSE
      ZDT=YDCPG_OPTS%ZDTPHY
    ENDIF
  ENDIF

  ZINVG=1._JPRB/YDCST%RG

  ! Input variable indeed. REK
  ZSFSVLIMA(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 1:YDMODEL%YRML_GCONF%YGFL%NLIMA)=0._JPRB

  ! 10.2 calcul TURB
  ZZTOP(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA) = PAPHIM(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA, 0)*ZINVG

  IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%TURBN%LLEONARD) THEN
    DO JLEV = 1,YDCPG_OPTS%KFLEVG
        ! The order is important
        ! 1 : dwdx, 2 : dwdy, 3 : drvdx, 4 : drvdy, 5 : drldx, 6 : drldy, 7 : dridx, 8 : dridy, 9 : dthetadx, 10: dthetady
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 1)=YDHGRAD%DWDXZ    (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 2)=YDHGRAD%DWDYZ    (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 3)=YDHGRAD%DRVDXZ   (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 4)=YDHGRAD%DRVDYZ   (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 5)=YDHGRAD%DRLDXZ   (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 6)=YDHGRAD%DRLDYZ   (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 7)=YDHGRAD%DRIDXZ   (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 8)=YDHGRAD%DRIDYZ   (YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV, 9)=YDHGRAD%DTHETADXZ(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DLEO(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV,10)=YDHGRAD%DTHETADYZ(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
    ENDDO
  ENDIF
  IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%TURBN%LGOGER) THEN
    DO JLEV = 1,YDCPG_OPTS%KFLEVG
        ! The order is important => 1 : dudx, 2 : dudy, 3 : dvdx, 4 : dvdy
        ZTURB3DGOG(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV,1)=YDHGRAD%DUDXZ(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DGOG(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV,2)=YDHGRAD%DUDYZ(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DGOG(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV,3)=YDHGRAD%DVDXZ(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
        ZTURB3DGOG(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV,4)=YDHGRAD%DVDYZ(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
    ENDDO
  ENDIF

  ! Appel avec les arguments modifi√©s pour variables LIMA :
  ! KSV_TURB, ZSFTURB, ZTURBM, ZTURBS, ZTENDSV_TURB
  IF (YDMODEL%YRML_PHY_MF%YRARPHY%LRDUST) THEN

    CALL ARO_TURB_MNH(PHYEX=YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & KKA=YDCPG_OPTS%KFLEVG, KKU=1, KKL=-1, &
    & KLON=YDCPG_BNDS%KFDIA, KLEV=YDCPG_OPTS%KFLEVG, &
    & KRR=YDMODEL%YRML_PHY_MF%YRPARAR%NRR, &
    & KRRL=YDMODEL%YRML_PHY_MF%YRPARAR%NRRL, &
    & KRRI=YDMODEL%YRML_PHY_MF%YRPARAR%NRRI, &
    & KSV=YDMODEL%YRML_GCONF%YGFL%NGFL_EXT, &
    & KGRADIENTSLEO=YDMODEL%YRML_PHY_MF%YGRAD%YPTR%NDIMLEO, &
    & KGRADIENTSGOG=YDMODEL%YRML_PHY_MF%YGRAD%YPTR%NDIMGOG, &
    & CMICRO=YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO, &
    & PTSTEP=ZDT, PZZ=PZZ, PZS=PZS, PZZF=PZZ_F, PZZTOP=ZZTOP, &
    & PRHODJ=PRHODJM, PTHVREF=PTHVREFM, &
    & PSFTH=PSFTH, PSFRV=PSFRV, PSFSV=PSFSV, PSFU=PSFU, PSFV=PSFV, &
    & PPABSM=PPABSM, PUM=PUM, PVM=PVM, PWM=PWM, PTKEM=PTKEM, PSVM=PSVM, PSRCM=PSRCS, &
    & PTHM=PTHM, PRM=PRM, &
    & PRUS=PUS, PRVS=PVS, PRWS=PWS, PRTHS=PTHS, PRRS=PRS,               &
    & PRSVSIN=PSVSIN, PRSVS=PSVS, PRTKES=PTKES, PRTKES_OUT=PTKES_OUT, &
    & PHGRADLEO=ZTURB3DLEO,PHGRADGOG=ZTURB3DGOG, &
    & PDELTAX=YDGEOMETRY%YREGEO%EDELX, PDELTAY=YDGEOMETRY%YREGEO%EDELY, &
    & PSIGS=PSIGS, &
    & PFLXZTHVMF=PFLXZTHVMF_SUM, PFLXZUMF=PFLXZUMF_SUM, PFLXZVMF=PFLXZVMF_SUM, &
    & PLENGTHM=PLENGTH_M, PLENGTHH=PLENGTH_H, MFMOIST=PMF_UP, &
    & PDRUS_TURB=PTENDU_TURB, PDRVS_TURB=PTENDV_TURB, &
    & PDRTHLS_TURB=ZTENDTHL_TURB, PDRRTS_TURB=ZTENDRT_TURB, PDRSVS_TURB=ZTENDSV_TURB, &
    & PDP=ZDP, PTP=ZTP, PDPMF=ZDPMF, PTPMF=ZTPMF, PTDIFF=ZTDIFF, PTDISS=ZTDISS, PEDR=ZEDR, &
    & YDDDH=YDDDH, YDLDDH=YDMODEL%YRML_DIAG%YRLDDH, YDMDDH=YDMODEL%YRML_DIAG%YRMDDH)

  ELSE

    CALL ARO_TURB_MNH(PHYEX=YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX, &
    & KKA=YDCPG_OPTS%KFLEVG, KKU=1, KKL=-1, &
    & KLON=YDCPG_BNDS%KFDIA, KLEV=YDCPG_OPTS%KFLEVG, &
    & KRR=YDMODEL%YRML_PHY_MF%YRPARAR%NRR, &
    & KRRL=YDMODEL%YRML_PHY_MF%YRPARAR%NRRL, &
    & KRRI=YDMODEL%YRML_PHY_MF%YRPARAR%NRRI, &
    & KSV=YDMODEL%YRML_GCONF%YGFL%NLIMA, &
    & KGRADIENTSLEO=YDMODEL%YRML_PHY_MF%YGRAD%YPTR%NDIMLEO, &
    & KGRADIENTSGOG=YDMODEL%YRML_PHY_MF%YGRAD%YPTR%NDIMGOG, &
    & CMICRO=YDMODEL%YRML_PHY_MF%YRPARAR%CMICRO, &
    & PTSTEP=ZDT, PZZ=PZZ, PZS=PZS, PZZF=PZZ_F, PZZTOP=ZZTOP, PRHODJ=PRHODJM, PTHVREF=PTHVREFM, &
    & PSFTH=PSFTH, PSFRV=PSFRV, PSFSV=ZSFSVLIMA, PSFU=PSFU, PSFV=PSFV, &
    & PPABSM=PPABSM, PUM=PUM, PVM=PVM, PWM=PWM, PTKEM=PTKEM, PSVM=PLIMAM, PSRCM=PSRCS, &
    & PTHM=PTHM, PRM=PRM, &
    & PRUS=PUS, PRVS=PVS, PRWS=PWS, PRTHS=PTHS, PRRS=PRS,               &
    & PRSVSIN=PLIMASIN, PRSVS=PLIMAS, PRTKES=PTKES, PRTKES_OUT=PTKES_OUT, &
    & PHGRADLEO=ZTURB3DLEO, PHGRADGOG=ZTURB3DGOG, &
    & PDELTAX=YDGEOMETRY%YREGEO%EDELX, PDELTAY=YDGEOMETRY%YREGEO%EDELY, &
    & PSIGS=PSIGS, &
    & PFLXZTHVMF=PFLXZTHVMF_SUM, PFLXZUMF=PFLXZUMF_SUM, PFLXZVMF=PFLXZVMF_SUM, &
    & PLENGTHM=PLENGTH_M, PLENGTHH=PLENGTH_H, MFMOIST=PMF_UP, &
    & PDRUS_TURB=PTENDU_TURB, PDRVS_TURB=PTENDV_TURB, &
    & PDRTHLS_TURB=ZTENDTHL_TURB, PDRRTS_TURB=ZTENDRT_TURB, PDRSVS_TURB=ZTENDSV_TURBLIMA, &
    & PDP=ZDP, PTP=ZTP, PDPMF=ZDPMF, PTPMF=ZTPMF, PTDIFF=ZTDIFF, PTDISS=ZTDISS, PEDR=ZEDR, &
    & YDDDH=YDDDH, YDLDDH=YDMODEL%YRML_DIAG%YRLDDH, YDMDDH=YDMODEL%YRML_DIAG%YRMDDH)

  ENDIF

  IF (YDXFU%LXVMAXTH) THEN
    CALL AROTHERDIA(YDMODEL%YRML_PHY_MF%YRTOPH, YDMODEL%YRML_PHY_MF%YRPARAR, &
                  & YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA, YDCPG_OPTS%KLON, YDCPG_OPTS%KFLEVG, &
                  & PFLXZTHVMF_SUM(1:YDCPG_BNDS%KFDIA, 1:YDCPG_OPTS%KFLEVG),  &
                  & PAPHIFM, YDVARS%GEOMETRY%OROG%T0, &
                  & YDMF_PHYS%OUT%CLPHMF, YDMF_PHYS%OUT%VMAXTH)
  ENDIF

  DO JLEV = 1 , YDCPG_OPTS%KFLEVG
     YDMF_PHYS%OUT%EDR(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = ZEDR(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
  ENDDO

  IF (YDMODEL%YRML_DIAG%YRLDDH%LFLEXDIA) THEN

    DO JLEV = 1,YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        ZDP(JLON,JLEV) = (ZDP(JLON,JLEV)-ZDPMF(JLON,JLEV))*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)*ZINVG
        ZTP(JLON,JLEV) = (ZTP(JLON,JLEV)-ZTPMF(JLON,JLEV))*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)*ZINVG
        ZDPMF(JLON,JLEV)  =  ZDPMF(JLON,JLEV)*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)*ZINVG
        ZTPMF(JLON,JLEV)  =  ZTPMF(JLON,JLEV)*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)*ZINVG
        ZTDIFF(JLON,JLEV) = ZTDIFF(JLON,JLEV)*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)*ZINVG
        ZTDISS(JLON,JLEV) = ZTDISS(JLON,JLEV)*YDMF_PHYS_BASE_STATE%YCPG_PHY%XYB%DELP(JLON,JLEV)*ZINVG
      ENDDO
    ENDDO
    CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZDP(:, 1:YDCPG_OPTS%KFLEVG), 'TKEPRDY', YDDDH)
    CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZTP(:, 1:YDCPG_OPTS%KFLEVG), 'TKEPRTH', YDDDH)
    CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZDPMF(:, 1:YDCPG_OPTS%KFLEVG), 'TKEPRDYMF', YDDDH)
    CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZTPMF(:, 1:YDCPG_OPTS%KFLEVG), 'TKEPRTHMF', YDDDH)
    CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZTDIFF(:, 1:YDCPG_OPTS%KFLEVG), 'TKEDIFF', YDDDH)
    CALL NEW_ADD_FIELD_3D(YDMODEL%YRML_DIAG%YRMDDH, ZTDISS(:, 1:YDCPG_OPTS%KFLEVG), 'TKEDISS', YDDDH)

  ENDIF

  ! avance temporelle et inversion niveau pour PSIGS
  IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%LSUBG_COND .AND. &
    & YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%NEBN%LSIGMAS) THEN
    IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_CLOUD == 'DIRE' .OR. &
      & YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_CLOUD == 'NONE') THEN

      DO JLEV = 1,YDCPG_OPTS%KFLEVG
        YDVARS%SRC%T1(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV) = PSIGS(YDCPG_BNDS%KIDIA:YDCPG_BNDS%KFDIA,JLEV)
      ENDDO

    ELSEIF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_CLOUD == 'STAT') THEN

      DO JLEV = 1,YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          YDVARS%SRC%T1(JLON,JLEV) = SQRT(PSIGS(JLON,JLEV)**2 + PSIGMF(JLON,JLEV)**2)
        ENDDO
      ENDDO

    ELSEIF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%PARAM_MFSHALLN%CMF_CLOUD == 'BIGA') THEN

      DO JLEV = 1,YDCPG_OPTS%KFLEVG
        DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
          YDVARS%SRC%T1(JLON,JLEV) = 0.5*SQRT((2*PSIGS(JLON,JLEV))**2 + PSIGMF(JLON,JLEV)**2)
        ENDDO
      ENDDO

    ENDIF
  ENDIF

  !10.3. traitement des sorties pour repasser dans le monde Aladin
  !calcul de tendance et inversion des niveaux pour le vent horizontal et la TKE

  DO JLEV = 1,YDCPG_OPTS%KFLEVG
    DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
      YDMF_PHYS%OUT%TENDU(JLON,JLEV) = YDMF_PHYS%OUT%TENDU(JLON,JLEV) + PTENDU_TURB(JLON,JLEV)
      YDMF_PHYS%OUT%TENDV(JLON,JLEV) = YDMF_PHYS%OUT%TENDV(JLON,JLEV) + PTENDV_TURB(JLON,JLEV)

      ! conversion de la tendance de theta en tendance de T et inversion niveau
      PTENDT(JLON,JLEV) = PTENDT(JLON,JLEV) + ZTENDTHL_TURB(JLON,JLEV)*PEXNREFM(JLON,JLEV)

      ! inversion niveaux tendances des rv et conversion en qv en multipliant par qd
      PTEND_Q(JLON,JLEV) = PTEND_Q(JLON,JLEV) + ZTENDRT_TURB(JLON,JLEV)*PQDM(JLON,JLEV)
    ENDDO
  ENDDO

  IF (YDMODEL%YRML_PHY_MF%YRPARAR%PHYEX%TURBN%LHARAT) THEN
    DO JLEV = 1,YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PTENDTKE(JLON,JLEV) = PTENDTKE(JLON,JLEV) + (PTKEEDMFS(JLON,JLEV) - PTKES(JLON,JLEV))
      ENDDO
    ENDDO
  ELSE
    DO JLEV = 1,YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA,YDCPG_BNDS%KFDIA
        PTENDTKE(JLON,JLEV) = PTENDTKE(JLON,JLEV) + (PTKES_OUT(JLON,JLEV) - PTKES(JLON,JLEV))
      ENDDO
    ENDDO
  ENDIF

  ! Tendances LIMA
  DO JGFL = 1, YDMODEL%YRML_GCONF%YGFL%NLIMA
    DO JLEV = 1, YDCPG_OPTS%KFLEVG
      DO JLON = YDCPG_BNDS%KIDIA, YDCPG_BNDS%KFDIA
        PTENDLIMA(JLON,JLEV,JGFL) = PTENDLIMA(JLON,JLEV,JGFL) + ZTENDSV_TURBLIMA(JLON,JLEV,JGFL)
      ENDDO
    ENDDO
  ENDDO

  IF (LHOOK) CALL DR_HOOK('APL_AROME_TURBULENCE_MOD:APL_AROME_TURBULENCE', 1, ZHOOK_HANDLE)

END SUBROUTINE APL_AROME_TURBULENCE
