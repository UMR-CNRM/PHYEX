PROGRAM MAIN_RAIN_ICE

USE XRD_GETOPTIONS,  ONLY: INITOPTIONS, GETOPTION, CHECKOPTIONS
USE GETDATA_RAIN_ICE_MOD, ONLY: GETDATA_RAIN_ICE
USE COMPUTE_DIFF,    ONLY: DIFF
USE MODI_RAIN_ICE
USE MODD_DIMPHYEX,   ONLY: DIMPHYEX_t
USE MODD_PHYEX,      ONLY: PHYEX_t
USE STACK_MOD,       ONLY: STACK
USE MODE_MNH_ZWORK,  ONLY: ZMNH_STACK, IMNH_BLOCK, YMNH_STACK, INUMPIN
USE OMP_LIB
USE YOMHOOK, ONLY : LHOOK, DR_HOOK, JPHOOK
#ifdef _OPENACC
USE MODD_UTIL_PHYEX_T, ONLY: COPY_PHYEX_T, WIPE_PHYEX_T
#endif

IMPLICIT NONE

INTEGER      :: KLEV
INTEGER      :: KRR

REAL,    ALLOCATABLE, DIMENSION(:,:,:,:) :: PRS, PRS_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:,:,:) :: PFPR, PFPR_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:,:,:) :: PRT   
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PDZZ     
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PRHODJ  
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PRHODREF
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PEXNREF 
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PEXNREF2
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PPABSM  
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PHLC_HRC
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PHLC_HCF
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PHLI_HRI
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PHLI_HCF
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PTHT    
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PSIGS   
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PCLDFR  
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PTHS    
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PEVAP, PEVAP_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: PCIT, PCIT_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:)     :: PSEA  
REAL,    ALLOCATABLE, DIMENSION(:,:)     :: PTOWN  
REAL,    ALLOCATABLE, DIMENSION(:,:)     :: PINPRR, PINPRR_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:)     :: PINPRS, PINPRS_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:)     :: PINPRG, PINPRG_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:)     :: ZINDEP, ZINDEP_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:,:)   :: ZRAINFR, ZRAINFR_OUT
REAL,    ALLOCATABLE, DIMENSION(:,:)     :: ZINPRC, ZINPRC_OUT
LOGICAL, ALLOCATABLE, DIMENSION(:,:,:) :: LLMICRO 

INTEGER :: NPROMA, NGPBLKS, NFLEVG
INTEGER :: JLON, JLEV
INTEGER, TARGET :: IBL

TYPE(DIMPHYEX_t)         :: D, D0
TYPE(PHYEX_t)            :: PHYEX
LOGICAL                  :: LLCHECK
LOGICAL                  :: LLCHECKDIFF
LOGICAL                  :: LLDIFF
INTEGER                  :: IBLOCK1, IBLOCK2
INTEGER                  :: ISTSZ, JBLK1, JBLK2
INTEGER                  :: NTID, ITID
INTEGER                  :: JRR
REAL                     :: ZTHVREFZIKB! for electricity use only

REAL, ALLOCATABLE, TARGET :: PSTACK(:,:)
TYPE(STACK), TARGET :: YLSTACK

REAL(KIND=8) :: TS,TE
REAL(KIND=8) :: TSC, TEC, TSD, TED, ZTC, ZTD 
INTEGER :: ITIME, NTIME
INTEGER :: IRANK, ISIZE
LOGICAL :: LLVERBOSE, LLSTAT, LLBIND
REAL (KIND=JPHOOK) :: ZHOOK_HANDLE
CHARACTER(LEN=32) :: CLTEXT

CALL INITOPTIONS ()
NGPBLKS = 150
CALL GETOPTION ("--blocks", NGPBLKS)
NPROMA = 32
CALL GETOPTION ("--nproma", NPROMA)
NFLEVG = -1
CALL GETOPTION ("--nflevg", NFLEVG)
CALL GETOPTION ("--check",  LLCHECK)
CALL GETOPTION ("--checkdiff",  LLCHECKDIFF)
IBLOCK1 = 1
CALL GETOPTION ("--check-block-1", IBLOCK1)
IBLOCK2 = NGPBLKS
CALL GETOPTION ("--check-block-2", IBLOCK2)
CALL GETOPTION ("--stat", LLSTAT)
NTIME = 1
CALL GETOPTION ("--times", NTIME)
CALL GETOPTION ("--verbose", LLVERBOSE)
CALL GETOPTION ("--bind", LLBIND)
CALL CHECKOPTIONS ()

LLDIFF = .FALSE.

IRANK = 0
ISIZE = 1
IF (LLBIND) THEN
  CALL LINUX_BIND      (IRANK, ISIZE)
  CALL LINUX_BIND_DUMP (IRANK, ISIZE)
ENDIF

CALL GETDATA_RAIN_ICE (NPROMA, NGPBLKS, NFLEVG, LLMICRO, PEXNREF, PDZZ, PRHODJ, PRHODREF, &
&PEXNREF2, PPABSM, PCIT, PCLDFR, PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF, PTHT, PRT, PTHS, &
&PRS, PSIGS, PSEA, PTOWN, PCIT_OUT, PRS_OUT, ZINPRC, ZINPRC_OUT, PINPRR, PINPRR_OUT, PEVAP, PEVAP_OUT, &
&PINPRS, PINPRS_OUT, PINPRG, PINPRG_OUT, ZINDEP, ZINDEP_OUT, ZRAINFR, ZRAINFR_OUT, PFPR, PFPR_OUT, LLVERBOSE)

KLEV = SIZE (PRS, 2)
KRR  = SIZE (PRS, 3)

IF (LLVERBOSE) PRINT *, " KLEV = ", KLEV, " KRR = ", KRR

PRINT *, " NPROMA = ", NPROMA, " KLEV = ", KLEV, " NGPBLKS = ", NGPBLKS

CALL INIT_PHYEX(KRR, PHYEX)

D0%NIT  = NPROMA
D0%NIB  = 1
D0%NIE  = NPROMA
D0%NJT  = 1
D0%NJB  = 1
D0%NJE  = 1
D0%NIJT = D0%NIT * D0%NJT
D0%NIJB = 1
D0%NIJE = NPROMA
D0%NKL  = -1
D0%NKT  = KLEV
D0%NKA  = KLEV
D0%NKU  = 1
D0%NKB  = KLEV 
D0%NKE  = 1
D0%NKTB = 1
D0%NKTE = KLEV

#if defined(USE_COLCALL) && defined(_OPENACC)
ISTSZ = NPROMA * 1500 * KLEV
#else
ISTSZ = NPROMA * 200 * KLEV
#endif
ALLOCATE (PSTACK (ISTSZ, NGPBLKS))
ZMNH_STACK => PSTACK

TS = OMP_GET_WTIME ()

ZTD = 0.
ZTC = 0.
IF (PHYEX%MISC%CELEC /='NONE') THEN
CALL ABORT ! The following value of ZTHVREFZIKB must be removed from the electricity scheme or computed correctly here
ELSE
  ZTHVREFZIKB = 0. ! for electricity use only
END IF

IF (LHOOK) CALL DR_HOOK ('MAIN',0,ZHOOK_HANDLE)

DO ITIME = 1, NTIME

  TSD = OMP_GET_WTIME ()

#ifdef _OPENACC
  CALL COPY_PHYEX_T(PHYEX)
#endif

!$acc data &
!$acc      & copyin  (D0, &
!$acc      &          ZTHVREFZIKB, PEXNREF, PDZZ, PRHODJ, PRHODREF, PEXNREF2, PPABSM, PCLDFR, &
!$acc      &          PTHT, PRT, PSIGS, PSEA, PTOWN) &
!$acc      & copy    (PCIT, PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF, PTHS, PRS) &
!$acc      & copyout (ZINPRC, PINPRR, PEVAP, PINPRS, PINPRG, ZINDEP, ZRAINFR, PFPR) &
!$acc      & create  (PSTACK) 

  TSC = OMP_GET_WTIME ()

#ifdef USE_OPENMP
!$OMP PARALLEL PRIVATE (D, YLSTACK, ITID, JBLK1, JBLK2)

  NTID = OMP_GET_MAX_THREADS ()
  ITID = OMP_GET_THREAD_NUM ()
  JBLK1 = 1 +  (NGPBLKS * (ITID+0)) / NTID
  JBLK2 =      (NGPBLKS * (ITID+1)) / NTID
#else
  JBLK1 = 1
  JBLK2 = NGPBLKS
#endif

  D = D0

!$acc parallel loop gang vector present (PHYEX) private (YLSTACK, IBL, JLON, D) collapse (2)

  DO IBL = JBLK1, JBLK2

#ifdef USE_COLCALL
  DO JLON = 1, NPROMA
    D = D0
    D%NIB = JLON
    D%NIE = JLON
    D%NIJB = JLON
    D%NIJE = JLON
#endif

#ifdef USE_STACK
    !Using cray pointers, AROME mechanism
#if defined(USE_COLCALL) && defined(_OPENACC)
    !Due to the collapse (2) directive, each point must have its own stack
    YLSTACK%L = LOC (PSTACK (1, IBL)) + CEILING(ISTSZ * KIND (PSTACK) / NPROMA / 8.) * 8 * (JLON - 1)
    YLSTACK%U = YLSTACK%L + FLOOR(ISTSZ * KIND (PSTACK) / NPROMA / 8.) * 8
#else
    YLSTACK%L = LOC (PSTACK (1, IBL))
    YLSTACK%U = YLSTACK%L + ISTSZ * KIND (PSTACK)
#endif
#else
    !Using fortran indexing, Meso-NH mechanism
    YLSTACK%L = 1
    YLSTACK%U = ISTSZ
    IMNH_BLOCK => IBL
    YMNH_STACK => YLSTACK
    INUMPIN = 0
#endif
CALL RAIN_ICE (D, PHYEX%CST, PHYEX%PARAM_ICEN, PHYEX%RAIN_ICE_PARAMN, &
             & PHYEX%RAIN_ICE_DESCRN, PHYEX%ELEC_PARAM, PHYEX%ELEC_DESCR, &
             & PHYEX%MISC%TBUCONF, OELEC=PHYEX%MISC%OELEC, OSEDIM_BEARD=PHYEX%MISC%OSEDIM_BEARD, &
             & PTHVREFZIKB=ZTHVREFZIKB, PTSTEP=PHYEX%MISC%PTSTEP, &
             & KRR=PHYEX%MISC%KRR, PEXN=PEXNREF(:,:,IBL),            &
             & PDZZ=PDZZ(:,:,IBL), PRHODJ=PRHODJ(:,:,IBL), PRHODREF=PRHODREF(:,:,IBL),PEXNREF=PEXNREF2(:,:,IBL),&
             & PPABST=PPABSM(:,:,IBL), PCIT=PCIT(:,:,IBL), PCLDFR=PCLDFR(:,:,IBL),  &
             & PHLC_HRC=PHLC_HRC(:,:,IBL), PHLC_HCF=PHLC_HCF(:,:,IBL), &
             & PHLI_HRI=PHLI_HRI(:,:,IBL), PHLI_HCF=PHLI_HCF(:,:,IBL), &
             & PTHT=PTHT(:,:,IBL),PRVT=PRT(:,:,1,IBL),PRCT=PRT(:,:,2,IBL), &
             & PRRT=PRT(:,:,3,IBL), &
             & PRIT=PRT(:,:,4,IBL), PRST=PRT(:,:,5,IBL), &
             & PRGT=PRT(:,:,6,IBL),       &
             & PTHS=PTHS(:,:,IBL), PRVS=PRS(:,:,1,IBL),PRCS=PRS(:,:,2,IBL),&
             & PRRS=PRS(:,:,3,IBL),&
             & PRIS=PRS(:,:,4,IBL),PRSS= PRS(:,:,5,IBL),PRGS= PRS(:,:,6,IBL),&
             & PINPRC=ZINPRC(:,IBL),PINPRR=PINPRR(:,IBL),PEVAP3D=PEVAP(:,:,IBL),&
             & PINPRS=PINPRS(:,IBL), PINPRG=PINPRG(:,IBL), PINDEP=ZINDEP(:,IBL), PRAINFR=ZRAINFR(:,:,IBL), &
             & PSIGS=PSIGS(:,:,IBL), &
             & TBUDGETS=PHYEX%MISC%YLBUDGET, KBUDGETS=PHYEX%MISC%NBUDGET, &
             & PSEA=PSEA(:,IBL), PTOWN=PTOWN(:,IBL), PFPR=PFPR(:,:,:,IBL) &
#ifdef USE_STACK                                                                                                                    
             & , YDSTACK=YLSTACK &                                                                                                           
#endif
             &)

#ifdef USE_COLCALL
    ENDDO
#endif

  ENDDO

#ifdef USE_OPENMP
!$OMP END PARALLEL
#endif

!$acc end parallel loop

  TEC = OMP_GET_WTIME ()

!$acc end data

#ifdef _OPENACC
  CALL WIPE_PHYEX_T(PHYEX)
#endif

  TED = OMP_GET_WTIME ()

  ZTC = ZTC + (TEC - TSC)
  ZTD = ZTD + (TED - TSD)

ENDDO

IF (LHOOK) CALL DR_HOOK ('MAIN',1,ZHOOK_HANDLE)

TE = OMP_GET_WTIME()

WRITE (*,'(A,F8.2,A)') 'elapsed time : ',TE-TS,' s'
WRITE (*,'(A,F8.4,A)') '          i.e. ',1000.*(TE-TS)/(NPROMA*NGPBLKS)/NTIME,' ms/gp'

PRINT *, " ZTD = ", ZTD, ZTD / REAL (NPROMA*NGPBLKS*NTIME)
PRINT *, " ZTC = ", ZTC, ZTC / REAL (NPROMA*NGPBLKS*NTIME)

IF (LLCHECK .OR. LLSTAT .OR. LLCHECKDIFF) THEN
  DO IBL = IBLOCK1, IBLOCK2
    PRINT *, " IBL = ", IBL
    DO JRR=1, KRR
      WRITE (CLTEXT, '("PRS JRR=",I3.3)') JRR
      CALL DIFF (CLTEXT,   PRS_OUT       (:,:,JRR,IBL), PRS      (:,:,JRR,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
      IF(JRR>=2) THEN
        WRITE (CLTEXT, '("PFPR JRR=",I3.3)') JRR
        CALL DIFF (CLTEXT, PFPR_OUT      (:,:,JRR,IBL), PFPR     (:,:,JRR,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
      ENDIF
    ENDDO
    CALL DIFF ("PCIT",     PCIT_OUT      (:,:,IBL), PCIT     (:,:,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("ZINPRC",   ZINPRC_OUT    (:,IBL),   ZINPRC   (:,IBL)  , LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PINPRRRS", PINPRR_OUT    (:,IBL),   PINPRR   (:,IBL)  , LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PEVAP",    PEVAP_OUT     (:,:,IBL), PEVAP    (:,:,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PINPRS",   PINPRS_OUT    (:,IBL),   PINPRS   (:,IBL)  , LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("PINPRG",   PINPRG_OUT    (:,IBL),   PINPRG   (:,IBL)  , LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    CALL DIFF ("ZINDEP",   ZINDEP_OUT    (:,IBL),   ZINDEP   (:,IBL)  , LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
    !CALL DIFF ("ZRAINFR",  ZRAINFR_OUT   (:,:,IBL), ZRAINFR  (:,:,IBL), LLSTAT, LLCHECK, NPROMA, LLCHECKDIFF, LLDIFF)
  ENDDO
ENDIF

IF (LLCHECKDIFF) THEN
  IF (LLDIFF) THEN
    PRINT*, "THERE ARE DIFF SOMEWHERE"
  ELSE
    PRINT*, "THERE IS NO DIFF AT ALL"
  ENDIF
ENDIF

STOP

CONTAINS

SUBROUTINE INIT_PHYEX(KRR, PHYEX)

USE MODD_BUDGET, ONLY: TBUCONF_ASSOCIATE, NBUDGET_RH, TBUCONF, LBU_ENABLE, LBUDGET_U, LBUDGET_V, LBUDGET_W, LBUDGET_TH, &
                       LBUDGET_TKE, LBUDGET_RV, LBUDGET_RC, LBUDGET_RR, LBUDGET_RI, LBUDGET_RS, LBUDGET_RG, LBUDGET_RH, LBUDGET_SV
USE MODD_PHYEX, ONLY: PHYEX_t
USE MODI_INI_PHYEX, ONLY: INI_PHYEX

IMPLICIT NONE

! -----------------------------------------------------------------------
!     DUMMY VARIABLES
INTEGER,          INTENT(IN)  :: KRR
TYPE(PHYEX_t),    INTENT(OUT) :: PHYEX

!-----------------------------------------------------------------------
!    LOCAL VARIABLES
INTEGER :: IULOUT, JRR
REAL :: ZDZMIN
CHARACTER(LEN=6) :: CPROGRAM
CHARACTER(LEN=4) :: CMICRO, CSCONV, CTURB
REAL             :: PTSTEP
! -----------------------------------------------------------------------

IULOUT=20
CPROGRAM='AROME'
ZDZMIN=20.
CMICRO='ICE3'
CSCONV='NONE'
CTURB='TKEL'
PTSTEP = 25.0000000000000
PHYEX%MISC%TPFILE%NLU=0

!Default values
CALL INI_PHYEX(CPROGRAM, PHYEX%MISC%TPFILE, .TRUE., IULOUT, 0, 1, &
              &PTSTEP, ZDZMIN, &
              &CMICRO, CSCONV, CTURB, &
              &LDDEFAULTVAL=.TRUE., LDREADNAM=.FALSE., LDCHECK=.FALSE., KPRINT=0, LDINIT=.FALSE., &
              &PHYEX_OUT=PHYEX)

!Control parameters
PHYEX%MISC%PTSTEP       = 2*PTSTEP
PHYEX%MISC%KRR          = KRR

!Emulate the namelist reading
PHYEX%PARAM_ICEN%LCRIAUTI=.TRUE.
PHYEX%PARAM_ICEN%XCRIAUTI_NAM=0.2E-3
PHYEX%PARAM_ICEN%XT0CRIAUTI_NAM=-5.
PHYEX%PARAM_ICEN%XCRIAUTC_NAM=0.1E-2
PHYEX%PARAM_ICEN%LOCND2=.FALSE.
PHYEX%PARAM_ICEN%CSEDIM='STAT'
PHYEX%PARAM_ICEN%LWARM=.TRUE.
PHYEX%PARAM_ICEN%LSEDIC=.TRUE.
PHYEX%PARAM_ICEN%CSNOWRIMING='M90 '
PHYEX%PARAM_ICEN%XFRACM90=0.1 ! Fraction used for the Murakami 1990 formulation
PHYEX%PARAM_ICEN%LCONVHG=.TRUE. ! TRUE to allow the conversion from hail to graupel
PHYEX%PARAM_ICEN%LCRFLIMIT=.TRUE. !True to limit rain contact freezing to possible heat exchange
PHYEX%PARAM_ICEN%LFEEDBACKT=.TRUE. ! When .TRUE. feed back on temperature is taken into account
PHYEX%PARAM_ICEN%LEVLIMIT=.TRUE.   ! When .TRUE. water vapour pressure is limited by saturation
PHYEX%PARAM_ICEN%LNULLWETG=.TRUE.  ! When .TRUE. graupel wet growth is activated with null rate (to allow water shedding)
PHYEX%PARAM_ICEN%LWETGPOST=.TRUE.  ! When .TRUE. graupel wet growth is activated with positive temperature (to allow water shedding)
PHYEX%PARAM_ICEN%LNULLWETH=.TRUE.  ! Same as LNULLWETG but for hail
PHYEX%PARAM_ICEN%LWETHPOST=.TRUE.  ! Same as LWETGPOST but for hail
PHYEX%PARAM_ICEN%LSEDIM_AFTER=.FALSE. ! Sedimentation done after microphysics
PHYEX%PARAM_ICEN%XSPLIT_MAXCFL=0.8
PHYEX%PARAM_ICEN%LDEPOSC=.FALSE.  ! water deposition on vegetation
PHYEX%PARAM_ICEN%XVDEPOSC=0.02    ! deposition speed (2 cm.s-1)
PHYEX%PARAM_ICEN%CSUBG_RC_RR_ACCR='NONE'
PHYEX%PARAM_ICEN%CSUBG_RR_EVAP='NONE'
PHYEX%PARAM_ICEN%CSUBG_PR_PDF='SIGM'

!Param initialisation
CALL INI_PHYEX(CPROGRAM, PHYEX%MISC%TPFILE, .TRUE., IULOUT, 0, 1, &
              &PTSTEP, ZDZMIN, &
              &CMICRO, CSCONV, CTURB, &
              &LDDEFAULTVAL=.FALSE., LDREADNAM=.FALSE., LDCHECK=.TRUE., KPRINT=2, LDINIT=.TRUE., &
              &PHYEX_IN=PHYEX, PHYEX_OUT=PHYEX)

!Budgets
CALL TBUCONF_ASSOCIATE
PHYEX%MISC%NBUDGET=NBUDGET_RH
DO JRR=1, PHYEX%MISC%NBUDGET
  PHYEX%MISC%YLBUDGET(JRR)%NBUDGET=JRR
ENDDO
LBU_ENABLE=.FALSE.                                                                                                       
LBUDGET_U=.FALSE.
LBUDGET_V=.FALSE.
LBUDGET_W=.FALSE.
LBUDGET_TH=.FALSE.
LBUDGET_TKE=.FALSE.
LBUDGET_RV=.FALSE.
LBUDGET_RC=.FALSE.
LBUDGET_RR=.FALSE.
LBUDGET_RI=.FALSE.
LBUDGET_RS=.FALSE.
LBUDGET_RG=.FALSE.
LBUDGET_RH=.FALSE.
LBUDGET_SV=.FALSE.
PHYEX%MISC%TBUCONF=TBUCONF

END SUBROUTINE INIT_PHYEX

END PROGRAM

