!MNH_LIC Copyright 1995-2023 CNRS, Meteo-France and Universite Paul Sabatier
!MNH_LIC This is part of the Meso-NH software governed by the CeCILL-C licence
!MNH_LIC version 1. See LICENSE, CeCILL-C_V1-en.txt and CeCILL-C_V1-fr.txt
!MNH_LIC for details. version 1.
!-----------------------------------------------------------------
! Modifications:
!  P. Wautelet 14/04/2023: initialize ZZ_LSFACT and ZZ_LVFACT
!-----------------------------------------------------------------
MODULE MODE_ICE4_BUDGETS
IMPLICIT NONE
CONTAINS
SUBROUTINE ICE4_BUDGETS(D, PARAMI, BUCONF, KRR, &
                        PLVFACT, PLSFACT, PRHODJ, PEXNREF, &
                        PRVHENI, PBUDGETS, &
                        TBUDGETS, KBUDGETS)
!
!*       0.    DECLARATIONS
!              ------------
!
USE YOMHOOK , ONLY : LHOOK, DR_HOOK, JPHOOK

USE MODD_DIMPHYEX,   ONLY: DIMPHYEX_t
USE MODD_BUDGET,     ONLY: TBUDGETDATA, TBUDGETCONF_t, NBUDGET_TH, NBUDGET_RV, NBUDGET_RC, &
                           NBUDGET_RI, NBUDGET_RR, NBUDGET_RS, NBUDGET_RG, NBUDGET_RH
USE MODD_PARAM_ICE_n,  ONLY: PARAM_ICE_t
!
USE MODD_FIELDS_ADDRESS ! index number for prognostic (theta and mixing ratios) and budgets
!
USE MODE_BUDGET_PHY,     ONLY: BUDGET_STORE_ADD_PHY
!
!
IMPLICIT NONE
!
!*       0.1   Declarations of dummy arguments :
!
!
!
TYPE(DIMPHYEX_t),                            INTENT(IN)    :: D
TYPE(PARAM_ICE_t),                           INTENT(IN)    :: PARAMI
TYPE(TBUDGETCONF_t),                         INTENT(IN)    :: BUCONF
INTEGER,                                     INTENT(IN)    :: KRR
REAL, DIMENSION(D%NIJT, D%NKT),              INTENT(IN)    :: PLVFACT
REAL, DIMENSION(D%NIJT, D%NKT),              INTENT(IN)    :: PLSFACT
REAL, DIMENSION(D%NIJT, D%NKT),              INTENT(IN)    :: PRHODJ
REAL, DIMENSION(D%NIJT, D%NKT),              INTENT(IN)    :: PEXNREF
REAL, DIMENSION(D%NIJT, D%NKT),              INTENT(IN)    :: PRVHENI
REAL, DIMENSION(D%NIJT, D%NKT, IBUNUM-IBUNUM_EXTRA), INTENT(IN)    :: PBUDGETS
TYPE(TBUDGETDATA), DIMENSION(KBUDGETS),      INTENT(INOUT) :: TBUDGETS
INTEGER,                                     INTENT(IN)    :: KBUDGETS
!
!
!*       0.2   Declarations of local variables :
!
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
!
INTEGER :: JIJ, JK
INTEGER :: IKTB, IKTE, IKB, IIJB, IIJE
REAL,    DIMENSION(D%NIJT, D%NKT) :: ZW1 ! work array
REAL, DIMENSION(D%NIJT, D%NKT) :: ZZ_DIFF, ZZ_LVFACT, ZZ_LSFACT
!
!-------------------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('ICE4_BUDGETS', 0, ZHOOK_HANDLE)
!
IKTB=D%NKTB
IKTE=D%NKTE
IKB=D%NKB
IIJB=D%NIJB
IIJE=D%NIJE
!
IF (BUCONF%LBUDGET_TH) THEN
  ZZ_DIFF(:,:)   = 0.
  ZZ_LSFACT(:,:) = 0.
  ZZ_LVFACT(:,:) = 0.
  DO JK = IKTB, IKTE
    DO JIJ = IIJB, IIJE
      ZZ_LVFACT(JIJ, JK) = PLVFACT(JIJ, JK) / PEXNREF(JIJ, JK)
      ZZ_LSFACT(JIJ, JK) = PLSFACT(JIJ, JK) / PEXNREF(JIJ, JK)
      ZZ_DIFF(JIJ, JK) = ZZ_LSFACT(JIJ, JK) - ZZ_LVFACT(JIJ, JK)
    ENDDO
  ENDDO
END IF

ZW1(:,:) = 0.
DO JK = IKTB, IKTE
  DO JIJ = IIJB, IIJE
    ZW1(JIJ, JK) = PBUDGETS(JIJ, JK, IRVHENI_MR) + PRVHENI(JIJ, JK)
  ENDDO
ENDDO
IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'HIN',  ZW1(:, :)*ZZ_LSFACT(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RV) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RV), 'HIN', -ZW1(:, :)                *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'HIN',  ZW1(:, :)                *PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'HON',  PBUDGETS(:, :, IRCHONI)*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'HON', -PBUDGETS(:, :, IRCHONI)              *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'HON',  PBUDGETS(:, :, IRCHONI)              *PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'SFR',  PBUDGETS(:, :, IRRHONG_MR)*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'SFR', -PBUDGETS(:, :, IRRHONG_MR)              *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'SFR',  PBUDGETS(:, :, IRRHONG_MR)              *PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'DEPS',  PBUDGETS(:, :, IRVDEPS)*ZZ_LSFACT(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RV) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RV), 'DEPS', -PBUDGETS(:, :, IRVDEPS)                *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'DEPS',  PBUDGETS(:, :, IRVDEPS)                *PRHODJ(:, :))

IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'AGGS', -PBUDGETS(:, :, IRIAGGS)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'AGGS',  PBUDGETS(:, :, IRIAGGS)*PRHODJ(:, :))

IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'AUTS', -PBUDGETS(:, :, IRIAUTS)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'AUTS',  PBUDGETS(:, :, IRIAUTS)*PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'DEPG',  PBUDGETS(:, :, IRVDEPG)*ZZ_LSFACT(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RV) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RV), 'DEPG', -PBUDGETS(:, :, IRVDEPG)                *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'DEPG',  PBUDGETS(:, :, IRVDEPG)                *PRHODJ(:, :))

IF(PARAMI%LWARM) THEN
  IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'AUTO', -PBUDGETS(:, :, IRCAUTR)*PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'AUTO',  PBUDGETS(:, :, IRCAUTR)*PRHODJ(:, :))

  IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'ACCR', -PBUDGETS(:, :, IRCACCR)*PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'ACCR',  PBUDGETS(:, :, IRCACCR)*PRHODJ(:, :))

  IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'REVA', -PBUDGETS(:, :, IRREVAV)*ZZ_LVFACT(:, :)*PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RV) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RV), 'REVA',  PBUDGETS(:, :, IRREVAV)                *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'REVA', -PBUDGETS(:, :, IRREVAV)                *PRHODJ(:, :))
ENDIF

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'RIM', &
                                                &(PBUDGETS(:, :, IRCRIMSS)+PBUDGETS(:, :, IRCRIMSG))*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'RIM', &
                                                &(-PBUDGETS(:, :, IRCRIMSS)-PBUDGETS(:, :, IRCRIMSG))*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'RIM', &
                                                &(PBUDGETS(:, :, IRCRIMSS)-PBUDGETS(:, :, IRCRIMSG))*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'RIM', &
                                                &(PBUDGETS(:, :, IRCRIMSG)+PBUDGETS(:, :, IRSRIMCG))*PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'ACC', &
                                                &(PBUDGETS(:, :, IRRACCSS)+PBUDGETS(:, :, IRRACCSG))*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'ACC', &
                                                &(-PBUDGETS(:, :, IRRACCSS)-PBUDGETS(:, :, IRRACCSG))*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'ACC', &
                                                &(PBUDGETS(:, :, IRRACCSS)-PBUDGETS(:, :, IRSACCRG))*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'ACC', &
                                                &(PBUDGETS(:, :, IRRACCSG)+PBUDGETS(:, :, IRSACCRG))*PRHODJ(:, :))

IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'CMEL', -PBUDGETS(:, :, IRSMLTG)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'CMEL',  PBUDGETS(:, :, IRSMLTG)*PRHODJ(:, :))

IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'CMEL', -PBUDGETS(:, :, IRCMLTSR)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'CMEL',  PBUDGETS(:, :, IRCMLTSR)*PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'CFRZ', &
                                                &PBUDGETS(:, :, IRRCFRIG)*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'CFRZ', &
                                                &(-PBUDGETS(:, :, IRRCFRIG)+PBUDGETS(:, :, IRICFRR))*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'CFRZ', &
                                                &(-PBUDGETS(:, :, IRICFRRG)-PBUDGETS(:, :, IRICFRR))*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'CFRZ', &
                                                &(PBUDGETS(:, :, IRICFRRG)+PBUDGETS(:, :, IRRCFRIG))*PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'WETG', &
                                                &(PBUDGETS(:, :, IRCWETG)+PBUDGETS(:, :, IRRWETG))*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'WETG', -PBUDGETS(:, :, IRCWETG)    *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'WETG', -PBUDGETS(:, :, IRRWETG)    *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'WETG', -PBUDGETS(:, :, IRIWETG)    *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'WETG', -PBUDGETS(:, :, IRSWETG)    *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'WETG', &
                                                &(PBUDGETS(:, :, IRCWETG)+PBUDGETS(:, :, IRRWETG)+&
                                                 &PBUDGETS(:, :, IRIWETG)+PBUDGETS(:, :, IRSWETG))*PRHODJ(:, :))

IF(KRR==7) THEN
  IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'GHCV', -PBUDGETS(:, :, IRWETGH)*PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RH), 'GHCV',  PBUDGETS(:, :, IRWETGH)*PRHODJ(:, :))
END IF

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'DRYG', &
                                                &(PBUDGETS(:, :, IRCDRYG)+PBUDGETS(:, :, IRRDRYG) )*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'DRYG', -PBUDGETS(:, :, IRCDRYG)     *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'DRYG', -PBUDGETS(:, :, IRRDRYG)     *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'DRYG', -PBUDGETS(:, :, IRIDRYG)     *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'DRYG', -PBUDGETS(:, :, IRSDRYG)     *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'DRYG', &
                                                &(PBUDGETS(:, :, IRCDRYG)+PBUDGETS(:, :, IRRDRYG)+&
                                                 &PBUDGETS(:, :, IRIDRYG)+PBUDGETS(:, :, IRSDRYG))*PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'GMLT', -PBUDGETS(:, :, IRGMLTR)*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'GMLT',  PBUDGETS(:, :, IRGMLTR)              *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'GMLT', -PBUDGETS(:, :, IRGMLTR)              *PRHODJ(:, :))

IF(KRR==7) THEN
  IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'WETH', &
                                                  &(PBUDGETS(:, :, IRCWETH)+PBUDGETS(:, :, IRRWETH))*ZZ_DIFF(:, :)*PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'WETH', -PBUDGETS(:, :, IRCWETH)    *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'WETH', -PBUDGETS(:, :, IRRWETH)    *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'WETH', -PBUDGETS(:, :, IRIWETH)    *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'WETH', -PBUDGETS(:, :, IRSWETH)    *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'WETH', -PBUDGETS(:, :, IRGWETH)    *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RH), 'WETH', &
                                                  &(PBUDGETS(:, :, IRCWETH)+PBUDGETS(:, :, IRRWETH)+PBUDGETS(:, :, IRIWETH)+ &
                                                   &PBUDGETS(:, :, IRSWETH)+PBUDGETS(:, :, IRGWETH))*PRHODJ(:, :))

  IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'DRYH', &
                                                  &(PBUDGETS(:, :, IRCDRYH)+PBUDGETS(:, :, IRRDRYH))*ZZ_DIFF(:, :)*PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'DRYH', -PBUDGETS(:, :, IRCDRYH) *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'DRYH', -PBUDGETS(:, :, IRRDRYH) *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'DRYH', -PBUDGETS(:, :, IRIDRYH) *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RS) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RS), 'DRYH', -PBUDGETS(:, :, IRSDRYH) *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'DRYH', -PBUDGETS(:, :, IRGDRYH) *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RH), 'DRYH', &
                                                  &(PBUDGETS(:, :, IRCDRYH)+PBUDGETS(:, :, IRRDRYH)+PBUDGETS(:, :, IRIDRYH)+ &
                                                   &PBUDGETS(:, :, IRSDRYH)+PBUDGETS(:, :, IRGDRYH))*PRHODJ(:, :))

  IF (BUCONF%LBUDGET_RG) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RG), 'HGCV', -PBUDGETS(:, :, IRDRYHG)*PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RH), 'HGCV',  PBUDGETS(:, :, IRDRYHG)*PRHODJ(:, :))

  IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'HMLT', -PBUDGETS(:, :, IRHMLTR)*ZZ_DIFF(:, :)*PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RR) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RR), 'HMLT',  PBUDGETS(:, :, IRHMLTR)              *PRHODJ(:, :))
  IF (BUCONF%LBUDGET_RH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RH), 'HMLT', -PBUDGETS(:, :, IRHMLTR)              *PRHODJ(:, :))
ENDIF

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'IMLT', -PBUDGETS(:, :, IRIMLTC_MR)*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'IMLT',  PBUDGETS(:, :, IRIMLTC_MR)              *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'IMLT', -PBUDGETS(:, :, IRIMLTC_MR)              *PRHODJ(:, :))

IF (BUCONF%LBUDGET_TH) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_TH), 'BERFI',  PBUDGETS(:, :, IRCBERI)*ZZ_DIFF(:, :)*PRHODJ(:, :))
IF (BUCONF%LBUDGET_RC) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RC), 'BERFI', -PBUDGETS(:, :, IRCBERI)              *PRHODJ(:, :))
IF (BUCONF%LBUDGET_RI) CALL BUDGET_STORE_ADD_PHY(D, TBUDGETS(NBUDGET_RI), 'BERFI',  PBUDGETS(:, :, IRCBERI)              *PRHODJ(:, :))
!
IF (LHOOK) CALL DR_HOOK('ICE4_BUDGETS', 1, ZHOOK_HANDLE)
!
END SUBROUTINE ICE4_BUDGETS
!
END MODULE MODE_ICE4_BUDGETS
